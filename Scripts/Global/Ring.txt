//------------Sonic CD Ring Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Player.Value0	:	Player.Rings

sub ObjectPlayerInteraction
	if Player.State != PlayerObject_Knockback
		if Player.CollisionPlane == Object.PropertyValue
			PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
			if CheckResult == true
				Object.Type = TypeName[RingSparkle]
				Player.Rings++
				if Player.Rings > 999
					Player.Rings = 999
				end if
				if Player.Rings >= Ring.ExtraLife
					if Options.GameMode != 2
						Player.Lives++
						if Stage.PlayerListPos == 0
							PlaySfx(20, 0)
						else
							PlaySfx(21, 0)
						end if
					end if

					Ring.ExtraLife += 100

					// You can only achieve 3 extra lives via rings
					if Ring.ExtraLife > 300
						Ring.ExtraLife = 1000
					end if

					// King of the Rings achievement criteria
					if Player.Rings >= 200
						if Stage.PlayerListPos == 0
							if Stage.DebugMode == false
								SetAchievement(4, 100)
							end if
						end if
					end if
				end if
				if Ring.Pan == 0	// Left panning
					SetSfxAttributes(1, -1, -100)
					Ring.Pan = 1
				else				// Right Panning
					SetSfxAttributes(2, -1, 100)
					Ring.Pan = 0
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(Ring.Frame)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Ring]
			if Object[ArrayPos0].PropertyValue == 0
				Object[ArrayPos0].DrawOrder = 4
			else
				Object[ArrayPos0].DrawOrder = 2
			end if
		end if
		ArrayPos0++
	loop
	SpriteFrame(-8, -8, 16, 16, 1, 1)		// #0 - Ring Frame 0
	SpriteFrame(-8, -8, 16, 16, 1, 18)		// #1 - Ring Frame 1
	SpriteFrame(-8, -8, 16, 16, 1, 35)		// #2 - Ring Frame 2
	SpriteFrame(-8, -8, 16, 16, 1, 52)		// #3 - Ring Frame 3
	SpriteFrame(-8, -8, 16, 16, 1, 69)		// #4 - Ring Frame 4
	SpriteFrame(-8, -8, 16, 16, 1, 86)		// #5 - Ring Frame 5
	SpriteFrame(-8, -8, 16, 16, 1, 103)		// #6 - Ring Frame 6
	SpriteFrame(-8, -8, 16, 16, 1, 120)		// #7 - Ring Frame 7
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
