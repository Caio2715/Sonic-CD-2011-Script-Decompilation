//---------------Sonic CD Special Ring Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Player.Value0	:	Player.Rings

// States
#alias 0	:	SPECIALRING_IDLE
#alias 1	:	SPECIALRING_BLANK

// Game Modes
#alias 1	:	GAMEMODE_MAINGAME
#alias 2	:	GAMEMODE_TIMEATTACK

// Decomp usage only, to check which behaviour of the special ring animation should be used for 2011 data files
#alias Object.Value7	:	Object.CheckVersion
#alias 1	:	UPDATED_VERSION


sub ObjectMain
	if Object.State == SPECIALRING_IDLE
		if Player.Rings > 49
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				Object[0].Type = TypeName[Blank Object]
				Object[1].Type = TypeName[Blank Object]

				Player.ControlMode = -1

				Player.Direction = FACING_RIGHT

				ResetObjectEntity(2, TypeName[Blank Object], 0, 0, 0)

				Object.DrawOrder = 4

				Object.State = SPECIALRING_BLANK

				Object.Frame = 32
#platform: Use_Standalone
				// reduce the frame amount for steam datafiles
				if Object.CheckVersion != UPDATED_VERSION
					Object.Frame -= 16
				end if
#endplatform
				StopSfx(24)
				StopSfx(25)
				PlaySfx(18, 0)
				Player.Left = false
				Player.Right = false
				
#platform: Use_Haptics
				HapticEffect(40, 0, 0, 0)
#endplatform

			end if
		end if
	end if
end sub


sub ObjectDraw
	switch Object.State
	case SPECIALRING_IDLE

		if Player.Rings > 49
			TempValue0 = Object.Frame

#platform: Use_Standalone	// 2011 Scripts
			if Object.CheckVersion != UPDATED_VERSION
				// Old Steam Animation
				TempValue0 >>= 3
				DrawSprite(TempValue0)
			else
				// Updated animation
				TempValue0 >>= 2
				if TempValue0 > 3
					Object.Direction = FACING_LEFT
				else
					Object.Direction = FACING_RIGHT
				end if
				DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
			end if

#endplatform

#platform: Use_Origins	// Origins animation, Same as 2011 mobile
			TempValue0   = Object.Frame
			TempValue0 >>= 2

			if TempValue0 > 3
				Object.Direction = FACING_LEFT
			else
				Object.Direction = FACING_RIGHT
			endif

			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
#endplatform

			Object.Frame++
			if Object.Frame > 31
				Object.Frame = 0
			endif
		end if
		break

	case SPECIALRING_BLANK
		TempValue0   = Object.Frame
		TempValue0 >>= 2

#platform: Use_Standalone	// 2011 Scripts

		if Object.CheckVersion != UPDATED_VERSION
			// Old Steam vanish animation
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
			Object.Frame++
			CheckGreater(Object.Frame, 31)
			if CheckResult == true
				Object.Direction = FACING_LEFT
			end if

			if Object.Frame > 47
				ResetObjectEntity(Object.EntityNo, TypeName[Blank Object], 0, 0, 0)
			end if
		else
			// Updated vanish animation
			if TempValue0 > 11
				Object.Direction = FACING_LEFT
			else
				Object.Direction = FACING_RIGHT
			end if

			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)

			CheckEqual(TempValue0,11)
			if CheckResult == true
				Object.Direction = FACING_LEFT
					DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
			else
				if TempValue0 == 15
					Object.Direction = FACING_LEFT
					DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
				end if
			end if

			Object.Frame++
			if Object.Frame > 63
				ResetObjectEntity(Object.EntityNo, TypeName[Blank Object], 0, 0, 0)
			end if
		end if

#endplatform

#platform: Use_Origins	// Origins animation, Same as 2011 mobile

		if TempValue0 > 11
			Object.Direction = FACING_LEFT
		else
			Object.Direction = FACING_RIGHT
		endif

		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		if TempValue0 == 11
			Object.Direction = FACING_LEFT
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		else
			if TempValue0 == 15
				Object.Direction = FACING_LEFT
				DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
			endif
		endif

		Object.Frame++
		if Object.Frame > 63
			ResetObjectEntity(Object.EntityNo, TypeName[BlankObject], 0, 0, 0)
		endif
#endplatform
		break
	endswitch
end sub


sub ObjectStartup
	if Stage.PlayerListPos == 0	// PlayerName[SONIC]
		LoadSpriteSheet("Global/Items2.gif")
	else
		LoadSpriteSheet("Global/Items2_t.gif")
	end if

#platform: Use_Origins
	// Special Ring Frames
	SpriteFrame(-32, -32, 64, 64, 83, 1)	// #0  - Special Ring Frame 0
	SpriteFrame(-28, -32, 56, 64, 199, 131)	// #1  - Special Ring Frame 1
	SpriteFrame(-24, -32, 48, 64, 148, 1)	// #2  - Special Ring Frame 2
	SpriteFrame(-16, -32, 32, 64, 223, 66)	// #3  - Special Ring Frame 3
	SpriteFrame(-12, -32, 24, 64, 197, 1)	// #4  - Special Ring Frame 4
	SpriteFrame(-16, -32, 32, 64, 223, 66)	// #5  - Special Ring Frame 5
	SpriteFrame(-24, -32, 48, 64, 148, 1)	// #6  - Special Ring Frame 6
	SpriteFrame(-28, -32, 56, 64, 199, 131)	// #7  - Special Ring Frame 7
	// Vanishing Frames
	SpriteFrame(0, -32, 32, 64, 222, 1)		// #8  - Ring Vanishing Frame 0
	SpriteFrame(-16, -32, 48, 64, 101, 66)	// #9  - Ring Vanishing Frame 1
	SpriteFrame(-24, -32, 56, 64, 150, 66)	// #10 - Ring Vanishing Frame 2
	SpriteFrame(0, -32, 32, 64, 156, 131)	// #11 - Ring Vanishing Frame 3
	SpriteFrame(-24, -32, 56, 64, 150, 66)	// #12 - Ring Vanishing Frame 4
	SpriteFrame(-16, -32, 48, 64, 101, 66)	// #13 - Ring Vanishing Frame 5
	SpriteFrame(0, -32, 32, 64, 222, 1)		// #14 - Ring Vanishing Frame 6
	SpriteFrame(-32, -32, 64, 64, 124, 131)	// #15 - Ring Vanishing Frame 7
#endplatform


#platform: Use_Decomp
	// Custom decomp check of versions
	// Checks the amount of rows Credits_Console has, a file that doesn't exist on mobile data files.
	// if it's 1, means there's no such file in the game, therefore we're using a mobile data file, and the updated animation is needed
	// otherwise, it means we are playing with a Steam/Blit datafile, and as such we need to use the old animation
	LoadTextFile(MENU_1,"Data/Game/Credits_Console.txt", false)
	GetTextInfo(CheckResult, MENU_1, 2, 0, 0)		
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[SpecialRing]
			Object[ArrayPos0].CheckVersion = CheckResult
		end if
		ArrayPos0++
	loop
#endplatform

#platform: Use_Standalone
	// only the spriteframes are the original code, the rest is custom code to ensure compatibility
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[SpecialRing]
			if Object[ArrayPos0].CheckVersion == UPDATED_VERSION
				// Special Ring Frames Mobile
				SpriteFrame(-32, -32, 64, 64, 83, 1)	// #0  - Special Ring Frame 0
				SpriteFrame(-28, -32, 56, 64, 199, 131)	// #1  - Special Ring Frame 1
				SpriteFrame(-24, -32, 48, 64, 148, 1)	// #2  - Special Ring Frame 2
				SpriteFrame(-16, -32, 32, 64, 223, 66)	// #3  - Special Ring Frame 3
				SpriteFrame(-12, -32, 24, 64, 197, 1)	// #4  - Special Ring Frame 4
				SpriteFrame(-16, -32, 32, 64, 223, 66)	// #5  - Special Ring Frame 5
				SpriteFrame(-24, -32, 48, 64, 148, 1)	// #6  - Special Ring Frame 6
				SpriteFrame(-28, -32, 56, 64, 199, 131)	// #7  - Special Ring Frame 7
				// Vanishing Frames
				SpriteFrame(0, -32, 32, 64, 222, 1)		// #8  - Ring Vanishing Frame 0
				SpriteFrame(-16, -32, 48, 64, 101, 66)	// #9  - Ring Vanishing Frame 1
				SpriteFrame(-24, -32, 56, 64, 150, 66)	// #10 - Ring Vanishing Frame 2
				SpriteFrame(0, -32, 32, 64, 156, 131)	// #11 - Ring Vanishing Frame 3
				SpriteFrame(-24, -32, 56, 64, 150, 66)	// #12 - Ring Vanishing Frame 4
				SpriteFrame(-16, -32, 48, 64, 101, 66)	// #13 - Ring Vanishing Frame 5
				SpriteFrame(0, -32, 32, 64, 222, 1)		// #14 - Ring Vanishing Frame 6
				SpriteFrame(-32, -32, 64, 64, 124, 131)	// #15 - Ring Vanishing Frame 7
			else
				// Special Ring Frames Steam
				SpriteFrame(-32, -32, 64, 64, 83, 1)	// #0  - Special Ring Frame 0
				SpriteFrame(-24, -32, 48, 64, 148, 1)	// #1  - Special Ring Frame 1
				SpriteFrame(-12, -32, 24, 64, 197, 1)	// #2  - Special Ring Frame 2
				SpriteFrame(-24, -32, 48, 64, 101, 66)	// #3  - Special Ring Frame 3
				// Vanishing Frames
				SpriteFrame(0, -32, 32, 64, 222, 1)		// #4  - Ring Vanishing Frame 0
				SpriteFrame(-16, -32, 48, 64, 150, 66)	// #5  - Ring Vanishing Frame 1
				SpriteFrame(-24, -32, 56, 64, 199, 66)	// #6  - Ring Vanishing Frame 2
				SpriteFrame(-32, -32, 64, 64, 191, 131)	// #7  - Ring Vanishing Frame 3
				SpriteFrame(-24, -32, 56, 64, 199, 66)	// #8  - Ring Vanishing Frame 4
				SpriteFrame(-16, -32, 48, 64, 150, 66)	// #9  - Ring Vanishing Frame 5
				SpriteFrame(0, -32, 32, 64, 222, 1)		// #10 - Ring Vanishing Frame 6
				SpriteFrame(-32, -32, 64, 64, 126, 131)	// #11 - Ring Vanishing Frame 7
			end if
		end if
		ArrayPos0++
	loop
#endplatform

	if SpecialStage.TimeStones >= 127	// We don't need this if all time stones are collected
		ArrayPos0 = 32
		while ArrayPos0 < 1056
			if Object[ArrayPos0].Type == TypeName[SpecialRing]
				ResetObjectEntity(ArrayPos0, TypeName[Blank Object], 0, 0, 0)
			end if
			ArrayPos0++
		loop
	end if
	
	if Options.GameMode > GAMEMODE_MAINGAME
		ArrayPos0 = 32
		while ArrayPos0 < 1056
			if Object[ArrayPos0].Type == TypeName[SpecialRing]
				ResetObjectEntity(ArrayPos0, TypeName[BlankObject], 0, 0, 0)
			endif
			ArrayPos0++
		loop
	endif

	if Engine.TrialMode == true
		ArrayPos0 = 32
		while ArrayPos0 < 1056
			if Object[ArrayPos0].Type == TypeName[SpecialRing]
				ResetObjectEntity(ArrayPos0, TypeName[BlankObject], 0, 0, 0)
			endif
			ArrayPos0++
		loop
	endif
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")

	SpriteFrame(-32, -32, 64, 64, 83, 1)	// #0  - Special Ring Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
