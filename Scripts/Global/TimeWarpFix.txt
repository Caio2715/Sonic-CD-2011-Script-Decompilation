//---------------Sonic CD Time Warp Fix Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.DissapearTimer

// Property Values
#alias 0	:	TIMEWARPFIX_ERASE_TOUCH		// Touch this, no more warp for you
#alias 1	:	TIMEWARPFIX_ERASE_AREA		// Get past this, no more warp for you
#alias 2	:	TIMEWARPFIX_SAVE_POINT		// tldr, sets your position to the next object after time-travelling
#alias 3	:	TIMEWARPFIX_SAVE_POINT_AIR	// tldr, sets your position to the next object after time-travelling, but in the air

// Gravity
#alias 1	:	GRAVITY_AIR

// Warp Destination
#alias 0	:	WARP_NONE


sub ObjectMain
	switch Object.PropertyValue
	case TIMEWARPFIX_ERASE_TOUCH
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Warp.Destination > WARP_NONE
				if Warp.Timer > 99
					if Warp.Timer < 220	// They forgot to fix the warp timer here
						Warp.Destination = WARP_NONE
					end if
				end if
				Warp.Timer = 0
			end if
		end if
		break

	case TIMEWARPFIX_ERASE_AREA
		if Player.XPos < Object.XPos
			Warp.Destination = WARP_NONE
			Warp.Timer = 0
		end if
		break

	case TIMEWARPFIX_SAVE_POINT
		PlayerObjectCollision(C_TOUCH, -48, -48, 48, 48)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			Player.XPos = Object[+1].XPos
			Player.YPos = Object[+1].YPos
		end if
		
		Object.DissapearTimer++
		if Object.DissapearTimer == 2
			Object.Type = TypeName[Blank Object]
		end if
		break

	case TIMEWARPFIX_SAVE_POINT_AIR
		PlayerObjectCollision(C_TOUCH, -96, -96, 96, 96)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			Player.XPos = Object[+1].XPos
			Player.YPos = Object[+1].YPos
			Player.Gravity = GRAVITY_AIR
		end if
		
		Object.DissapearTimer++
		if Object.DissapearTimer == 2
			Object.Type = TypeName[Blank Object]
		end if
		break
		
	end switch
end sub


sub ObjectPlayerInteraction

	// Check some interaction with the Player... but don't do anything with it
	PlayerObjectCollision(C_TOUCH, -14, -15, 14, 16)
	
	// All the actual interaction is handled in ObjectMain instead, which is certainly quite the risky business..
	
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			CheckResult &= 3
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			CheckResult &= 3
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 3
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 3
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(0)

	if Editor.ShowGizmos == true
		switch Object.PropertyValue
		case TIMEWARPFIX_ERASE_TOUCH
			// Draw the object's hitbox
			// (Initial idea was to tile T icons instead, but for a size of 32x32, it didn't look too good)
			TempValue0 = Object.iXPos
			TempValue1 = Object.iYPos
			TempValue0 -= 16
			TempValue1 -= 16
			DrawRectOutline(TempValue0, TempValue1, 32, 32, 255, 255, 255, 255)
			break

		case TIMEWARPFIX_SAVE_POINT
			// Draw hitbox rectangle
			TempValue0 = Object.iXPos
			TempValue1 = Object.iYPos
			TempValue0 -= 48
			TempValue1 -= 48
			DrawRectOutline(TempValue0, TempValue1, 96, 96, 255, 255, 255, 255)
			
			// Draw an arrow to where the player will be sent
			DrawArrow(Object.XPos, Object.YPos, Object[+1].XPos, Object[+1].YPos, 255, 255, 255)
			break

		case TIMEWARPFIX_SAVE_POINT_AIR
			// Draw hitbox rectangle
			TempValue0 = Object.iXPos
			TempValue1 = Object.iYPos
			TempValue0 -= 96
			TempValue1 -= 96
			DrawRectOutline(TempValue0, TempValue1, 192, 192, 255, 255, 255, 255)
			
			// Draw an arrow to where the player will be sent
			DrawArrow(Object.XPos, Object.YPos, Object[+1].XPos, Object[+1].YPos, 255, 255, 0)
			break
			
		end switch
	end if
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67) // "T" (rigger) Icon


	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Erase Time Warp on Touch", 0)
	AddEnumVariable("Erase Time Warp on Pass", 1)
	AddEnumVariable("Set Time Warp Pos (Ground)", 2)
	AddEnumVariable("Set Time Warp Pos (Air)", 3)
end sub
