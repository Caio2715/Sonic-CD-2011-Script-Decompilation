//------------Sonic CD Time Warp Fix Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.DissapearTimer

// Property Values
#alias 0	:	TIMEWARPFIX_ERASE_TOUCH		// Touch this, no more warp for you
#alias 1	:	TIMEWARPFIX_ERASE_AREA		// Get past this, no more warp for you
#alias 2	:	TIMEWARPFIX_SAVE_POINT		// tldr, sets your position to the next object after time-travelling
#alias 3	:	TIMEWARPFIX_SAVE_POINT_AIR	// tldr, sets your position to the next object after time-travelling, but in the air

// Gravity
#alias 1	:	GRAVITY_AIR


sub ObjectMain
	switch Object.PropertyValue
	case TIMEWARPFIX_ERASE_TOUCH
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Warp.Destination > 0
				if Warp.Timer > 99
					if Warp.Timer < 220
						Warp.Destination = 0
					end if
				end if
				Warp.Timer = 0
			end if
		end if
		break

	case TIMEWARPFIX_ERASE_AREA
		if Player.XPos < Object.XPos
			Warp.Destination = 0
			Warp.Timer = 0
		end if
		break

	case TIMEWARPFIX_SAVE_POINT
		PlayerObjectCollision(C_TOUCH, -48, -48, 48, 48)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			Player.XPos = Object[+1].XPos
			Player.YPos = Object[+1].YPos
		end if
		
		Object.DissapearTimer++
		if Object.DissapearTimer == 2
			Object.Type = TypeName[Blank Object]
		end if
		break

	case TIMEWARPFIX_SAVE_POINT_AIR
		PlayerObjectCollision(C_TOUCH, -96, -96, 96, 96)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			Player.XPos = Object[+1].XPos
			Player.YPos = Object[+1].YPos
			Player.Gravity = GRAVITY_AIR
		end if
		
		Object.DissapearTimer++
		if Object.DissapearTimer == 2
			Object.Type = TypeName[Blank Object]
		end if
		break
		
	end switch
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -14, -15, 14, 16)
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = Object.PropertyValue
			checkResult &= 3
			break
		case 0 // type
			checkResult = Object.PropertyValue
			checkResult &= 3
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			Object.propertyValue &= 3
			break
		case 0 // type
			Object.propertyValue = Editor.VariableValue
			Object.propertyValue &= 3
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(0)

	if Editor.ShowGizmos == true
		switch Object.PropertyValue
		case TIMEWARPFIX_ERASE_TOUCH
			// Draw the object's hitbox
			// (Initial idea was to tile T icons instead, but for a size of 32x32, it didn't look too good)
			TempValue0 = Object.XPos
			TempValue1 = Object.YPos
			TempValue0 -= 1048576
			TempValue1 -= 1048576
			DrawRectOutline(TempValue0, TempValue1, 2097152, 2097152, 255, 255, 255, 0)
			break

		case TIMEWARPFIX_SAVE_POINT
			// Draw hitbox rectangle
			TempValue0 = Object.XPos
			TempValue1 = Object.YPos
			TempValue0 -= 3145728
			TempValue1 -= 3145728
			DrawRectOutline(TempValue0, TempValue1, 6291456, 6291456, 255, 255, 255, 0)
			
			// Draw an arrow to where the player will be sent
			DrawArrow(Object.XPos, Object.YPos, Object[+1].XPos, Object[+1].YPos, 255, 255, 0)
			break

		case TIMEWARPFIX_SAVE_POINT_AIR
			// Draw hitbox rectangle
			TempValue0 = Object.XPos
			TempValue1 = Object.YPos
			TempValue0 -= 6291456
			TempValue1 -= 6291456
			DrawRectOutline(TempValue0, TempValue1, 12582912, 12582912, 255, 255, 255, 0)
			
			// Draw an arrow to where the player will be sent
			DrawArrow(Object.XPos, Object.YPos, Object[+1].XPos, Object[+1].YPos, 255, 255, 0)
			break
			
		end switch
	end if
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67) // "T" (Trigger) Icon


	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Erase Time Warp on Touch", 0)
	AddEnumVariable("Erase Time Warp on Pass", 1)
	AddEnumVariable("Set Time Warp Pos (Ground)", 2)
	AddEnumVariable("Set Time Warp Pos (Air)", 3)
end sub
