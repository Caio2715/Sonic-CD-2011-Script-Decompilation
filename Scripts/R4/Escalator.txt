//-----------------Sonic CD Escalator Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer
#alias Object.Value1			:	Object.OnObject
#alias Object.Value2			:	Object.Speed
#alias Object.Value2			:	Object.TimerSet

// HUD alias
#alias Object[24].PropertyValue	:	Hud.TimePeriod

// States
#alias 0						:	ESCALATOR_IDLE
#alias 1						:	ESCALATOR_ASCEND
#alias 2						:	ESCALATOR_STOPPED
#alias 3						:	ESCALATOR_RESTORE

// Time Periods
#alias 0						:	PRESENT
#alias 1						:	PAST
#alias 2						:	GOOD_FUTURE
#alias 3						:	BAD_FUTURE

// Priority
#alias 0				:	PRIORITY_ACTIVE_BOUNDS
#alias 1				:	PRIORITY_ACTIVE



sub ObjectMain
	switch Object.State
	case ESCALATOR_ASCEND
		if Object.Timer > 0
			Object.Timer--
			Object.XPos += Object.Speed
			Object.YPos -= Object.Speed
		else
			Object.State++
		end if
		break
	case ESCALATOR_STOPPED
		if Object.OutOfBounds == true
			Object.State = ESCALATOR_RESTORE
			Object.XPos -= 10485760	// 0xA00000
			Object.YPos += 10485760	// 0xA00000
		end if
		break

	case ESCALATOR_RESTORE
		if Object.OutOfBounds == true
			Object.State = ESCALATOR_IDLE
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	switch Object.State
	case ESCALATOR_IDLE
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		if CheckResult == true
			Object.State    = ESCALATOR_ASCEND
			Object.Timer    = Object.TimerSet
			Object.Priority = PRIORITY_ACTIVE

			Player.TileCollisions = false

			Object.OnObject = true
		else
			if Object.OnObject == true
				Player.TileCollisions = true
			end if

			Object.OnObject = false
		end if
		break

	case ESCALATOR_ASCEND
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		if CheckResult == true
			Player.XPos += Object.Speed

			Player.TileCollisions = false

			Object.OnObject = true

			PlayerObjectCollision(C_BOX, 20, -32, 32, -16)
			Player.Pushing = false
		else
			if Object.OnObject == true
				Player.TileCollisions = true
			end if

			Object.OnObject = false
		end if
		break

	case ESCALATOR_STOPPED
		Player.Pushing = false
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		if CheckResult == true
			Player.TileCollisions = false

			Object.OnObject = true
		else
			if Object.OnObject == true
				Player.TileCollisions = true
			end if
			Object.OnObject = false
		end if
		break

	end switch
end sub


sub ObjectDraw
	if Object.State < 3
		DrawSprite(0)
	end if
end sub


sub ObjectStartup
	switch Hud.TimePeriod
	case PRESENT
	case PAST
		LoadSpriteSheet("R4/Objects.gif")
		SpriteFrame(-16, -16, 32, 32, 130, 1)	// #0 - Escalator
		break

	case GOOD_FUTURE
		LoadSpriteSheet("R4/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 150)	// #0 - Escalator
		break

	case BAD_FUTURE
		LoadSpriteSheet("R4/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 183)	// #0 - Escalator
		break

	end switch

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Escalator]
			if Hud.TimePeriod < GOOD_FUTURE	// aka present or past
				Object[ArrayPos0].Speed = 65536		// 0x10000
				Object[ArrayPos0].TimerSet = 160
			else
				Object[ArrayPos0].Speed = 131072	// 0x20000
				Object[ArrayPos0].TimerSet = 80
			end if
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
