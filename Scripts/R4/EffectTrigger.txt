//--------------Sonic CD Effect Trigger Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Property Values

// Level
#alias 0	:	SET_BG_LAYER
#alias 1	:	SWAP_BACKGROUND
#alias 2	:	WATER_HEIGHT
#alias 13	:	WATER_HEIGHT_2	// not sure if this is used

// Pipes
#alias 4	:	PIPE_BOTTOM_ENTRY
#alias 5	:	PIPE_TOP_ENTRY
#alias 6	:	PIPE_TURN_LEFT
#alias 7	:	PIPE_TURN_RIGHT
#alias 8	:	PIPE_TURN_UP
#alias 9	:	PIPE_TURN_DOWN
#alias 10	:	UNUSED
#alias 11	:	UNUSED_2
#alias 12	:	PIPE_EXIT_PIPE


sub ObjectMain
	switch Object.PropertyValue
	case SET_BG_LAYER
		if Player.XPos < 0x2800000		// X - 640 in map coords
			Stage[0].ActiveLayer = 1	// why it does this instead of starting with this layer directly? no clue
			
			Object.Type 	= TypeName[Blank Object]
			Object[19].Type = TypeName[BGAnimation]
		end if
		break

	case SWAP_BACKGROUND
		if Player.YPos > Object.YPos
			Stage[0].ActiveLayer = 2

			Object[19].Type = TypeName[BGAnimation]
		else
			Stage[0].ActiveLayer = 1

			Object[19].Type = TypeName[BGAnimation]
		end if
		break

	case WATER_HEIGHT
		if Player.XPos > Object.XPos
			Stage.NewWaterLevel = 0x2640000	// Y - 612 in map coords
		else
			Stage.NewWaterLevel = 0x2840000	// Y - 644 in map coords
		end if
		break

	case PIPE_BOTTOM_ENTRY
		PlayerObjectCollision(C_TOUCH, -10, -16, 10, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Warp.Timer = 0
					
					Player.State = Player_State_WaterCurrent
					Player.Animation = ANI_FANROTATE

					Player.XVelocity =  0
					Player.YVelocity = -0x40000

					if Player.XPos < Object.XPos
						Player.XPos += 0x10000
					else
						Player.XPos -= 0x10000
					end if
				end if
			end if
		else
			if Player.State == Player_State_WaterCurrent
				PlayerObjectCollision(C_TOUCH, -64, -16, -32, 16)
				if CheckResult == true

#platform: Use_Origins
					Player.State	 = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State 	 = Player_State_Air
#endplatform

					Player.Animation = ANI_WALKING
				end if

				PlayerObjectCollision(C_TOUCH, 32, -16, 64, 16)
				if CheckResult == true
				
#platform: Use_Origins
					Player.State	 = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State 	 = Player_State_Air
#endplatform

					Player.Animation = ANI_WALKING
				end if
			end if
		end if
		break

	case PIPE_TOP_ENTRY
		PlayerObjectCollision(C_TOUCH, -10, -16, 10, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
#platform: Mobile
					Warp.Timer = 0
#endplatform
					Player.State 	 = Player_State_WaterCurrent
					Player.Animation = ANI_FANROTATE

					Player.XVelocity = 0
					Player.YVelocity = 0x40000

					if Player.XPos < Object.XPos
						Player.XPos += 0x10000
					else
						Player.XPos -= 0x10000
					end if
				end if
			end if
		else
			if Player.State == Player_State_WaterCurrent
				PlayerObjectCollision(C_TOUCH, -64, -16, -32, 16)
				if CheckResult == true
					Player.State 	 = Player_State_Ground
					Player.Animation = ANI_WALKING
				end if
				
				PlayerObjectCollision(C_TOUCH, 32, -16, 64, 16)
				if CheckResult == true
					Player.State 	 = Player_State_Ground
					Player.Animation = ANI_WALKING
				end if
			end if
		end if
		break

	case PIPE_TURN_LEFT
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.XVelocity = -0x40000
					Player.Animation = ANI_FANROTATE
				end if
			end if
		end if
		break

	case PIPE_TURN_RIGHT
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.XVelocity = 0x40000
					Player.Animation = ANI_FANROTATE
				end if
			end if
		end if
		break

	case PIPE_TURN_UP
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.YVelocity = -0x40000
					Player.Animation = ANI_FANROTATE
				end if
			end if
		end if
		break

	case PIPE_TURN_DOWN
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.YVelocity = 0x40000
					Player.Animation = ANI_FANROTATE
				end if
			end if
		end if
		break

	case UNUSED
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.XVelocity = 0
				end if
			end if
		end if
		break

	case UNUSED_2
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.YVelocity = 0
				end if
			end if
		end if
		break

	case PIPE_EXIT_PIPE
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			if Player.State != Player_State_Death
				if Player.State != Player_State_Drown
					Player.YVelocity = 0x40000

#platform: Use_Origins
					Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State = Player_State_Air
#endplatform

					Player.Animation = ANI_WALKING
				end if
			end if
		end if
		break

	case WATER_HEIGHT_2
		if Player.XPos > Object.XPos
			Stage.NewWaterLevel = 0x7800000	// Y - 1920 in map coords
		else
			Stage.NewWaterLevel = 0x5340000	// Y - 1332 in map coords
		end if
		break

	end switch
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult  = Object.PropertyValue
			CheckResult %= 14
			break
		case 0 // levelTrigger
			CheckResult = Object.PropertyValue
			CheckResult %= 14
			break
		case 1 // streamDir
			CheckResult = Object.PropertyValue
			CheckResult %= 14
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue  = Editor.VariableValue
			Object.PropertyValue %= 14
			break
		case 0 // levelTrigger
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue %= 14
			break
		case 1 // streamDir
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue %= 14
			break
		end switch
	end if
end sub


sub RSDKDraw
	LoadSpriteSheet("Global/Display.gif")
	TempValue6 = Object.SpriteSheet
	LoadSpriteSheet("Players/Sonic3.gif")
	TempValue7 = Object.SpriteSheet

	switch Object.PropertyValue
	case SET_BG_LAYER
	case SWAP_BACKGROUND
	case WATER_HEIGHT
	case WATER_HEIGHT_2
		Object.SpriteSheet = TempValue6
		DrawSprite(0)
		break
	case PIPE_BOTTOM_ENTRY
	case PIPE_TOP_ENTRY
	case PIPE_TURN_LEFT
	case PIPE_TURN_RIGHT
	case PIPE_TURN_UP
	case PIPE_TURN_DOWN
	case UNUSED
	case UNUSED_2
	case PIPE_EXIT_PIPE
		Object.SpriteSheet = TempValue7
		DrawSprite(1)
		break
	end switch
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	LoadSpriteSheet("Players/Sonic3.gif")
	SpriteFrame(-28, -12, 56, 24, 199, 75)		// #1 - Sonic Fan Rotate

	AddEditorVariable("levelTrigger")
	AddEditorVariable("streamDir")

	SetActiveVariable("levelTrigger")
	AddEnumVariable("Set background layer", SET_BG_LAYER)
	AddEnumVariable("Swap backgrounds", SWAP_BACKGROUND)
	AddEnumVariable("Set water height", WATER_HEIGHT)
	AddEnumVariable("Set water height (Bottom of level)", WATER_HEIGHT_2)

	SetActiveVariable("streamDir")
	AddEnumVariable("Stream entry (Bottom)", PIPE_BOTTOM_ENTRY)
	AddEnumVariable("Stream entry (Top)", PIPE_TOP_ENTRY)
	AddEnumVariable("Stream turn (Left)", PIPE_TURN_LEFT)
	AddEnumVariable("Stream turn (Right)", PIPE_TURN_RIGHT)
	AddEnumVariable("Stream turn (Up)", PIPE_TURN_UP)
	AddEnumVariable("Stream turn (Down)", PIPE_TURN_DOWN)
	AddEnumVariable("Unused", UNUSED)
	AddEnumVariable("Unused 2", UNUSED_2)
	AddEnumVariable("Exit stream", PIPE_EXIT_PIPE)
end sub
