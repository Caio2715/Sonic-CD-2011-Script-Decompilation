//----------------Sonic CD Solid Barrier Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Property Values
#alias 0	:	INACTIVE
#alias 1	:	CLOSE_CHECK
#alias 2	:	CLOSE_AUTO
#alias 3	:	OPEN_EGGMAN

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	switch Object.PropertyValue
	case CLOSE_CHECK
		PlayerObjectCollision(C_TOUCH, 112, -64, 256, 256)
		if CheckResult == true
			Object.PropertyValue = CLOSE_AUTO
		end if
		break

	case CLOSE_AUTO
		if Object.Timer < 64
			Object.Timer += 4
			Object.YPos  += 0x40000
		else
			Object.PropertyValue = INACTIVE
		end if
		break

	case OPEN_EGGMAN
		if Object.Timer < 128
			Object.Timer += 4
			Object.YPos  += 0x40000
		else
			Object.PropertyValue = INACTIVE
		end if
		break

	end switch
end sub


sub ObjectPlayerInteraction
	if Object.PropertyValue > 1
		PlayerObjectCollision(C_BOX, -64, -64, 64, 128)
	else
		PlayerObjectCollision(C_BOX, -64, -64, 64, 64)
	end if 
end sub


sub ObjectDraw
	TempValue0 = 0
	while TempValue0 < 16
		DrawSprite(TempValue0)
		TempValue0++
	loop
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue2 = 163
		TempValue3 = 1
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue2 = 1
		TempValue3 = 157
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue2 = 1
		TempValue3 = 190
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue2 = 1
		TempValue3 = 223
		break

	end switch

	TempValue0 = -64
	TempValue1 = -64
	while TempValue1 < 64
		while TempValue0 < 64
			SpriteFrame(TempValue0, TempValue1, 32, 32, TempValue2, TempValue3)
			TempValue0 += 32
		loop
		TempValue0  = -64
		TempValue1 += 32
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
