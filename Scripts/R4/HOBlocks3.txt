//------------Sonic CD Horizontal Blocks 3 Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias OBject.Value1			:	Object.XDrawPos
#alias OBject.Value2			:	Object.XOffSet
#alias OBject.Value3			:	Object.Angle
#alias OBject.Value4			:	Object.AgainstBlock

// HUD alias
#alias Object[24].PropertyValue	:	Hud.TimePeriod

// Property Values
#alias 0	:	ONE_BLOCK
#alias 1	:	TWO_BLOCKS
#alias 2	:	THREE_BLOCKS
#alias 3	:	FOUR_BLOCKS

// Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 2	:	PLAYER_COL_LWALL
#alias 3	:	PLAYER_COL_RWALL

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1
	Cos(Object.XOffSet, Object.Angle)

	switch Object.PropertyValue
	case ONE_BLOCK
		Object.XOffSet <<= 11
		break
	case TWO_BLOCKS
		Object.XOffSet <<= 12
		break
	case THREE_BLOCKS
		Object.XOffSet  *= 0x1800
		break
	case FOUR_BLOCKS
		Object.XOffSet <<= 13
		break
	end switch

	Object.XOffSet += Object.XPos
	Object.XOffSet &= 0xFFFF0000 // Truncate the value
	Object.XOffSet -= Object.XDrawPos
	Object.AgainstBlock = 0
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.XPos
	Object.XPos = Object.XDrawPos
	
	switch Object.PropertyValue
	case ONE_BLOCK
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		break
	case TWO_BLOCKS
		PlayerObjectCollision(C_BOX, -32, -16, 32, 16)
		break
	case THREE_BLOCKS
		PlayerObjectCollision(C_BOX, -48, -16, 48, 16)
		break
	case FOUR_BLOCKS
		PlayerObjectCollision(C_BOX, -64, -16, 64, 16)
		break
	end switch

	Object.XPos = TempValue0

	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.XPos += Object.XOffSet
		break

	case PLAYER_COL_LWALL
		PlayerObjectCollision(C_BOX, -160, -16, -128, 16)
		if CheckResult == PLAYER_COL_RWALL
			CallFunction(Player_Kill)
		end if
		break
	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case ONE_BLOCK
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 11
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(1, Object.XDrawPos, Object.YPos)
		break

	case TWO_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 12
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(4, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(5, Object.XDrawPos, Object.YPos)
		break

	case THREE_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos *= 0x1800
		Object.XDrawPos += Object.XPos
		Object.XDrawPos &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(0, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(1, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(2, Object.XDrawPos, Object.YPos)
		break

	case FOUR_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 13
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(3, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(4, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(5, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(6, Object.XDrawPos, Object.YPos)
		break
	end switch
end sub


sub ObjectStartup
	switch Hud.TimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-48, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue0, TempValue1)
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
