//--------------Sonic CD Horizontal Door Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
// HUD alias
#alias Object[24].PropertyValue	:	Hud.TimePeriod

// Push Button alias
#alias Object[-1].PropertyValue :	PushButton.PropertyValue

// Property Values
#alias 0	:	HORIZONTALDOOR_CLOSED
#alias 1	:	HORIZONTALDOOR_OPEN

// Push Button Property Values
#alias 1						:	PUSHBUTTON_PRESSED

sub ObjectMain
	switch Object.PropertyValue
	case HORIZONTALDOOR_CLOSED
		if Object.Timer > 0
			Object.Timer -= 524288	// 0x80000
			if Object.Direction == FACING_RIGHT
				Object.XPos += 524288	// 0x80000
			else
				Object.XPos -= 524288	// 0x80000
			end if
		end if
		if PushButton.PropertyValue == PUSHBUTTON_PRESSED
			Object.PropertyValue = HORIZONTALDOOR_OPEN
		end if
		break
		
	case HORIZONTALDOOR_OPEN
		if Object.Value0 < 8388608	// 0x800000
			Object.Value0 += 524288	// 0x80000
			if Object.Direction == FACING_RIGHT
				Object.XPos -= 524288	// 0x80000
			else
				Object.XPos += 524288	// 0x80000
			end if
		end if
		TempValue0 = Object.YPos
		TempValue0 -= 3145728	// 0x300000
		if Player.YPos < TempValue0
			Object.PropertyValue = HORIZONTALDOOR_CLOSED
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_BOX, -64, -8, 64, 8)
	if CheckResult > 0
		Player.CollisionMode = 0
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	if Hud.TimePeriod < GOOD_FUTURE	// aka past or present
		LoadSpriteSheet("R4/Objects.gif")
		SpriteFrame(-64, -8, 128, 16, 18, 69)	// Horizontal Door
	else
		LoadSpriteSheet("R4/Objects3.gif")
		SpriteFrame(-64, -8, 128, 16, 62, 211)	// Horizontal Door
	end if
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[HorizontalDoor]
			Object[ArrayPos0].Priority = 3
			Object[ArrayPos0].Direction = Object[ArrayPos0].PropertyValue
			Object[ArrayPos0].PropertyValue = 0
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
