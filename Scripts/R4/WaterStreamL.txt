//---------------Sonic CD Water Stream L Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.HitboxLeft
#alias Object.Value2	:	Object.HitboxRight

// Control Mode
#alias 0	:	CONTROL_ENABLED


sub ObjectPlayerInteraction
	// Water streams are activated through a chain, originated from a push button
	if Object[-1].PropertyValue == 1
		Object.PropertyValue = 1
		PlayerObjectCollision(C_TOUCH, Object.HitboxLeft, -32, Object.HitboxRight, 32)
		if CheckResult == true
			if Player.State != Player_State_Hurt
				if Player.State != Player_State_Death
					if Player.State != Player_State_Drown
						if Player.State != Player_State_Static
							Player.State = Player_State_WaterCurrent

							Player.Animation = ANI_FANROTATE

							if Player.Up == false
								if Player.Down == false
									if Player.YPos < Object.YPos
										Player.YPos += 0x10000
									end if
									if Player.YPos > Object.YPos
										Player.YPos -= 0x10000
									end if
								end if
							end if

							Player.XVelocity = -0x40000
							Player.Speed     = -0x40000

							if Player.WaterStream == false
								Player.YVelocity = 0
								Player.WaterStream = true
							end if
						end if
					end if
				end if
			end if
		else
			if Player.State == Player_State_WaterCurrent
				if Player.WaterStream == false

#platform: Use_Origins
					Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State = Player_State_Air
#endplatform

					Player.ControlMode = CONTROL_ENABLED
					Player.Animation   = ANI_WALKING
				end if
			end if
		end if
	end if
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[WaterStreamL]
			Object[ArrayPos0].HitboxLeft   = Object[ArrayPos0].PropertyValue
			Object[ArrayPos0].HitboxLeft <<= 3
			Object[ArrayPos0].HitboxRight  = Object[ArrayPos0].HitboxLeft
			FlipSign(Object[ArrayPos0].HitboxLeft)
			Object[ArrayPos0].PropertyValue = 0
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
