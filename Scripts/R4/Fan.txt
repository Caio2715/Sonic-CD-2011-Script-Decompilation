//--------------------Sonic CD Fan Script---------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// States
#alias 0	:	FAN_CLOSED
#alias 1	:	FAN_OPEN
#alias 2	:	FUTURE_FAN_CLOSED
#alias 3	:	FUTURE_FAN_OPEN

// Button alias
#alias Object[-1].PropertyValue	:	Button.Pushed

// HUD alias
#alias Object[24].PropertyValue	:	Hud.TimePeriod

// Time Periods
#alias 0						:	PRESENT
#alias 1						:	PAST
#alias 2						:	GOOD_FUTURE
#alias 3						:	BAD_FUTURE



sub ObjectMain
	switch Object.State
	case FAN_CLOSED
	case FUTURE_FAN_CLOSED
		if Button.Pushed == true
			Object.PropertyValue = 1
			Object.State++
		end if
		break

	case FAN_OPEN
	case FUTURE_FAN_OPEN
		if Hud.TimePeriod < 2
			Object.Frame++
			if Object.Frame > 9
				Object.Frame = 0
			end if
		else
			Object.Frame = 2
		end if
		break

	end switch
end sub


sub ObjectDraw
	switch Object.State
	case FAN_CLOSED
		DrawSprite(0)
		break

	case FAN_OPEN
		TempValue0 = Object.Frame
		TempValue0 >>= 1
		DrawSprite(TempValue0)
		break
		
	case FUTURE_FAN_CLOSED
		DrawSpriteFX(5, FX_FLIP, Object.XPos, Object.YPos)
		break

	case FUTURE_FAN_OPEN
		TempValue0 = Object.Frame
		TempValue0 >>= 1
		TempValue0 += 5
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		break

	end switch
end sub


sub ObjectStartup
	switch Hud.TimePeriod
	case PRESENT
	case PAST
		LoadSpriteSheet("R4/Objects.gif")
		SpriteFrame(-16, -8, 32, 16, 163, 35)	// #0 - Fan vertical frame 0
		SpriteFrame(-12, -8, 24, 16, 229, 1)	// #1 - Fan vertical frame 1
		SpriteFrame(-8, -8, 16, 16, 229, 18)	// #2 - Fan vertical frame 2
		SpriteFrame(-12, -8, 24, 16, 196, 35)	// #3 - Fan vertical frame 3
		SpriteFrame(-16, -8, 32, 16, 221, 35)	// #4 - Fan vertical frame 4
		SpriteFrame(-8, -16, 16, 32, 239, 136)	// #5 - Fan horizontal frame 0
		SpriteFrame(-8, -12, 16, 24, 222, 169)	// #6 - Fan horizontal frame 1
		SpriteFrame(-8, -8, 16, 16, 239, 194)	// #7 - Fan horizontal frame 2
		SpriteFrame(-8, -12, 16, 24, 239, 169)	// #8 - Fan horizontal frame 3
		SpriteFrame(-8, -16, 16, 32, 222, 136)	// #9 - Fan horizontal frame 4
		break

	case GOOD_FUTURE
		LoadSpriteSheet("R4/Objects3.gif")
		SpriteFrame(-16, -8, 32, 16, 51, 67)	// #0 - Future fan vertical closed
		SpriteFrame(-16, -8, 32, 16, 51, 84)	// #1 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 51, 84)	// #2 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 51, 84)	// #3 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 51, 84)	// #4 - Future fan vertical open
		SpriteFrame(-8, -16, 16, 32, 50, 101)	// #5 - Future fan horizontal closed
		SpriteFrame(-8, -16, 16, 32, 67, 101)	// #6 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 67, 101)	// #7 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 67, 101)	// #8 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 67, 101)	// #9 - Future fan horizontal open
		break

	case BAD_FUTURE
		LoadSpriteSheet("R4/Objects3.gif")
		SpriteFrame(-16, -8, 32, 16, 62, 228)	// #0 - Future fan vertical closed
		SpriteFrame(-16, -8, 32, 16, 94, 228)	// #1 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 94, 228)	// #2 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 94, 228)	// #3 - Future fan vertical open
		SpriteFrame(-16, -8, 32, 16, 94, 228)	// #4 - Future fan vertical open
		SpriteFrame(-8, -16, 16, 32, 76, 144)	// #5 - Future fan horizontal closed
		SpriteFrame(-8, -16, 16, 32, 93, 144)	// #6 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 93, 144)	// #7 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 93, 144)	// #8 - Future fan horizontal open
		SpriteFrame(-8, -16, 16, 32, 93, 144)	// #9 - Future fan horizontal open
		break

	end switch

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Fan]
			Object[ArrayPos0].State = Object[ArrayPos0].PropertyValue
			if Object[ArrayPos0].PropertyValue > 3
				Object[ArrayPos0].State -= 2
				Object[ArrayPos0].Direction = FACING_LEFT
			end if
			Object[ArrayPos0].PropertyValue &= 1
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
