//------------Sonic CD Button Blocks 1 Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.MinHeight
#alias Object.Value2			:	Object.YVelocity
#alias Object.Value3			:	Object.MaxHeight

// Push Button aliases
#alias Object[+1].PropertyValue	:	PushButton.PropertyValue
#alias Object[+1].XPos			:	PushButton.XPos
#alias Object[+1].YPos			:	PushButton.YPos

// HUD alias
#alias Object[24].PropertyValue	:	Hud.TimePeriod

// Gravity
#alias 0			:	GRAVITY_GROUND

// Collision
#alias 1			:	FLOOR
#alias 4			:	ROOF

// Time Periods
#alias 0			:	PROPVAL_PRESENT
#alias 1			:	PROPVAL_PAST
#alias 2			:	PROPVAL_GOOD_FUTURE
#alias 3			:	PROPVAL_BAD_FUTURE

// Push Button Property Values
#alias 1			:	PUSHBUTTON_PRESSED


sub ObjectMain
	if PushButton.PropertyValue == PUSHBUTTON_PRESSED
		if Object.YVelocity > -0x10000	// -0x10000
			Object.YVelocity -= 512		//  0x200
		end if
		Object.YPos += Object.YVelocity

		if Object.YPos < Object.MaxHeight
			Object.YPos = Object.MaxHeight
			Object.YVelocity = 0
		end if
	else
		if Object.YVelocity < 0x10000	// 0x10000
			Object.YVelocity += 512	// 0x200
		end if
		Object.YPos += Object.YVelocity

		if Object.YPos > Object.MinHeight
			Object.YPos = Object.MinHeight
			Object.YVelocity = 0
		end if
	end if

	PushButton.XPos = Object.XPos
	PushButton.XPos += 0x100000		// 0x100000

	PushButton.YPos = Object.YPos
	PushButton.YPos -= 0x480000		// 0x480000
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_BOX, -32, -1, 0, 64)
	switch CheckResult
	case FLOOR
		Player.YPos += 0x10000	// 0x10000
		break

	case ROOF
		if Player.Gravity == GRAVITY_GROUND
			CallFunction(PlayerObject_Kill)
		end if
		
	end switch

	PlayerObjectCollision(C_BOX, 0, -73, 32, 64)
	switch CheckResult
	case FLOOR
		Player.YPos += 0x10000	// 0x10000
		break

	case ROOF
		if Player.Gravity == GRAVITY_GROUND
			CallFunction(PlayerObject_Kill)
		end if
		
	end switch
end sub



sub ObjectDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
	DrawSprite(3)
	DrawSprite(4)
	DrawSprite(5)
end sub


sub ObjectStartup
	switch Hud.TimePeriod
	case PROPVAL_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case PROPVAL_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case PROPVAL_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case PROPVAL_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(0, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 32, 32, 32, TempValue0, TempValue1)

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[ButtonBlocks1]
			Object[ArrayPos0].MinHeight = Object[ArrayPos0].YPos
			Object[ArrayPos0].MaxHeight = Object[ArrayPos0].YPos
			Object[ArrayPos0].MaxHeight -= 0x800000
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
