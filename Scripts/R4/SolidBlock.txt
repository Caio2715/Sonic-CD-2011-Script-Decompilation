//----------------Sonic CD Solid Block Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// HUD aliases
#alias Object[24].PropertyValue	:	HUD.TimePeriod

// Property Values
#alias 0	:	ONE_BLOCK
#alias 1	:	ONE_BLOCK_ALT
#alias 2	:	TWO_BLOCKS_HORIZONTAL
#alias 3	:	TWO_BLOCKS_VERTICAL
#alias 4	:	FOUR_BLOCKS_CUBE
#alias 5	:	FIVE_BLOCKS_HORIZONTAL

// Time Periods
#alias 0	:	PROPVAL_PRESENT
#alias 1	:	PROPVAL_PAST
#alias 2	:	PROPVAL_GOOD_FUTURE
#alias 3	:	PROPVAL_BAD_FUTURE


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case ONE_BLOCK
	case ONE_BLOCK_ALT
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		break

	case TWO_BLOCKS_HORIZONTAL
		PlayerObjectCollision(C_BOX, -32, -16, 32, 16)
		break

	case TWO_BLOCKS_VERTICAL
		PlayerObjectCollision(C_BOX, -16, -32, 16, 32)
		break

	case FOUR_BLOCKS_CUBE
		PlayerObjectCollision(C_BOX, -32, -32, 32, 32)
		break

	case FIVE_BLOCKS_HORIZONTAL
		PlayerObjectCollision(C_BOX, -64, -16, 64, 16)
		break

	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case ONE_BLOCK
	case ONE_BLOCK_ALT
		DrawSprite(Object.PropertyValue)
		break

	case TWO_BLOCKS_HORIZONTAL
		DrawSprite(2)
		DrawSprite(3)
		break

	case TWO_BLOCKS_VERTICAL
		DrawSprite(4)
		DrawSprite(5)
		break
		
	case FOUR_BLOCKS_CUBE
		DrawSprite(6)
		DrawSprite(7)
		DrawSprite(8)
		DrawSprite(9)
		break
		
	case FIVE_BLOCKS_HORIZONTAL
		DrawSprite(2)
		DrawSprite(10)
		DrawSprite(11)
		DrawSprite(3)
		break

	end switch
end sub


sub ObjectStartup
	switch HUD.TimePeriod
	case PROPVAL_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case PROPVAL_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case PROPVAL_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case PROPVAL_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(-16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -16, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-16, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, 0, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-32, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue2, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue2, TempValue1)

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[SolidBlock]
			Object[ArrayPos0].DrawOrder = 4
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
