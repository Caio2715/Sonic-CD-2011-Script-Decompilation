//-------------Sonic CD Floating Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.OnObject
#alias Object.Value1	:	Object.Timer
#alias Object.Value2	:	Object.PlayerFloor.Y
#alias Object.Value3	:	Object.OriginPos.Y

sub ObjectMain
	if Object.OnObject == true

		if Object.Timer < 16
			Object.Timer++

			TempValue0 = Object.Timer
			TempValue0 &= 3

			if TempValue0 == 3
				Object.PlayerFloor.Y = 65536	// 0x10000
			else
				Object.PlayerFloor.Y = 0
			end if

		else
			Object.PlayerFloor.Y = 0
		end if

	else

		if Object.Timer > 0
			Object.Timer--

			TempValue0 = Object.Timer
			TempValue0 &= 3

			if TempValue0 == 3
				Object.PlayerFloor.Y = -65536	// -0x10000
			else
				Object.PlayerFloor.Y = 0
			end if
		else
			Object.PlayerFloor.Y = 0
			Object.YPos = Object.OriginPos.Y
		end if

	end if
	Object.OnObject = false
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_PLATFORM, -32, -17, 32, 12)
	if CheckResult == true
		Object.OnObject = true
		Player.YPos += Object.PlayerFloor.Y
		Player.YPos += 65536
	end if
end sub


sub ObjectDraw
	Object.YPos += Object.PlayerFloor.Y
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 100, 1)		// #0 - Floating Platform

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[FPlatform]
			Object[ArrayPos0].OriginPos.Y = Object[ArrayPos0].YPos
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
