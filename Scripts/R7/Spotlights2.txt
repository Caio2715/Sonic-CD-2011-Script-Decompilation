//------------Sonic CD Spotlights Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases

// Position for where the checkpoint should progress to its next version
#alias Object.Value1 : Object.Checkpoint

// The colours the Spotlight should use
// Just like everything else, these colours are in RRGGBB
#alias Object.Value2 : Object.PrimaryBaseColour
#alias Object.Value3 : Object.SecondaryBaseColour
#alias Object.Value4 : Object.PrimaryDisplayColour
#alias Object.Value5 : Object.SecondaryDisplayColour

// There are several different types of spotlights that appear in the stage
#alias Object.Value0 : Object.SpotlightType

// Function declarations
#function Spotlights_UpdateCorner


function Spotlights_UpdateCorner

	// Called individually for each corner in a spotlight to update it
	// Preconditions:
	//   - ArrayPos0 is the ID of the vertex that should be changes
	//   - TempValue0 is the X multiplier for how much extra this vertex should move
	//   - TempValue1 is the Y multiplier for how much extra this vertex should move
	//      (These last two values are here since spotlights aren't uniform in width; they can be skewed to uneven dimensions as well)

	Sin(TempValue2, Object.Rotation)
	TempValue2 *= TempValue1

	Cos(TempValue3, Object.Rotation)
	TempValue3 *= TempValue0

	VertexBuffer[ArrayPos0].x = TempValue2
	VertexBuffer[ArrayPos0].x -= TempValue3
	VertexBuffer[ArrayPos0].x >>= 9
	VertexBuffer[ArrayPos0].x += Object.XPos

	Cos(TempValue2, Object.Rotation)
	TempValue2 *= TempValue1

	Sin(TempValue3, Object.Rotation)
	TempValue3 *= TempValue0

	VertexBuffer[ArrayPos0].y = TempValue2
	VertexBuffer[ArrayPos0].y += TempValue3
	VertexBuffer[ArrayPos0].y >>= 9
	VertexBuffer[ArrayPos0].y += 256

end function


sub ObjectMain

	TempValue0 = Oscillation
	TempValue0 <<= 2

	// Get some smooth arc movement values

	Sin(TempValue1, TempValue0)
	TempValue1 >>= 5
	TempValue1 += 64

	Cos(TempValue2, TempValue0)
	TempValue2 >>= 5
	TempValue2 += 64

	// Make the spotlight colours oscillate

	Object.PrimaryDisplayColour = TempValue1
	Object.PrimaryDisplayColour <<= 24
	Object.PrimaryDisplayColour += Object.PrimaryBaseColour

	Object.SecondaryDisplayColour = TempValue2
	Object.SecondaryDisplayColour <<= 24
	Object.SecondaryDisplayColour += Object.SecondaryBaseColour

	TempValue0 = Screen.XOffset
	TempValue0 += Screen.CenterX
	if TempValue0 > Object.Checkpoint
		TempValue0 -= Object.Checkpoint
		if TempValue0 > 460
			Object.Checkpoint += 920
			Object.SpotlightType++
			if Object.SpotlightType > 4
				Object.SpotlightType = 0
			end if
		end if
	else
		TempValue0 -= Object.Checkpoint
		if TempValue0 < -460
			Object.Checkpoint -= 920
			Object.SpotlightType--
			if Object.SpotlightType < 0
				Object.SpotlightType = 4
			end if
		end if
	end if

	Object.XPos = Object.Checkpoint
	Object.XPos -= Screen.XOffset
	
	switch Object.SpotlightType
	case 0
		Object.DrawOrder = 5
		3DScene.NoVertices = 8
		3DScene.NoFaces = 2
		Object.XPos -= 64
		FaceBuffer[0].Color = Object.PrimaryDisplayColour
		FaceBuffer[1].Color = Object.PrimaryDisplayColour
		TempValue0 = Oscillation
		Sin(Object.Rotation, TempValue0)
		Object.Rotation >>= 4
		Object.Rotation += 520
		ArrayPos0 = 0
		TempValue0 = -16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		Object.XPos += 128
		TempValue0 = Oscillation
		TempValue0 <<= 1
		Sin(Object.Rotation, TempValue0)
		Object.Rotation /= 10
		Object.Rotation += 512
		TempValue0 = -16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		break

	case 1
		Object.DrawOrder = 2
		3DScene.NoVertices = 4
		3DScene.NoFaces = 1
		FaceBuffer[0].Color = Object.PrimaryDisplayColour
		TempValue0 = Oscillation
		TempValue0 <<= 1
		Sin(Object.Rotation, TempValue0)
		Object.Rotation >>= 3
		Object.Rotation += 512
		ArrayPos0 = 0
		TempValue0 = -16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 16
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		break

	case 2
		Object.DrawOrder = 5
		3DScene.NoVertices = 8
		3DScene.NoFaces = 2
		FaceBuffer[0].Color = Object.SecondaryDisplayColour
		FaceBuffer[1].Color = Object.SecondaryDisplayColour
		Object.XPos -= 48
		FaceBuffer[0].Color = Object.SecondaryDisplayColour
		FaceBuffer[1].Color = Object.SecondaryDisplayColour
		TempValue0 = Oscillation
		TempValue0 <<= 1
		Sin(Object.Rotation, TempValue0)
		Object.Rotation >>= 4
		Object.Rotation += 500
		ArrayPos0 = 0
		TempValue0 = -20
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 20
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -10
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 10
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		Object.XPos += 96
		TempValue0 = Oscillation
		TempValue0 += 32
		TempValue0 <<= 1
		Sin(Object.Rotation, TempValue0)
		Object.Rotation >>= 3
		Object.Rotation += 508
		TempValue0 = -16
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 16
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 8
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		break

	case 3
		Object.DrawOrder = 5
		3DScene.NoVertices = 4
		3DScene.NoFaces = 1
		FaceBuffer[0].Color = Object.PrimaryDisplayColour
		TempValue0 = 512
		TempValue0 -= Oscillation
		TempValue0 *= 3
		Sin(Object.Rotation, TempValue0)
		Object.Rotation /= 12
		Object.Rotation += 520
		ArrayPos0 = 0
		TempValue0 = -24
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 24
		TempValue1 = -384
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -16
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 16
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		break

	case 4
		Object.DrawOrder = 2
		3DScene.NoVertices = 8
		3DScene.NoFaces = 2
		Object.XPos -= 48
		FaceBuffer[0].Color = Object.SecondaryDisplayColour
		FaceBuffer[1].Color = Object.PrimaryDisplayColour
		TempValue0 = Oscillation
		TempValue0 += 64
		TempValue0 <<= 1
		Sin(Object.Rotation, TempValue0)
		Object.Rotation /= 20
		Object.Rotation += 496
		ArrayPos0 = 0
		TempValue0 = -14
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 14
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -4
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 4
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		Object.XPos += 96
		TempValue0 = Oscillation
		TempValue0 <<= 2
		Sin(Object.Rotation, TempValue0)
		Object.Rotation >>= 4
		Object.Rotation += 520
		TempValue0 = -20
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 20
		TempValue1 = -420
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = -14
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		TempValue0 = 14
		TempValue1 = 0
		CallFunction(Spotlights_UpdateCorner)
		ArrayPos0++
		break

	end switch

end sub


sub ObjectDraw

	// The spotlights are 3d, so draw an entire 3d scene
	Draw3DScene()

end sub


sub ObjectStartup

	// Reset the 3d stuff
	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixTranslateXYZ(MAT_VIEW, 0, 0, 0)

	// Setup the first spotlight
	FaceBuffer[0].a = 0
	FaceBuffer[0].b = 1
	FaceBuffer[0].c = 2
	FaceBuffer[0].d = 3
	FaceBuffer[0].Flag = 3 // This is the FACE_COLOURED_2D flag, since spotlights are a solid colour

	// And now setup the second spotlight
	FaceBuffer[1].a = 4
	FaceBuffer[1].b = 5
	FaceBuffer[1].c = 6
	FaceBuffer[1].d = 7
	FaceBuffer[1].Flag = 3 // This is the FACE_COLOURED_2D flag as well

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Spotlights]

			// Spotlights should always be active since they're seen throughout the level
			Object[ArrayPos0].Priority = 1

			// The default draw priority for a spotlight should be above the stage & objects (but below the HUD of course)
			Object[ArrayPos0].DrawOrder = 5

			// And set up the initial starting checkpoint value as well
			Object[ArrayPos0].Checkpoint = Screen.CenterX
			Object[ArrayPos0].Checkpoint += 192
			
			// Setup the colours the spotlights will use depending on the current time zone
			switch Object[24].PropertyValue
			case 0
				// Present gets standard colours - set the primary spotlight to green
				Object[ArrayPos0].PrimaryBaseColour = 128
				Object[ArrayPos0].PrimaryBaseColour <<= 16
				TempValue0 = 224
				TempValue0 <<= 8
				Object[ArrayPos0].PrimaryBaseColour += TempValue0
				Object[ArrayPos0].PrimaryBaseColour += 128
				
				// Set the secondary spotlight to yellow
				Object[ArrayPos0].SecondaryBaseColour = 224
				Object[ArrayPos0].SecondaryBaseColour <<= 16
				TempValue0 = 224
				TempValue0 <<= 8
				Object[ArrayPos0].SecondaryBaseColour += TempValue0
				Object[ArrayPos0].SecondaryBaseColour += 0
				break

			// No case 1 since spotlights probably didn't even exist back then in the Past

			case 2
				// The Good Future gets nice and bright colours - primary spotlight is brown
				Object[ArrayPos0].PrimaryBaseColour = 192
				Object[ArrayPos0].PrimaryBaseColour <<= 16
				TempValue0 = 128
				TempValue0 <<= 8
				Object[ArrayPos0].PrimaryBaseColour += TempValue0
				Object[ArrayPos0].PrimaryBaseColour += 0

				// And the secondary spotlight is blue
				Object[ArrayPos0].SecondaryBaseColour = 0
				Object[ArrayPos0].SecondaryBaseColour <<= 16
				TempValue0 = 192
				TempValue0 <<= 8
				Object[ArrayPos0].SecondaryBaseColour += TempValue0
				Object[ArrayPos0].SecondaryBaseColour += 224
				break

			case 3
				// Bad Future gets cool and fancy colours, primary spotlight is pink
				Object[ArrayPos0].PrimaryBaseColour = 224
				Object[ArrayPos0].PrimaryBaseColour <<= 16
				TempValue0 = 64
				TempValue0 <<= 8
				Object[ArrayPos0].PrimaryBaseColour += TempValue0
				Object[ArrayPos0].PrimaryBaseColour += 128

				// And secondary spotlight is purple
				Object[ArrayPos0].SecondaryBaseColour = 224
				Object[ArrayPos0].SecondaryBaseColour <<= 16
				TempValue0 = 128
				TempValue0 <<= 8
				Object[ArrayPos0].SecondaryBaseColour += TempValue0
				Object[ArrayPos0].SecondaryBaseColour += 192
				break

			end switch

		end if

		ArrayPos0++
	loop

	Screen.XOffset = Player.XPos
	Screen.XOffset >>= 16
	Screen.XOffset -= Screen.CenterX

end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon
	
	// This object uses 3d, so there's not really any good way to show it in the editor...
	// It's normally placed far out of bounds though, so it doesn't matter too much anyways

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
