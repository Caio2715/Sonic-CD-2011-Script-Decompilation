//------------Sonic CD Kanabun Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer
#alias Object.Value1 : Object.Angle
#alias Object.Value2 : Object.StartPosX
#alias Object.Value3 : Object.StartPosY
#alias Object.Value4 : Object.Distance
#alias Object.Value5 : Object.DrawOrderEX

// States
#alias 0 : KANABUN_FLYL
#alias 1 : KANABUN_FLYR
#alias 2 : KANABUN_OFFSCREEN

// Badnik Conditions
#alias 0 : BADNIK_GOOD
#alias 1 : BADNIK_BAD


sub ObjectMain
	
	if Object.OutOfBounds == true
		Object.State = KANABUN_OFFSCREEN
		Object.Timer = 0
		Object.Angle = 256
		Object.Direction = FACING_RIGHT
		Object.XPos = Object.StartPosX
		Object.YPos = Object.StartPosY
	end if

	// The different Kanabun types have their own main update routines
	// However, their main State Switches are exactly the same

	if Object.PropertyValue == BADNIK_GOOD		
		switch Object.State
		case KANABUN_FLYL
			// This Kanabun is initially given always-active priority, in order for its OOB state to work properly
			Object.Priority = 1
			
			// Move left at a rate of 0.25 pixels per frame
			Object.XPos -= 16384
			
			Object.Timer++
			if Object.Timer == 256
				// Turn around
				
				Object.State = KANABUN_FLYR
				Object.Direction = FACING_LEFT
			end if
			break
			
		case KANABUN_FLYR
			// When entering this state, it is assumed that Object.Timer is already 256
			
			// Move right, still at a rate of 0.25 pixels per frame
			Object.XPos += 16384
			
			Object.Timer--
			if Object.Timer == 0
				// Turn back around again
				
				Object.State = KANABUN_FLYL
				Object.Direction = FACING_RIGHT
			end if
			break
			
		case KANABUN_OFFSCREEN
			// Buffer state to hold the Kanabun if it went off-screen
			if Object.OutOfBounds == true
				Object.State = KANABUN_FLYL
				
				// It's safe to give it normal priority again
				Object.Priority = 0
			end if
			break
			
		end switch
		
		// Update the Kanabun's movement angle
		Object.Angle += 4
		Object.Angle &= 511
		
		// And animate it, too
		Object.Frame = Object.AnimationTimer
		Object.Frame >>= 1
		
		Object.AnimationTimer++
		Object.AnimationTimer &= 3
	else
		switch Object.State
		case KANABUN_FLYL
			// Give the Kanabun active priority so that its offscreen state will work fine when needed
			//(or else the object will go offscreen and immediately pause, not returning to its original position)
			Object.Priority = 1
			
			// Go left at a rate of 0.25 pixels per frame
			Object.XPos -= 16384
			
			Object.Timer++
			if Object.Timer == 256
				// Turn the other way
				Object.State = KANABUN_FLYR
				Object.Direction = FACING_LEFT
			end if
			break
			
		case KANABUN_FLYR
			// Object.Timer is assumed to be 256 when going into this state
			
			// Go right at a rate of 0.25 pixels per frame
			Object.XPos += 16384
			
			Object.Timer--
			if Object.Timer == 0
				// Turn the right way again
				Object.State = KANABUN_FLYL
				Object.Direction = FACING_RIGHT
			end if
			break
			
		case KANABUN_OFFSCREEN
			// Buffer state to hold the Kanabun if it went off-screen
			if Object.OutOfBounds == true
				Object.State = KANABUN_FLYL
				
				// And give the object normal priority again, too
				Object.Priority = 0
			end if
			break

		end switch

		Object.Angle += 2
		Object.Angle &= 511

		Object.Frame = Object.AnimationTimer
		Object.Frame >>= 2
		Object.Frame += 6
		
		Object.AnimationTimer++
		Object.AnimationTimer &= 7
	end if

	// Make the Kanabun move around, vertically
	// Object.Distance is 0, 2, or 4, based on how into the background the Kanabun is
	// 0 for FG, 4 for furthest in the BG
	// These also correspond to Sprite Frames, with smaller sprites
	
	Sin(Object.YPos, Object.Angle)
	if Object.Angle < 128
		Object.DrawOrder = 5
		Object.Distance = 0
		if Object.YPos < -384
			Object.Distance = 2
		end if
		
		if Object.YPos > 384
			Object.Distance = 2
		end if
	else
		if Object.Angle > 384
			Object.DrawOrder = 5
			Object.Distance = 0
			if Object.YPos < -384
				Object.Distance = 2
			end if
			
			if Object.YPos > 384
				Object.Distance = 2
			end if
		else
			Object.DrawOrder = Object.DrawOrderEX

			Object.Distance = 4
			if Object.YPos < -384
				Object.Distance = 2
			end if

			if Object.YPos > 384
				Object.Distance = 2
			end if
		end if
	end if
	
	Object.Frame += Object.Distance
	
	Object.YPos <<= 13
	Object.YPos += Object.StartPosY
	
	CallFunction(StageSetup_CheckGoodFuture)
	
end sub


sub ObjectPlayerInteraction

	if Object.State < KANABUN_OFFSCREEN
		if Object.Distance == 0
			// If in the Foreground, then act as a normal badnik
			PlayerObjectCollision(C_TOUCH, -14, -14, 14, 14)
			if CheckResult == true
				CallFunction(PlayerObject_BadnikBreak)
			end if
		else
			// If in the BG, only interact with the player if the player is curled
			if Player.Animation == ANI_JUMPING
				PlayerObjectCollision(C_TOUCH, -14, -14, 14, 14)
				if CheckResult == true
					CallFunction(PlayerObject_BadnikBreak)
				end if
			end if
		end if
	end if

end sub


sub ObjectDraw
	if Object.State < KANABUN_OFFSCREEN
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R7/Objects.gif")
	
	// All these Sprite Frames are in sets of 2 for all the depth levels of the Kanabun
	
	// "Good" Kanabun Frames
	SpriteFrame(-16, -16, 32, 32, 1, 100)
	SpriteFrame(-16, -16, 32, 32, 1, 133)
	SpriteFrame(-12, -12, 24, 24, 34, 71)
	SpriteFrame(-12, -12, 27, 27, 62, 43)
	SpriteFrame(-12, -12, 24, 24, 59, 71)
	SpriteFrame(-12, -12, 24, 24, 84, 71)

	// "Bad" Kanabun Frames
	SpriteFrame(-16, -16, 32, 32, 1, 34)
	SpriteFrame(-16, -16, 32, 32, 1, 67)
	SpriteFrame(-12, -12, 24, 24, 34, 18)
	SpriteFrame(-12, -12, 27, 27, 34, 43)
	SpriteFrame(-12, -12, 24, 24, 59, 18)
	SpriteFrame(-12, -12, 24, 24, 84, 18)
	
	// Find all Kanabun objects in the level
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Kanabun]
		
			// Make the Kanabun start halfway across its movement cycle, right in the middle
			Object[ArrayPos0].Angle = 256

			// And store the Kanabun's starating position, not only for object resetting but also for Y movement calculations
			Object[ArrayPos0].StartPosX = Object[ArrayPos0].XPos
			Object[ArrayPos0].StartPosY = Object[ArrayPos0].YPos
			
			// And setup where the Kanabun's gonna fly back to
			if Object[ArrayPos0].PropertyValue == 2
				Object[ArrayPos0].PropertyValue = BADNIK_GOOD
				Object[ArrayPos0].DrawOrderEX = 3
			else
				Object[ArrayPos0].DrawOrderEX = 2
			end if
		end if
	
		ArrayPos0++
	loop

end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 100)
	SpriteFrame(-16, -16, 32, 32, 1, 34)
	SpriteFrame(-16, -16, 32, 32, 1, 100)

	AddEditorVariable("condition")
	SetActiveVariable("condition")
	AddEnumVariable("Good", 0)
	AddEnumVariable("Bad", 1)
	AddEnumVariable("Good (Return to high layer)", 2)
end sub
