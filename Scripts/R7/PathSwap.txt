//------------Sonic CD Path Swap Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer
#alias Object.Value7 : Object.Interacted

#alias Object.Value1 : Object.ChunkX1
#alias Object.Value3 : Object.ChunkX2
#alias Object.Value2 : Object.ChunkY1
#alias Object.Value4 : Object.ChunkY2

// Old holds the chunk ID of what's already there on the level map, New holds what it should be replaced with
#alias Object.Value5 : Object.ChunkIDOld
#alias Object.Value6 : Object.ChunkIDNew

#alias 0 : PATHSWAP_UNTOUCHED
#alias 1 : PATHSWAP_INTERACTED


sub ObjectMain

	if Object.State == PATHSWAP_INTERACTED
		if Object.Timer < 39
			Object.Timer++
		else
			Object.State = PATHSWAP_UNTOUCHED
			Object.Timer = 0
			Object.Priority = 0
		end if

		PathSwap_Frame = Object.Timer
		PathSwap_Frame /= 5
	end if

	if PathSwap_Flag == false
		TempValue0 = Object.ChunkIDOld
	else
		TempValue0 = Object.ChunkIDNew
	end if
	
	if Object.PropertyValue != 1
		SetTileLayerEntry(TempValue0, 0, Object.ChunkX1, Object.ChunkY1)
		TempValue0++
		SetTileLayerEntry(TempValue0, 0, Object.ChunkX2, Object.ChunkY1)
		TempValue0++
		SetTileLayerEntry(TempValue0, 0, Object.ChunkX1, Object.ChunkY2)
		TempValue0++
		SetTileLayerEntry(TempValue0, 0, Object.ChunkX2, Object.ChunkY2)
	end if

end sub


sub ObjectPlayerInteraction

	if Object.PropertyValue < 2
		if Object.State == PATHSWAP_UNTOUCHED
			if PathSwap_Frame == 0
				PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
				if CheckResult == true
					if Object.Interacted == false
						Object.State = PATHSWAP_INTERACTED
						Object.Priority = 1
						Object.Interacted = true

						PathSwap_Flag++
						PathSwap_Flag &= true

#platform: Use_Haptics
						// Give the player a bit of a shake to really convey the feeling that they're in a loop, y'know?
						HapticEffect(19, 0, 0, 0)
#endplatform
					end if
				else
					Object.Interacted = false
				end if
			end if
		end if
	end if
	
end sub


sub ObjectDraw
	if Object.PropertyValue < 2
		DrawSprite(PathSwap_Frame)
	end if
end sub


sub ObjectStartup

	LoadSpriteSheet("R7/Objects.gif")

	// Path Swapper Frames
	SpriteFrame(-24, -15, 48, 40, 109, 18)
	SpriteFrame(-24, -11, 48, 36, 158, 22)
	SpriteFrame(-24, -7, 48, 32, 207, 26)
	SpriteFrame(-24, -11, 48, 36, 158, 63)
	SpriteFrame(-24, -15, 48, 40, 109, 59)
	SpriteFrame(-24, -11, 48, 36, 158, 63)
	SpriteFrame(-24, -7, 48, 32, 207, 26)
	SpriteFrame(-24, -11, 48, 36, 158, 22)

	PathSwap_Flag = false
	PathSwap_Frame = 0

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Path Swap]

			// Setup chunk positions

			Object[ArrayPos0].ChunkX1 = Object[ArrayPos0].iXPos
			Object[ArrayPos0].ChunkX1 >>= 8
			Object[ArrayPos0].ChunkX1 <<= 1

			Object[ArrayPos0].ChunkX2 = Object[ArrayPos0].ChunkX1
			Object[ArrayPos0].ChunkX2++

			Object[ArrayPos0].ChunkY1 = Object[ArrayPos0].iYPos
			Object[ArrayPos0].ChunkY1 >>= 8
			Object[ArrayPos0].ChunkY1 <<= 1

			Object[ArrayPos0].ChunkY2 = Object[ArrayPos0].ChunkY1
			Object[ArrayPos0].ChunkY2++

			// Setup chunk IDs

			// Get the base chunk ID
			GetTileLayerEntry(Object[ArrayPos0].ChunkIDOld, 0, Object[ArrayPos0].ChunkX1, Object[ArrayPos0].ChunkY1)

			// The chunk's B replacement is always 4 chunks immediately after it
			Object[ArrayPos0].ChunkIDNew = Object[ArrayPos0].ChunkIDOld
			Object[ArrayPos0].ChunkIDNew += 4

		end if

		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // type
			Object.propertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	if Object.PropertyValue == 2
		// Invisible...
		Object.InkEffect = 1
	end if
	
	DrawSpriteFX(0, FX_INK, Object.XPos, Object.YPos)
	
	if Editor.ShowGizmos == true
		Editor.DrawingOverlay = true
		
		TempValue0 = Object.iXPos
		TempValue0 -= 16
		TempValue1 = Object.iYPos
		TempValue1 -= 16
		DrawRectOutline(TempValue0, TempValue1, 32, 32, 255, 0, 0, 0)
		
		Editor.DrawingOverlay = false
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects.gif")
	SpriteFrame(-24, -15, 48, 40, 109, 18)

	AddEditorVariable("type")
	SetActiveVariable("type")

	// Bit of a guess here - 
	// Subtype 0 is used just about everywhere, 2 is used in SSZ1B once, and then 2 is completely unused it seems?

	AddEnumVariable("Normal", 0)
	AddEnumVariable("Decoration", 1)
	AddEnumVariable("Invisible", 2) // Used to update chunks but without the sign being visible there
end sub
