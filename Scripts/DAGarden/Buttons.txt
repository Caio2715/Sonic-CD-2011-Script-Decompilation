//------------Sonic CD Touch Buttons Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 5: TYPE_TOUCHBUTTONS
#alias Object.Value1			: Object.ButtonPressed
#alias Object.PropertyValue		: Object.ButtonCount
// This are also Touch Buttons
#alias Object[35].PropertyValue	: Object.Button1Value
#alias Object[36].PropertyValue	: Object.Button2Value
#alias Object[37].PropertyValue	: Object.Button3Value

// Little Planet Aliases
#alias Object[32].Value1		: Object.LPZoomDepth
#alias Object[32].Value3		: Object.RotationSpeed

// Music Selector Aliases
#alias Object[33].Frame			: Object.MSFrame
#alias Object[33].XPos			: Object.MSXPos
#alias Object[33].State			: Object.MSState
#alias Object[33].DrawOrder		: Object.MSDrawOrder
#alias Object[33].Priority		: Object.MSPriority

// MenuControl Aliases
#alias Object[0].State			: Object.MCState

// Music Selector States
#alias 1	: MUSICSELECTOR_STOP_MOVE_L
#alias 2	: MUSICSELECTOR_MOVE_L
#alias 4	: MUSICSELECTOR_MOVE_R

// MenuControl States
#alias 3	: MENUCONTROL_MUSIC_CONTROLS
#alias 4	: MENUCONTROL_SET_MUSIC
#alias 6	: MENUCONTROL_EXIT

//Engine & CallBacks
#alias 2 : MESSAGE_LOSTFOCUS

sub ObjectMain
	if Object.MCState > 1
		if Object.MCState < 4
			if Engine.Message == MESSAGE_LOSTFOCUS
				Object.ButtonPressed = 0
			end if
			TempValue0 = Object.XPos
			TempValue0 >>= 16
			TempValue1 = TempValue0
			TempValue0 -= 32
			TempValue0 -= Screen.XOffset
			TempValue1 += 32
			TempValue1 -= Screen.XOffset
			CheckTouchRect(TempValue0, 0, TempValue1, 32)
			if CheckResult > -1
				Object.ButtonPressed = Object.ButtonCount
				Object.ButtonPressed++
				switch Object.ButtonCount
				case 0
					if Object.LPZoomDepth < 16384
						Object.LPZoomDepth += 8
					end if
					break
				case 1
					if Object.LPZoomDepth > 48
						Object.LPZoomDepth -= 8
					end if
					break
				case 2
					if Object.RotationSpeed < 256
						Object.RotationSpeed += 2
					end if
					break
				case 3
					if Object.RotationSpeed > -256
						Object.RotationSpeed -= 2
					end if
					break
				end switch
			else
				CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
				if CheckResult < 0
					switch Object.ButtonCount
					// Zooming
					case 0
						if Object.ButtonPressed == 1
							Object.ButtonPressed = 0
							Object.Button1Value = 1
						end if
						break

					case 1
						if Object.ButtonPressed == 2
							Object.ButtonPressed = 0
							Object.Button1Value = 0
						end if
						break
					// Rotation
					case 2
						if Object.ButtonPressed == 3
							Object.ButtonPressed = 0
						end if
						break

					case 3
						if Object.ButtonPressed == 4
							Object.ButtonPressed = 0
						end if
						break
					// Music Selector
					case 4
						if Object.ButtonPressed == 5
							Object.ButtonPressed = 0
							PlaySfx(23, 0) 	//Menu Button SFX
							Object.MSState = MUSICSELECTOR_MOVE_R
						end if
						break

					case 5
						if Object.ButtonPressed == 6
							Object.ButtonPressed = 0
							Object.MCState = MENUCONTROL_SET_MUSIC
							StopMusic()
						end if
						break

					case 6
						if Object.ButtonPressed == 7
							Object.ButtonPressed = 0
							PlaySfx(23, 0) 	//Menu Button SFX
							Object.MSState = MUSICSELECTOR_MOVE_L
						end if
						break

					case 7
						if Object.ButtonPressed == 8
							Object.ButtonPressed = 0
							PlaySfx(27, 0)	//Select SFX
							Object.MCState = MENUCONTROL_MUSIC_CONTROLS
							Object.MSXPos = Screen.XSize
							Object.MSXPos += 96
							Object.MSXPos <<= 16

							Object.MSState = MUSICSELECTOR_STOP_MOVE_L
							Object.MSDrawOrder = 5
							Object.MSPriority = 1
							Object.Button1Value = 4
							Object.Button2Value = 5
							Object.Button3Value = 6
						end if
						break
					// Exit
					case 8
						if Object.ButtonPressed == 9
							Object.ButtonPressed = 0
							StopMusic()
							PlaySfx(27, 0)	//Select SFX
							Object.MCState = MENUCONTROL_EXIT
						end if
						break
					end switch
				else	// Reset buttons
					switch Object.ButtonCount
					case 0
						if Object.ButtonPressed == 1
							Object.ButtonPressed = 0
						end if
						break
					case 1
						if Object.ButtonPressed == 2
							Object.ButtonPressed = 0
						end if
						break
					case 2
						if Object.ButtonPressed == 3
							Object.ButtonPressed = 0
						end if
						break
					case 3
						if Object.ButtonPressed == 4
							Object.ButtonPressed = 0
						end if
						break
					case 4
						if Object.ButtonPressed == 5
							Object.ButtonPressed = 0
						end if
						break
					case 5
						if Object.ButtonPressed == 6
							Object.ButtonPressed = 0
						end if
						break
					case 6
						if Object.ButtonPressed == 7
							Object.ButtonPressed = 0
						end if
						break
					case 7
						if Object.ButtonPressed == 8
							Object.ButtonPressed = 0
						end if
						break
					case 8
						if Object.ButtonPressed == 9
							Object.ButtonPressed = 0
						end if
						break
					end switch
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	TempValue0 = Object.ButtonCount
	TempValue0 <<= 1

	TempValue1 = Object.ButtonCount
	TempValue1++
	if Object.ButtonPressed == TempValue1
		TempValue0++
	end if
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("DAGarden/Objects.gif")
	SpriteFrame(-32, -16, 63, 31, 146, 577)		// #0 - Zoom Out
	SpriteFrame(-32, -16, 63, 31, 146, 609)		// #1 - Zoom Out Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 577)		// #2 - Zoom In
	SpriteFrame(-32, -16, 63, 31, 210, 609)		// #3 - Zoom In Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 577)		// #4 - Rotation Right
	SpriteFrame(-32, -16, 63, 31, 274, 609)		// #5 - Rotation Right Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 577)		// #6 - Rotation Left
	SpriteFrame(-32, -16, 63, 31, 338, 609)		// #7 - Rotation Left Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 641)		// #8 - Prior Theme
	SpriteFrame(-32, -16, 63, 31, 210, 673)		// #9 - Prior Theme Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 641)		// #10 - Play
	SpriteFrame(-32, -16, 63, 31, 274, 673)		// #11 - Play Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 641)		// #12 - Next Theme
	SpriteFrame(-32, -16, 63, 31, 338, 673)		// #13 - Next Theme Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 641)		// #14 - Music Toggle
	SpriteFrame(-32, -16, 63, 31, 402, 673)		// #15 - Music Toggle Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 577)		// #16 - Exit
	SpriteFrame(-32, -16, 63, 31, 402, 609)		// #17 - Exit Highlight
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
