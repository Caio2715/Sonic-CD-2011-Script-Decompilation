//------------Sonic CD Penguin Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.YVelocity
#alias Object.Value1 : Object.Hops

#alias 0 : PENGUIN_HOPRIGHT
#alias 1 : PENGUIN_HOPLEFT

// Time Period Aliases
#alias 2 : PERIOD_FUTURE_G


sub ObjectMain
	
	Object.YPos += Object.YVelocity
	
	// Apply a gravity of about 0.17 pixels per frame
	Object.YVelocity += 11264
	
	switch Object.State
	case PENGUIN_HOPRIGHT
		
		// Move the Penguin a pixel right
		Object.XPos += 65536
		
		// Collision is only to be checked when falling faster than 4px a frame
		if Object.YVelocity > 262144
			ObjectTileCollision(0, 0, 8, 0)
			
			if CheckResult == true
				
				// Bounce back up, at a starting rate of 5px per frame
				Object.YVelocity = -327680
				
				Object.Hops++
				if Object.Hops == 2
					// Turn around
					
					Object.Hops = 0
					Object.State = PENGUIN_HOPLEFT
					Object.Direction = FACING_LEFT
				end if
			end if
		end if
		break
		
	case PENGUIN_HOPLEFT
		
		// Move the Penguin a pixel to the left
		Object.XPos -= 65536
		
		// Only check for colliison when falling, at a rate faster than 4px downwards
		if Object.YVelocity > 262144
			ObjectTileCollision(0, 0, 8, 0)
			
			if CheckResult == true
				// Bounce right back up, 5px per frame is the starting rate
				Object.YVelocity = -327680
				
				Object.Hops++
				if Object.Hops == 2
					// Turn around
					
					Object.Hops = 0
					Object.State = PENGUIN_HOPRIGHT
					Object.Direction = FACING_RIGHT
				end if
			end if
		end if
		break
		
	end switch
	
end sub


sub ObjectDraw
	
	if MetalSonic_Destroyed == true
		if Object.YVelocity < 0
			DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		else
			DrawSpriteFX(1, FX_FLIP, Object.XPos, Object.YPos)
		end if
	else
		
		if Object[24].PropertyValue == PERIOD_FUTURE_G
			if Object.YVelocity < 0
				DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
			else
				DrawSpriteFX(1, FX_FLIP, Object.XPos, Object.YPos)
			end if
		end if
		
	end if
	
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")
	
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Penguin]
		
			// Start bouncing up, at a rate of 5px per frame
			Object[ArrayPos0].YVelocity = -327680
			
			// And start facing right, to correspond with the starting state of PENGUIN_HOPRIGHT, as well
			Object[ArrayPos0].Direction = FACING_RIGHT
			
		end if
		
		ArrayPos0++
	loop
	
	SpriteFrame(-8, -12, 16, 24, 222, 66)
	SpriteFrame(-8, -12, 16, 24, 239, 66)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects.gif")
	SpriteFrame(-8, -12, 16, 24, 222, 66)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
