//------------Sonic CD V Spin Platform Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.PlayerOffSetAngle
#alias Object.Value1	:	Object.OffSetYPos
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.Angle
#alias Object.Value4	:	Object.KickPlayer
#alias Object.Value5	:	Object.PlatAnimFrame
#alias Object.Value6	:	Object.LightAnimFrame

// Player Aliases
#alias Player.Value6	:	Player.MinRollSpeed

// Property Values
#alias 0				:	START_UPWARDS_MOVEMENT
#alias 1				:	START_DOWNWARDS_MOVEMENT

// Gravity
#alias 0				:	GRAVITY_GROUND


sub ObjectMain
	Player.Score = object.PropertyValue
	Object.Angle = Oscillation
	Object.Angle <<= 1
	if Object.PropertyValue == START_UPWARDS_MOVEMENT
		Object.Angle += 256
	end if
	Sin(Object.YVelocity, Object.Angle)
	Object.YVelocity <<= 13
	Object.YVelocity += Object.YPos
	Object.YVelocity &= -65536
	Object.YVelocity -= Object.OffSetYPos
end sub


sub ObjectPlayerInteraction
	TempValue1 = Object.YPos
	Object.YPos = Object.OffSetYPos
	PlayerObjectCollision(C_PLATFORM, -24, -12, 24, 8)
	if CheckResult == true
		Player.YPos += Object.YVelocity

		if Player.State != PlayerObject_SpinningTop
			Player.Speed = 0
			Object.KickPlayer = true
			Player.MinRollSpeed = 3072
			if Player.XPos > Object.XPos
				Player.Timer = Player.XPos
				Player.Timer -= Object.XPos
				Object.PlayerOffSetAngle = 128
			else
				Player.Timer = Object.XPos
				Player.Timer -= Player.XPos
				Object.PlayerOffSetAngle = 384
			end if
		end if

		if Player.XPos > Object.XPos
			if Player.Left == true
				Player.Timer -= 2048
			end if
			if Player.Right == true
				Player.Timer += 2048
			end if
		else
			if Player.Left == true
				Player.Timer += 2048
			end if
			if Player.Right == true
				Player.Timer -= 2048
			end if
		end if

		Player.State = PlayerObject_SpinningTop
		Player.Animation = ANI_SPINNING_TOP

		Sin(Player.XPos, Object.PlayerOffSetAngle)
		Player.XPos *= Player.Timer
		Player.XPos >>= 9
		Player.XPos += Object.XPos

		TempValue0 = Object.PlayerOffSetAngle
		TempValue0 -= 128
		TempValue0 /= 86
		Player.Frame = TempValue0

		Object.PlayerOffSetAngle += 8
		if Object.PlayerOffSetAngle > 639
			Object.PlayerOffSetAngle -= 512
		end if

		if Player.JumpPress == true
			CallFunction(PlayerObject_StartJump)

			Player.Speed = Player.XPos
			Player.Speed -= Object.XPos
			Player.Speed >>= 3

			Player.State = PlayerObject_HandleAir
			Player.Timer = 0
			PlaySfx(0, 0)
		end if
	else
		if Object.KickPlayer == true
			if Player.State == PlayerObject_SpinningTop
				if Player.Gravity == GRAVITY_GROUND
					Player.State = PlayerObject_HandleGround
					Object.KickPlayer = false
					Player.Timer = 0
				end if
			end if
		end if
	end if
	Object.YPos = TempValue1
end sub


sub ObjectDraw
	Sin(Object.OffSetYPos, Object.Angle)
	Object.OffSetYPos <<= 13
	Object.OffSetYPos += Object.YPos
	Object.OffSetYPos &= -65536

	TempValue0 = Object.PlatAnimFrame
	TempValue0 /= 10

	Object.PlatAnimFrame++
	Object.PlatAnimFrame %= 20

	DrawSpriteXY(TempValue0, Object.XPos, Object.OffSetYPos)

	TempValue0 = Object.LightAnimFrame
	TempValue0 /= 10
	TempValue0 += 2

	Object.LightAnimFrame++
	Object.LightAnimFrame %= 60

	DrawSpriteXY(TempValue0, Object.XPos, Object.OffSetYPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	SpriteFrame(-24, -16, 48, 32, 1, 84)	// #0 - Vertical Spin Platform frame 0
	SpriteFrame(-24, -16, 48, 32, 50, 84)	// #1 - Vertical Spin Platform frame 1

	SpriteFrame(-8, 16, 16, 16, 1, 117)		// #2 - Spin Platform Light frame 0
	SpriteFrame(-8, 16, 16, 16, 18, 117)	// #3 - Spin Platform Light frame 1
	SpriteFrame(-8, 16, 16, 16, 35, 117)	// #4 - Spin Platform Light frame 2
	SpriteFrame(-8, 16, 16, 16, 35, 117)	// #5 - Spin Platform Light frame 3
	SpriteFrame(-8, 16, 16, 16, 35, 117)	// #6 - Spin Platform Light frame 4
	SpriteFrame(-8, 16, 16, 16, 52, 117)	// #7 - Spin Platform Light frame 5

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[VSpinPlatform]
			Object[ArrayPos0].DrawOrder = 4
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
