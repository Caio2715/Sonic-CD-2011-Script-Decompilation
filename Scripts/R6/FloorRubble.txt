//------------Sonic CD FloorRubble Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.RubbleType
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.FallSpeed


sub ObjectMain
	Object.XPos += Object.XVelocity
	Object.YVelocity += Object.FallSpeed
	Object.YPos += Object.YVelocity

	if Object.PropertyValue == 0
		ObjectTileCollision(0, 0, 8, 0)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			CreateTempObject(TypeName[FloorRubble], 2, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos -= 0x40000
			Object[TempObjectPos].YPos -= 0x40000
			Object[TempObjectPos].XVelocity = -0x10000
			Object[TempObjectPos].YVelocity = -0x28000
			Object[TempObjectPos].FallSpeed = 0x3000

			CreateTempObject(TypeName[FloorRubble], 3, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos += 0x40000
			Object[TempObjectPos].YPos -= 0x40000
			Object[TempObjectPos].XVelocity = 0x10000
			Object[TempObjectPos].YVelocity = -0x28000
			Object[TempObjectPos].FallSpeed = 0x3000

			CreateTempObject(TypeName[FloorRubble], 4, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos -= 0x40000
			Object[TempObjectPos].YPos += 0x40000
			Object[TempObjectPos].XVelocity = -0x20000
			Object[TempObjectPos].YVelocity = -0x1C000
			Object[TempObjectPos].FallSpeed = 0x3000

			CreateTempObject(TypeName[FloorRubble], 5, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos += 0x40000
			Object[TempObjectPos].YPos += 0x40000
			Object[TempObjectPos].XVelocity = 0x20000
			Object[TempObjectPos].YVelocity = -0x1C000
			Object[TempObjectPos].FallSpeed = 0x3000
		end if
	else
		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
		end if
	end if
end sub


sub ObjectDraw
	if Object.PropertyValue == 0
		TempValue0 = Object.RubbleType
		TempValue0 /= 10
		DrawSprite(TempValue0)

		Object.RubbleType++
		Object.RubbleType %= 20
	else
		DrawSprite(Object.PropertyValue)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects2.gif")

	SpriteFrame(-8, -8, 16, 16, 165, 191)	// #0 - Big rubble
	SpriteFrame(-8, -8, 16, 16, 181, 191)	// #1 - Big rubble alt
	SpriteFrame(-4, -4, 8, 8, 165, 191)		// #2 - Rubble chunk 1
	SpriteFrame(-4, -4, 8, 8, 173, 191)		// #3 - Rubble chunk 2
	SpriteFrame(-4, -4, 8, 8, 165, 199)		// #4 - Rubble chunk alt 1
	SpriteFrame(-4, -4, 8, 8, 173, 199)		// #5 - Rubble chunk alt 2
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
