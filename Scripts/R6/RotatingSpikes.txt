//------------Sonic CD Rotating Spikes Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Angle
#alias Object.Value1			:	Object.Increment
#alias Object.Value2			:	Object.SpawnedSpikes

#alias Object[-1].Rotation		:	MovingBlocks.Rotation

// HUD Aliases
#alias Object[24].PropertyValue	:	HUD.TimePeriod

// Time Periods
#alias 0						:	PROPVAL_PRESENT
#alias 1						:	PROPVAL_PAST
#alias 2						:	PROPVAL_GOOD_FUTURE
#alias 3						:	PROPVAL_BAD_FUTURE

// Property Values
#alias 0						:	ROTATION_CLOCKWISE
#alias 1						:	ROTATION_COUNTERCLOCKWISE
#alias 2						:	ROTATION_LINK_MOVINGBLOCKS
#alias 3						:	ROTATION_CCW_TWO_SPIKES
#alias 4						:	ROTATION_CW_SIX_SPIKES


sub ObjectMain
	if Object.PropertyValue == ROTATION_LINK_MOVINGBLOCKS
		Object.Angle = MovingBlocks.Rotation
	else
		Object.Angle += Object.Increment
	end if
	
	if Object.Angle < 0
		Object.Angle += 512
	end if
	Object.Angle &= 511
end sub


sub ObjectPlayerInteraction
	TempValue0 = 0
	TempValue1 = 16
	TempValue2 = Object.XPos
	TempValue3 = Object.YPos

	while TempValue0 < Object.SpawnedSpikes
	
		Cos(Object.XPos, Object.Angle)
		Object.XPos *= TempValue1
		Object.XPos <<= 7
		Object.XPos += TempValue2

		Sin(Object.YPos, Object.Angle)
		Object.YPos *= TempValue1
		Object.YPos <<= 7
		Object.YPos += TempValue3

		PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
		if CheckResult == true
			CallFunction(PlayerObject_Hit)
		end if

		TempValue0++
		TempValue1 += 16
	loop
	Object.XPos = TempValue2
	Object.YPos = TempValue3
end sub


sub ObjectDraw
	DrawSprite(0)
	TempValue0 = 0
	TempValue1 = 16
	while TempValue0 < Object.SpawnedSpikes

		Cos(TempValue2, Object.Angle)
		TempValue2 *= TempValue1
		TempValue2 <<= 7
		TempValue2 += Object.XPos

		Sin(TempValue3, Object.Angle)
		TempValue3 *= TempValue1
		TempValue3 <<= 7
		TempValue3 += Object.YPos

		DrawSpriteXY(1, TempValue2, TempValue3)

		TempValue0++
		TempValue1 += 16
	loop
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	SpriteFrame(-8, -8, 16, 16, 133, 199)	// #0 - Rotating Spikes Center
	SpriteFrame(-8, -8, 16, 16, 150, 199)	// #1 - Spikes

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[RotatingSpikes]
			switch Object[ArrayPos0].PropertyValue
			case ROTATION_CLOCKWISE
				Object[ArrayPos0].Increment = 2
				switch HUD.TimePeriod
				case PROPVAL_PRESENT
					Object[ArrayPos0].SpawnedSpikes = 5
					break

				case PROPVAL_PAST
					Object[ArrayPos0].SpawnedSpikes = 4
					break

				case PROPVAL_GOOD_FUTURE
				case PROPVAL_BAD_FUTURE
					Object[ArrayPos0].SpawnedSpikes = 6
					break
				end switch

				break

			case ROTATION_COUNTERCLOCKWISE
				Object[ArrayPos0].Increment = -2
				switch HUD.TimePeriod
				case PROPVAL_PRESENT
					Object[ArrayPos0].SpawnedSpikes = 5
					break

				case PROPVAL_PAST
					Object[ArrayPos0].SpawnedSpikes = 4
					break

				case PROPVAL_GOOD_FUTURE
				case PROPVAL_BAD_FUTURE
					Object[ArrayPos0].SpawnedSpikes = 6
					break
				end switch
				
				Object[ArrayPos0].Value0 = 384
				break

			case ROTATION_LINK_MOVINGBLOCKS
				Object[ArrayPos0].Increment = -2
				switch HUD.TimePeriod
				case PROPVAL_PRESENT
					Object[ArrayPos0].SpawnedSpikes = 5
					break

				case PROPVAL_PAST
					Object[ArrayPos0].SpawnedSpikes = 4
					break

				case PROPVAL_GOOD_FUTURE
				case PROPVAL_BAD_FUTURE
					Object[ArrayPos0].SpawnedSpikes = 6
					break
				end switch

				break

			case ROTATION_CCW_TWO_SPIKES
				Object[ArrayPos0].Increment = -2
				Object[ArrayPos0].SpawnedSpikes = 2
				break

			case ROTATION_CW_SIX_SPIKES
				Object[ArrayPos0].Increment = 2
				Object[ArrayPos0].SpawnedSpikes = 6
				break
			end switch
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
