//------------Sonic CD Launcher Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.OriginXPos

// States
#alias 0				:	LAUNCHER_IDLE
#alias 1				:	LAUNCHER_DASH
#alias 2				:	LAUNCHER_EJECT_PLAYER
#alias 3				:	LAUNCHER_RECOIL
#alias 4				:	LAUNCHER_RESET

// Priority
#alias 0				:	PRIORITY_ACTIVE_BOUNDS
#alias 1				:	PRIORITY_ACTIVE

// Gravity
#alias 1				:	GRAVITY_AIR


sub ObjectMain
	if Player.State != PlayerObject_Blank
		Object.State = LAUNCHER_IDLE
		Object.Timer = 0
	end if

	switch Object.State
	case LAUNCHER_DASH
		Object.XPos += 786432
		Player.XPos = Object.XPos

		Player.iYPos = Player.CollisionBottom
		FlipSign(Player.YPos)
		Player.YPos -= 196608
		Player.YPos += Object.YPos

		ObjectTileGrip(0, 32, 4, 0)
		if CheckResult == false
			Object.State++
		end if
		break

	case LAUNCHER_EJECT_PLAYER
		Player.XPos += 786432

		Player.iYPos = Player.CollisionBottom
		FlipSign(Player.YPos)
		Player.YPos -= 196608
		Player.YPos += Object.YPos
		Object.State++
		break

	case LAUNCHER_RECOIL
		if Object.Timer == 0
			Player.XPos += 786432
		end if

		Player.State = PlayerObject_HandleAir

		Player.Animation = ANI_WALKING

		Player.Speed = 794624
		Player.XVelocity = 794624

		Player.iYPos = Player.CollisionBottom
		FlipSign(Player.YPos)
		Player.YPos -= 196608
		Player.YPos += Object.YPos

		if Object.Timer < 2
			Object.Timer++
		else
			Object.State++
		end if

		Object.XPos -= 262144
		if Object.XPos < Object.OriginXPos
			Object.XPos = Object.OriginXPos
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
			Object.State = LAUNCHER_IDLE
		end if

		if Object.OutOfBounds == true
			Object.XPos = Object.OriginXPos
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
			Object.State = LAUNCHER_IDLE
		end if
		break

	case LAUNCHER_RESET
		Object.XPos -= 262144
		if Object.XPos < Object.OriginXPos
			Object.XPos = Object.OriginXPos
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
			Object.State = LAUNCHER_IDLE
		end if
		
		if Object.OutOfBounds == true
			Object.XPos = Object.OriginXPos
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
			Object.State = LAUNCHER_IDLE
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	if Object.State == LAUNCHER_IDLE
		PlayerObjectCollision(C_BOX, -24, -26, -18, 4)
		PlayerObjectCollision(C_TOUCH, -20, -16, 20, 4)
		if CheckResult == true
			Object.State = LAUNCHER_DASH
			Player.State = PlayerObject_Blank

			Player.Animation = ANI_LAUNCHER
			Player.Direction = FACING_RIGHT

			Player.XPos = Object.XPos
			Player.iYPos = Player.CollisionBottom
			FlipSign(Player.YPos)
			Player.YPos -= 196608
			Player.YPos += Object.YPos

			Player.YVelocity = 0

			Object.Priority = PRIORITY_ACTIVE
			Player.TrackScroll = false
			Player.Gravity = GRAVITY_AIR
			PlayStageSfx(6, 0)
#platform: Use_Haptics
			HapticEffect(30, 0, 0, 0)
#endplatform
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")
	
	SpriteFrame(-28, -28, 56, 32, 83, 1)	// #0 - Launcher

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Launcher]
			Object[ArrayPos0].OriginXPos = Object[ArrayPos0].XPos
			Object[ArrayPos0].DrawOrder = 4
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
