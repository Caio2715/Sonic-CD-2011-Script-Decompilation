//------------Sonic CD Tube Trap Door Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 42: TYPE_TUBETRAPDOOR


sub ObjectMain
	switch Object.State
	case 0
		if Object.Value0 < 8
			Object.Value0++
			Object.YPos -= 0xA0000
		else
			Object.Value0 = 0
			Object.State++
			
#platform: Use_Standalone
	#platform: Use_Haptics
			HapticEffect(100, 0, 0, 0)
	#endplatform
#endplatform
		end if
		break
	case 1
		if Object.Value0 < 60
			Object.Value0++
		else
			Object.Value0 = 0
			Object.State++
		end if
		break
	case 2
		if Object.Value0 < 80
			Object.Value0++
			Object.YPos += 0x10000
		else
			Object.Value0 = 0
			Object.State++
		end if
		break
	case 3
		if Object.Value0 < 60
			Object.Value0++
		else
			Object.Value0 = 0
			Object.State = 0
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -33, -40, 33, 20)
	if CheckResult == true
		if Player.XPos < Object.XPos
			TempValue0 = Object.XPos
			TempValue0 -= Player.XPos
			TempValue1 = 0x240000
			TempValue1 -= Player.XVelocity
			if TempValue0 > TempValue1
				if Player.Speed > -1
					Player.XPos = -32
					Player.XPos -= Player.CollisionRight
					Player.XPos <<= 16
					Player.XPos += Object.XPos
				end if
				if Player.Speed > 0
					if Player.Direction == FACING_RIGHT
						Player.Pushing = 2
					end if
					Player.Speed = 0
					Player.XVelocity = 0
				end if
			else
				if Object.State < 2
					if TempValue0 > 1572864
						Player.XPos += 0x140000
						Player.Speed = 0
						Player.XVelocity = 0
					end if
				end if
			end if
		else
			TempValue0 = Player.XPos
			TempValue0 -= Object.XPos
			TempValue1 = 0x240000
			TempValue1 += Player.XVelocity
			if TempValue0 > TempValue1
				if Player.Speed < 1
					Player.XPos = 32
					Player.XPos -= Player.CollisionLeft
					Player.XPos <<= 16
					Player.XPos += Object.XPos
				end if
				if Player.Speed < 0
					if Player.Direction == FACING_LEFT
						Player.Pushing = 2
					end if
					Player.Speed = 0
					Player.XVelocity = 0
				end if
			else
				if Object.State < 2
					if TempValue0 > 1572864
						Player.XPos -= 0x140000
						Player.Speed = 0
						Player.XVelocity = 0
					end if
				end if
			end if
		end if
	end if
	switch Object.State
	case 0
		if Object.Value0 < 4
			PlayerObjectCollision(C_PLATFORM, -30, -40, 30, -20)
		else
			PlayerObjectCollision(C_TOUCH, -16, -40, 16, 20)
			if CheckResult == 1
				Player.Speed >>= 1
				Player.XVelocity >>= 1
				PlayerObjectCollision(C_BOX, -48, -80, -32, 20)
				PlayerObjectCollision(C_BOX, 32, -80, 48, 20)
				if Player.State == PlayerObject_HandleTailsFlight
#platform: Use_Origins
					Player.State = PlayerObject_ResetDropDash
#endplatform

#platform: Use_Standalone
					Player.State = PlayerObject_HandleAir
#endplatform
					Player.Animation = ANI_WALKING
				end if
			end if
		end if
		break
	case 2
		PlayerObjectCollision(C_PLATFORM, -30, -41, 30, -20)
		if CheckResult == 1
			Player.YPos += 0x10000
		end if
		break
	case 3
		PlayerObjectCollision(C_PLATFORM, -30, -40, 30, -20)
		break
	end switch
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")
	SpriteFrame(-32, -40, 64, 80, 191, 175)
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[TubeTrapDoor]
			Object[ArrayPos0].DrawOrder = 4
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
