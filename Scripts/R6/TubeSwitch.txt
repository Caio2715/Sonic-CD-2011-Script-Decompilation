//----------------Sonic CD Tube Switch Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.OnObject
#alias Object.Value1	:	Object.LeaveObject
#alias Object.Value2	:	Object.ColLeftTop
#alias Object.Value3	:	Object.ColRightBottom
#alias Object.Value4	:	Object.OnJumpArea

// Player Alias
#alias Player.Value6	:	Player.MinRollSpeed

// Property Values
#alias 0	:	TUBESW_ENTER_H
#alias 1	:	TUBESW_EXIT_H
#alias 2	:	TUBESW_DROP
#alias 3	:	TUBESW_ENTER_V
#alias 4	:	TUBESW_EXIT_V
#alias 5	:	TUBESW_SPIN_WHEEL_L_D	// Diagonal Left Down
#alias 6	:	TUBESW_SPIN_WHEEL_R_U	// Diagonal Right Up
#alias 7	:	TUBESW_SPIN_WHEEL_OVERLAP
#alias 8	:	TUBESW_SPIN_WHEEL_L_U	// Diagonal Left Up
#alias 9	:	TUBESW_ENTER_V_2		// Unused?
#alias 10	:	TUBESW_EXIT_V_2			// Unused?
#alias 11	:	TUBESW_SPIN_WHEEL_L_D_2
#alias 12	:	TUBESW_CURVE

// Global SFX
#alias 7	:	SFX_G_RELEASE

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Collision Modes
#alias 0	:	CMODE_FLOOR
#alias 1	:	CMODE_LWALL
#alias 2	:	CMODE_ROOF
#alias 3	:	CMODE_RWALL


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, Object.ColLeftTop, Object.ColLeftTop, Object.ColRightBottom, Object.ColRightBottom)
	if CheckResult == true
		switch Object.PropertyValue
		case TUBESW_ENTER_H
			if Player.Speed > -1
				Player.Direction	= FACING_RIGHT
				Player.Speed		= 0xA0000

				Player.State		= Player_State_TubeRoll
				Player.Animation	= ANI_JUMPING
				
				Player.MinRollSpeed = 0xA0000
			else
				Player.State	 = Player_State_Roll
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESW_EXIT_H
			if Player.CollisionMode == CMODE_FLOOR
				if Player.Speed < 1
					Player.Direction	= FACING_LEFT
					Player.Speed		= -0xA0000
					
					Player.State		= Player_State_TubeRoll
					Player.Animation	= ANI_JUMPING

					Player.MinRollSpeed = 0xA0000
				else
					Player.State	 = Player_State_Roll
					Player.Animation = ANI_JUMPING
				end if
			else
				Player.Gravity   = GRAVITY_AIR
				FlipSign(Player.Speed)
				Player.XVelocity = Player.Speed

#platform: Use_Origins
				Player.State	 = Player_State_Air_NoDropDash
#endplatform
#platform: Use_Standalone
				Player.State	 = Player_State_Air
#endplatform

			end if
			break

		case TUBESW_DROP
			Player.Gravity		 = GRAVITY_AIR
			Player.CollisionMode = CMODE_FLOOR
			Player.XVelocity	 = 0
			Player.Speed		 = 0
			break

		case TUBESW_ENTER_V
			if Player.YVelocity < 1
				Player.State		 = Player_State_TubeRoll

				Player.MinRollSpeed  = 0xC0000
				Player.Speed		 = -0xC0000
				Player.Gravity		 = GRAVITY_GROUND
				Player.Angle		 = 64

				Player.CollisionMode = CMODE_RWALL
				Player.Animation	 = ANI_JUMPING
				Screen.AdjustCameraY = 0
			else
				if Player.State != Player_State_Fly
					Player.Animation = ANI_JUMPING
				end if
			end if
			break

		case TUBESW_EXIT_V
			if Player.State == Player_State_TubeRoll
				Player.State = Player_State_Roll
			end if
			break

		case TUBESW_SPIN_WHEEL_L_D
			if Player.State == Player_State_TubeAirRoll
				if Player.JumpPress == true
					Object.LeaveObject = true
				else
					if Player.JumpHold == false
						Object.LeaveObject = false
					end if
				end if

				if Object.LeaveObject == true
					PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
					if CheckResult == true

#platform: Use_Origins
						Player.State	 = Player_State_Air_NoDropDash
#endplatform
#platform: Use_Standalone
						Player.State	 = Player_State_Air
#endplatform

						Player.Gravity	 = GRAVITY_AIR

						Player.XPos		 = Object.XPos
						Player.YPos		 = Object.YPos

						Player.Speed	 = -0x80000
						Player.XVelocity = -0x80000
						Player.YVelocity = -0x40000

						Object.LeaveObject = false
						Object.OnJumpArea  = true
					end if
				end if
			else
				PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
				if CheckResult == true
					if Object.OnJumpArea == false
						Player.Gravity			= GRAVITY_GROUND

						Player.XPos				= Object.XPos
						Player.YPos				= Object.YPos

						Player.XVelocity		= 0
						Player.YVelocity		= 0
						Player.Speed			= 0x80000

						Player.State			= Player_State_TubeAirRoll
						Player.Animation		= ANI_JUMPING

						Player.MinRollSpeed		= 0x80000
						Player.Angle			= 224
						Player.CollisionMode	= CMODE_FLOOR

						PlaySfx(SFX_G_RELEASE, 0) 
						Object.OnJumpArea = true
					end if
				else
					Object.OnJumpArea = false
				end if
			end if
			break

		case TUBESW_SPIN_WHEEL_R_U
			if Player.State == Player_State_TubeAirRoll
				if Player.JumpPress == true
					Object.LeaveObject = true
				else
					if Player.JumpHold == false
						Object.LeaveObject = false
					end if
				end if

				if Object.LeaveObject == true
					PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
					if CheckResult == true

#platform: Use_Origins
						Player.State		= Player_State_Air_NoDropDash
#endplatform
#platform: Use_Standalone
						Player.State		= Player_State_Air
#endplatform

						Player.Gravity		= GRAVITY_AIR
						Player.XPos			= Object.XPos
						Player.YPos			= Object.YPos

						Object.LeaveObject	= false

						Player.Speed	 	=  0x80000
						Player.XVelocity	=  0x80000
						Player.YVelocity 	= -0x40000

						Object.OnJumpArea = true
					end if
				end if
			else
				PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
				if CheckResult == true
					if Object.OnJumpArea == false
						Player.Gravity			= GRAVITY_GROUND

						Player.XPos				= Object.XPos
						Player.YPos				= Object.YPos

						Player.XVelocity		= 0
						Player.YVelocity		= 0
						Player.Speed			= 0x80000

						Player.State			= Player_State_TubeAirRoll
						Player.Animation		= ANI_JUMPING

						Player.MinRollSpeed 	= 0x80000
						Player.Angle			= 36
						Player.CollisionMode	= CMODE_RWALL

						PlaySfx(SFX_G_RELEASE, 0) 
						Object.OnJumpArea = true
					end if
				else
					Object.OnJumpArea = false
				end if
			end if
			break

		case TUBESW_SPIN_WHEEL_OVERLAP
			if Object.OnObject == false
				if Player.State == Player_State_TubeAirRoll
					FlipSign(Player.Speed)
					if Player.CollisionMode == CMODE_FLOOR
						Player.CollisionMode = CMODE_ROOF
						Player.Angle		 = 128
					else
						Player.CollisionMode = CMODE_FLOOR
						Player.Angle		 = 0
					end if
				end if
			end if
			break

		case TUBESW_SPIN_WHEEL_L_U
			if Player.State == Player_State_TubeAirRoll
				if Player.JumpPress == true
					Object.LeaveObject = true
				else
					if Player.JumpHold == false
						Object.LeaveObject = false
					end if
				end if
				
				if Object.LeaveObject == true
					PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
					if CheckResult == true

#platform: Use_Origins
						Player.State 	 = Player_State_Air_NoDropDash
#endplatform
#platform: Use_Standalone
						Player.State	 = Player_State_Air
#endplatform

						Player.Gravity	 = GRAVITY_AIR
						
						Player.XPos		 = Object.XPos
						Player.YPos		 = Object.YPos

						Player.Speed	 = -0x80000
						Player.XVelocity = -0x80000
						Player.YVelocity =  0x20000

						Object.LeaveObject = false
						Object.OnJumpArea  = true
					end if
				end if
			else
				PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
				if CheckResult == true
					if Object.OnJumpArea == false
						Player.Speed		 = -0x80000
						Player.Gravity		 = GRAVITY_GROUND

						Player.XPos			 = Object.XPos
						Player.YPos			 = Object.YPos

						Player.XVelocity	 = 0
						Player.YVelocity	 = 0
						Player.State		 = Player_State_TubeAirRoll
						Player.Animation	 = ANI_JUMPING

						Player.MinRollSpeed	 = 0x80000
						Player.Angle		 = 172
						Player.CollisionMode = CMODE_LWALL
						
						PlaySfx(SFX_G_RELEASE, 0) 
						Object.OnJumpArea = true
					end if
				else
					Object.OnJumpArea = false
				end if
			end if
			break

		case TUBESW_ENTER_V_2
			if Player.YVelocity < 1
				Player.State		 = Player_State_TubeRoll
				Player.MinRollSpeed	 = 0xC0000
				Player.Speed		 = 0xC0000

				Player.Gravity		 = GRAVITY_GROUND
				Player.Angle		 = 192
				Player.CollisionMode = CMODE_LWALL
				Player.Animation	 = ANI_JUMPING
				
				Screen.AdjustCameraY = 0
			else
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESW_EXIT_V_2
			if Object.OnObject == false
				if Player.State == Player_State_Roll
					
#platform: Use_Origins
					Player.State	 = Player_State_Air_NoDropDash
#endplatform
#platform: Use_Standalone
					Player.State	 = Player_State_Air
#endplatform

					Player.Gravity	 = GRAVITY_AIR
					Player.Speed	 = 0
					Player.XVelocity = 0
				end if

				if Player.State == Player_State_TubeRoll
					Player.State = Player_State_Roll
				end if
			end if
			break

		case TUBESW_SPIN_WHEEL_L_D_2
			if Player.State != Player_State_TubeAirRoll
				if Object.OnObject == false
					Player.Gravity		 = GRAVITY_GROUND

					Player.XPos			 = Object.XPos
					Player.YPos			 = Object.YPos

					Player.XVelocity	 = 0
					Player.YVelocity	 = 0
					Player.Speed		 = 0x80000

					Player.State		 = Player_State_TubeAirRoll
					Player.Animation	 = ANI_JUMPING
					Player.MinRollSpeed  = 0x80000
					Player.Angle		 = 224
					Player.CollisionMode = CMODE_FLOOR

					PlaySfx(SFX_G_RELEASE, 0) 
				end if
			end if
			break

		case TUBESW_CURVE
			if Player.YVelocity > -1
				Player.State		 = Player_State_TubeRoll

				Player.MinRollSpeed	 = 0xC0000
				Player.Speed		 = -0xC0000

				Player.Gravity		 = GRAVITY_GROUND
				Player.Angle		 = 192
				Player.CollisionMode = CMODE_LWALL
				Player.Animation	 = ANI_JUMPING

				Screen.AdjustCameraY = 0
			else
				Player.State		 = Player_State_TubeRoll

				Player.MinRollSpeed	 = 0xC0000
				Player.Speed		 = Player.YVelocity
				FlipSign(Player.Speed)

				Player.Gravity		 = GRAVITY_GROUND
				Player.Angle		 = 192
				Player.CollisionMode = CMODE_LWALL
				Player.Animation	 = ANI_JUMPING
			end if
			break
		end switch

		Object.OnObject = true
	else
		Object.OnObject = false
	end if
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[TubeSW]
			switch Object[ArrayPos0].PropertyValue
			case TUBESW_ENTER_H
			case TUBESW_EXIT_H
			case TUBESW_DROP
			case TUBESW_ENTER_V
			case TUBESW_EXIT_V
			case TUBESW_SPIN_WHEEL_OVERLAP
				Object[ArrayPos0].ColLeftTop	 = -16
				Object[ArrayPos0].ColRightBottom =  16
				break

			case TUBESW_SPIN_WHEEL_L_D
			case TUBESW_SPIN_WHEEL_R_U
			case TUBESW_SPIN_WHEEL_L_U
				Object[ArrayPos0].ColLeftTop	 = -48
				Object[ArrayPos0].ColRightBottom =  48
				break

			case TUBESW_ENTER_V_2
			case TUBESW_EXIT_V_2
				Object[ArrayPos0].ColLeftTop	 = -8
				Object[ArrayPos0].ColRightBottom =  8
				break
				
			case TUBESW_CURVE
				Object[ArrayPos0].ColLeftTop	 = -16
				Object[ArrayPos0].ColRightBottom =  16
				break
			end switch
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
