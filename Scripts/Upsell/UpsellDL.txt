//------------Sonic CD Upsell DL Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Fade
#alias Object.Value1 : Object.ButtonCount
//States
#alias 0 : UpsellDL_SETUP_INFADE
#alias 1 : UpsellDL_FADEIN
#alias 2 : UpsellDL_CONTROLS
#alias 3 : UpsellDL_EXIT

// Callbacks & Engine States
#alias 8 : CALLBACK_BUY_FULL_GAME_SELECTED

// Languages
#alias 0 : ENGLISH
#alias 1 : FRENCH
#alias 2 : ITALIAN
#alias 3 : DEUTSCH
#alias 4 : SPANISH
#alias 5 : JAPANESE


sub ObjectMain
#platform: Mobile
	switch Object.State
	case UpsellDL_FADEIN
		if Options.PhysicalControls == false
			Object.ButtonCount = -1
		else
			Object.ButtonCount = 0
		end if

		if Object.Fade < 16
			Object.Fade++
		else
			Object.Fade = 288
			Object.State++
		end if
		SetScreenFade(0, 0, 0, 255)
		break

	case UpsellDL_FADEIN
		if Object.Fade > 0
			Object.Fade -= 8
		else
			Object.State++
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break

	case UpsellDL_CONTROLS
		if Options.PhysicalControls == true
			if KeyPress.Up == true
				Object.ButtonCount--
				if Object.ButtonCount < 0
					Object.ButtonCount = 1
				end if
				PlaySfx(23, 0) 	//Menu Button SFX
			end if

			if KeyPress.Down == true
				Object.ButtonCount++
				if Object.ButtonCount > 1
					Object.ButtonCount = 0
				end if
				PlaySfx(23, 0) 	//Menu Button SFX
			end if

			if KeyPress.Start == true
				switch Object.ButtonCount
				case 0
					EngineCallback(CALLBACK_BUY_FULL_GAME_SELECTED)
					break
				case 1
					Object.State = UpsellDL_EXIT
					break
				end switch
			end if

			if KeyPress.ButtonA == true
				switch Object.ButtonCount
				case 0
					EngineCallback(CALLBACK_BUY_FULL_GAME_SELECTED)
					break
				case 1
					Object.State = UpsellDL_EXIT
					break
				end switch
			end if

			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.ButtonCount = -1
			end if
		else
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			TempValue3 = CheckResult
			TempValue0 = Screen.CenterX
			TempValue0 -= 80
			TempValue1 = Screen.CenterX
			TempValue1 += 80
			CheckTouchRect(TempValue0, 168, TempValue1, 200)
			if CheckResult > -1
				Object.ButtonCount = 0
			else
				if TempValue3 < 0
					if Object.ButtonCount == 0
						EngineCallback(CALLBACK_BUY_FULL_GAME_SELECTED)
						Object.ButtonCount = -1
					end if
				else
					if Object.ButtonCount == 0
						Object.ButtonCount = -1
					end if
				end if
			end if

			TempValue0 = Screen.CenterX
			TempValue0 -= 64
			TempValue1 = Screen.CenterX
			TempValue1 += 64
			CheckTouchRect(TempValue0, 208, TempValue1, 236)
			if CheckResult > -1
				Object.ButtonCount = 1
			else
				if TempValue3 < 0
					if Object.ButtonCount == 1
						Object.State = UpsellDL_EXIT
						Object.ButtonCount = -1
					end if
				else
					if Object.ButtonCount == 1
						Object.ButtonCount = -1
					end if
				end if
			end if

			if KeyPress.Up == true
				Object.ButtonCount = 1
				Options.PhysicalControls = true
			end if

			if KeyPress.Down == true
				Object.ButtonCount = 0
				Options.PhysicalControls = true
			end if
		end if
		break
	case UpsellDL_EXIT
		if Object.Fade < 288
			Object.Fade += 8
		else
			// Going back to normal-quality graphics, signal the engine to exit high-res mode
			Engine.State = 6

			// Load the menu
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos = 1
			LoadStage()
		end if

		TempValue0 = Object.Fade
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break
	end switch
#endplatform
end sub


sub ObjectDraw
#platform: Mobile
	Object.Scale = 216
	DrawSpriteScreenFX(0, FX_SCALE, Screen.CenterX, Screen.CenterY)
	if Object.ButtonCount == 0
		// The "Buy full game" button is selected, draw its selected sprite as well
		DrawSpriteScreenFX(1, FX_SCALE, Screen.CenterX, 186)
	end if

	if Object.ButtonCount == 1
		// The "Play again" button is selected, draw its selected sprite as well
		DrawSpriteScreenFX(2, FX_SCALE, Screen.CenterX, 222)
	end if
#endplatform
end sub


sub ObjectStartup
#platform: Mobile

	// Cycle through all scene objects and find if there are any Upsell DL Objects
	TempValue0 = false
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Upsell DL]
			// If there are, then set the load sheet flag to true
			TempValue0 = true
		end if

		ArrayPos0++
	loop

	// Only if there's an Upsell DL object in the scene, load the sheet
	// (It's a really large sheet! Loading it unnecessarily can heavily increase loading times!)
	if TempValue0 == true
		switch Engine.Language
		case ENGLISH
//			LoadSpriteSheet("Upsell/UpsellDL_EN.pvr")		// Sonic CD Lite used PVRs, but the latest releases use gif instead
			LoadSpriteSheet("Upsell/UpsellDL_EN.gif")
			break
		case FRENCH
			LoadSpriteSheet("Upsell/UpsellDL_FR.gif")
			break
		case ITALIAN
			LoadSpriteSheet("Upsell/UpsellDL_IT.gif")
			break
		case DEUTSCH
			LoadSpriteSheet("Upsell/UpsellDL_DE.gif")
			break
		case SPANISH
			LoadSpriteSheet("Upsell/UpsellDL_ES.gif")
			break
		case JAPANESE
//			LoadSpriteSheet("Upsell/UpsellDL_JP.pvr")		// Sonic CD Lite used PVRs, but the latest releases use gif instead
			LoadSpriteSheet("Upsell/UpsellDL_JP.gif")
			break
		end switch

		// Load the entire palette file, notably overriding the global section of the palette as well
		LoadPalette("UpsellDL.act", 0, 0, 0, 256)
	end if
	
	// The gif is a high-res sprite, so signal to the Engine to start up High-Res mode
	Engine.State = 5

	// Entire BG sprite
	SpriteFrame(-512, -288, 1024, 576, 0, 0)

	// The Japanese version of the selected frames are just a tad bit taller
	if Engine.Language != JAPANESE
		// "Buy full game" (or some equivalent in the chosen language)
		SpriteFrame(-128, -35, 260, 69, 381, 580)

		// "Try again" (or some equivalent in the chosen language)
		SpriteFrame(-91, -24, 182, 49, 420, 673)
	else
		// "完全版の購入"
		SpriteFrame(-128, -36, 260, 69, 381, 580)

		// "もう一度遊ぶ"
		SpriteFrame(-91, -24, 183, 49, 420, 677)
	end if

#endplatform
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Upsell/UpsellDL_EN.gif")
	SpriteFrame(-512, -288, 1024, 576, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
