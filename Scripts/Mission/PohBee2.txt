//------------Sonic CD PohBee2 Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 71: TYPE_POHBEE2


sub ObjectMain
	if Object.OutOfBounds == true
		Object.State = POHBEE_RESET
		Object.Timer = 0
		Object.Direction = FACING_RIGHT
		Object.XPos = Object.XOriginPos
		Object.CurrentFrame = 0
		Object.ShootingCooldown = 0
		Object.FrameIncrease = 0
	end if
	
	if Object.Quality == PROPVAL_QUALITY_GOOD
		switch Object.State
		case POHBEE_MOVING_LEFT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown > 0
				Object.ShootingCooldown--
			end if
			if Object.Timer < 256
				Object.Timer++
				Object.XPos -= 0x20000
			else
				Object.State = POHBEE_TURN_RIGHT
				Object.Timer = 0
			end if
			break

		case POHBEE_TURN_RIGHT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown > 0
				Object.ShootingCooldown--
			end if
			if Object.Timer < 60
				if Object.Timer == 30
					Object.Direction=FACING_LEFT
				end if
				Object.Timer++
			else
				Object.State = POHBEE_MOVING_RIGHT
			end if
			break

		case POHBEE_MOVING_RIGHT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown > 0
				Object.ShootingCooldown--
			end if
			if Object.Timer < 256
				Object.Timer++
				Object.XPos += 0x20000
			else
				Object.State = POHBEE_TURN_LEFT
				Object.Timer = 0
			end if
			break

		case POHBEE_TURN_LEFT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown > 0
				Object.ShootingCooldown--
			end if
			if Object.Timer < 60
				if Object.Timer == 30
					Object.Direction = FACING_RIGHT
				end if
				Object.Timer++
			else
				Object.State = POHBEE_MOVING_LEFT
			end if
			break

		case POHBEE_SHOOTING_LEFT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown < 46
				Object.ShootingCooldown++
				if Object.ShootingCooldown == 15
					Object.FrameIncrease = 4
				end if
				if Object.ShootingCooldown == 32
					CreateTempObject(TypeName[PohBeeBullet],0,Object.XPos,Object.YPos)
					Object[TempObjectPos].XPos -= 0xF0000
					Object[TempObjectPos].YPos += 0x1B0000
					Object[TempObjectPos].XVelocity = -0x40000
					Object[TempObjectPos].YVelocity =  0x40000
					PlayStageSfx(7,0)
				end if
			else
				Object.State = POHBEE_MOVING_LEFT
				Object.FrameIncrease = 0
				Object.ShootingCooldown = 30
			end if
			break
			
		case POHBEE_SHOOTING_RIGHT
			Object.Priority = PRIORITY_ACTIVE
			if Object.ShootingCooldown < 46
				Object.ShootingCooldown++
				if Object.ShootingCooldown == 15
					Object.FrameIncrease = 4
				end if
				if Object.ShootingCooldown == 32
					CreateTempObject(TypeName[PohBeeBullet],0,Object.XPos,Object.YPos)
					Object[TempObjectPos].Direction=FACING_LEFT
					Object[TempObjectPos].XPos += 0xF0000
					Object[TempObjectPos].YPos += 0x1B0000
					Object[TempObjectPos].XVelocity = 0x40000
					Object[TempObjectPos].YVelocity = 0x40000
					PlayStageSfx(7,0)
				end if
			else
				Object.State = POHBEE_MOVING_RIGHT
				Object.FrameIncrease = 0
				Object.ShootingCooldown = 30
			end if
			break

		case POHBEE_RESET
			if Object.OutOfBounds == true
				Object.State = POHBEE_MOVING_LEFT
				Object.Priority = PRIORITY_ACTIVE_BOUNDS
			end if
			break

		end switch
	else
		switch Object.State
		case POHBEE_MOVING_LEFT
			Object.Priority = PRIORITY_ACTIVE
			if Object.Timer < 256
				Object.Timer++
				Object.XPos -= 0x10000
			else
				Object.State = POHBEE_TURN_RIGHT
				Object.Timer = 0
			end if
			break
			
		case POHBEE_TURN_RIGHT
			Object.Priority = PRIORITY_ACTIVE
			if Object.Timer < 30
				if Object.Timer == 15
					Object.Direction = FACING_LEFT
				end if
				Object.Timer++
			else
				Object.State = POHBEE_MOVING_RIGHT
			end if
			break
			

		case POHBEE_MOVING_RIGHT
			Object.Priority = PRIORITY_ACTIVE
			if Object.Timer < 512
				Object.Timer++
				Object.XPos += 0x10000
			else
				Object.State = POHBEE_TURN_LEFT
				Object.Timer = 0
			end if
			break

		case POHBEE_TURN_LEFT
			Object.Priority = PRIORITY_ACTIVE
			if Object.Timer < 30
				if Object.Timer == 15
					Object.Direction = FACING_RIGHT
				end if
				Object.Timer++
			else
				Object.State = POHBEE_MOVING_LEFT
			end if
			break

		case POHBEE_RESET
			if Object.OutOfBounds == true
				Object.State = POHBEE_MOVING_LEFT
				Object.Priority = PRIORITY_ACTIVE_BOUNDS
			end if
			break
		end switch
	end if

	CallFunction(StageSetup_CheckGoodFuture)
end sub


sub ObjectPlayerInteraction
	if Object.Quality == PROPVAL_QUALITY_GOOD
		if Object.State < POHBEE_SHOOTING_LEFT
			if Object.ShootingCooldown == 0
				PlayerObjectCollision(C_TOUCH, -96, -64, 96, 64)
				if CheckResult == true
					if Object.XPos > Player.XPos
						Object.Direction = FACING_RIGHT
						Object.State = POHBEE_SHOOTING_LEFT
					else
						Object.Direction = FACING_LEFT
						Object.State = POHBEE_SHOOTING_RIGHT
					end if
				end if
			end if
		end if
	end if
	
	if Object.State < POHBEE_RESET
		PlayerObjectCollision(C_TOUCH, -20, -16, 20, 14)
		if CheckResult == true
			CallFunction(Player_BadnikBreak)
		end if
	end if
end sub


sub ObjectDraw
	if Object.State < POHBEE_RESET
		if Object.Quality == PROPVAL_QUALITY_GOOD
			Object.CurrentFrame++
			Object.CurrentFrame &= 3

			Object.Frame   = Object.CurrentFrame
			Object.Frame >>= 1
			Object.Frame  += Object.FrameIncrease
		else
			Object.CurrentFrame++
			Object.CurrentFrame &= 3

			Object.Frame   = Object.CurrentFrame
			Object.Frame >>= 1
			Object.Frame  += 2
		end if
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")
	// Good
	SpriteFrame(-24, -20, 48, 32, 148, 75)	// #0 - PohBee Fly frame 0
	SpriteFrame(-24, -16, 48, 28, 148, 108)	// #1 - PohBee Fly frame 1
	// Bad
	SpriteFrame(-24, -20, 48, 32, 99, 133)	// #2 - PohBee Fly frame 0
	SpriteFrame(-24, -16, 48, 28, 148, 137)	// #3 - PohBee Fly frame 1
	// Shooting
	SpriteFrame(-24, -20, 48, 40, 197, 91)	// #4 - PohBee Shoot frame 0
	SpriteFrame(-24, -16, 48, 36, 197, 132)	// #5 - PohBee Shoot frame 1
	
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type == TypeName[PohBee]
			Object[ArrayPos0].Type = TypeName[PohBee2]
		end if
		if Object[ArrayPos0].Type == TypeName[PohBee2]
			Object[ArrayPos0].XOriginPos = Object[ArrayPos0].XPos
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


