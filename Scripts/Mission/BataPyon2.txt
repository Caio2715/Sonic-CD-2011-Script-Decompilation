//---------------Sonic CD Bata Pyon 2 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 		:	Object.Timer
#alias Object.Value1 		:	Object.XVelocity
#alias Object.Value2 		:	Object.YVelocity
#alias Object.Value3 		:	Object.XOriginPos
#alias Object.Value4 		:	Object.YOriginPos
#alias Object.Value5 		:	Object.TurnTimer

#alias Object.PropertyValue :	Object.Quality

// States
#alias 0	:	BATAPYON2_JUMP
#alias 1	:	BATAPYON2_BOUNCE
#alias 2	:	BATAPYON2_BAD_RECOIL_1
#alias 3	:	BATAPYON2_BAD_RECOIL_2
#alias 4	:	BATAPYON2_BAD_RECOIL_3
#alias 5	:	BATAPYON2_BAD_RECOIL_4
#alias 6	:	BATAPYON2_BAD_BOUNCE
#alias 7	:	BATAPYON2_RESET

// Collision Sides
#alias 0	:	CSIDE_FLOOR
#alias 1	:	CSIDE_LWALL
#alias 2	:	CSIDE_RWALL
#alias 3	:	CSIDE_ROOF

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

// Property Values
#alias 0	:	PROPVAL_QUALITY_GOOD
#alias 1	:	PROPVAL_QUALITY_BAD


sub ObjectMain
	if Object.OutOfBounds == true
		Object.State	 = BATAPYON2_RESET
		Object.Direction = FACING_RIGHT
		Object.XPos		 = Object.XOriginPos
		Object.YPos		 = Object.YOriginPos

		Object.XVelocity = -0x14000
		Object.YVelocity =  0x100000
		Object.TurnTimer =  0
	end if

	switch Object.State
	case BATAPYON2_JUMP
		Object.XPos		 += Object.XVelocity

		Object.YPos		 += Object.YVelocity
		Object.YVelocity += 0x4000

		ObjectTileCollision(CSIDE_ROOF, 0, -18, 0)
		if CheckResult == true
			Object.YVelocity = 0
		end if

		if Object.YVelocity > 0
			ObjectTileCollision(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == true
				if Object.Quality == PROPVAL_QUALITY_GOOD
					Object.State	= BATAPYON2_BOUNCE
					Object.Priority = PRIORITY_ACTIVE
					Object.TurnTimer++
					if Object.TurnTimer == 8
						Object.TurnTimer = 0
						Object.Direction++
						Object.Direction &= FACING_LEFT
						FlipSign(Object.XVelocity)
					end if
				else
					Object.State	= BATAPYON2_BAD_RECOIL_1
					Object.Priority = PRIORITY_ACTIVE
					Object.TurnTimer++
					if Object.TurnTimer == 4
						Object.TurnTimer = 0
						Object.Direction++
						Object.Direction &= FACING_LEFT
						FlipSign(Object.XVelocity)
					end if
				end if
			end if
		end if

		if Object.XVelocity > 0
			ObjectTileCollision(CSIDE_LWALL, 16, 0, 0)
			if CheckResult == true
				Object.TurnTimer = 0
				Object.Direction = FACING_RIGHT
				FlipSign(Object.XVelocity)
			end if
		else
			ObjectTileCollision(CSIDE_RWALL, -16, 0, 0)
			if CheckResult == true
				Object.TurnTimer = 0
				Object.Direction = FACING_LEFT
				FlipSign(Object.XVelocity)
			end if
		end if
		
		Object.Frame   = Object.Quality
		Object.Frame <<= 1

		if Object.YVelocity < 0
			Object.Frame++
		end if
		break

	case BATAPYON2_BOUNCE
		Object.XPos		+= Object.XVelocity
		Object.YPos		-= 0x120000

		Object.YVelocity = -0xC0000

		Object.State     = BATAPYON2_JUMP

		Object.Frame     = Object.Quality
		Object.Frame   <<= 1

		if Object.YVelocity < 0
			Object.Frame++
		end if
		break

	case BATAPYON2_BAD_RECOIL_1
		if Object.Timer < 2
			Object.Timer++
		else
			Object.State++
			Object.Frame = 3
			Object.YPos -= 0x120000
		end if
		break

	case BATAPYON2_BAD_RECOIL_2
		if Object.Timer < 3
			Object.Timer++
		else
			Object.State++
			Object.Frame = 2
			Object.YPos += 0x120000
		end if
		break

	case BATAPYON2_BAD_RECOIL_3
		if Object.Timer < 5
			Object.Timer++
		else
			Object.State++
			Object.Frame = 3
			Object.YPos -= 0x120000
		end if
		break

	case BATAPYON2_BAD_RECOIL_4
		if Object.Timer < 8
			Object.Timer++
		else
			Object.State++
			Object.Frame = 2
			Object.YPos += 0x120000
		end if
		break

	case BATAPYON2_BAD_BOUNCE
		if Object.Timer < 10
			Object.Timer++
		else
			Object.Timer = 0
			Object.Frame = 3

			Object.YPos		-=  0x120000
			Object.YVelocity = -0x50000

			Object.State = BATAPYON2_JUMP
		end if
		break

	case BATAPYON2_RESET
		if Object.OutOfBounds == true
			Object.State = BATAPYON2_JUMP
			Object.Priority = PRIORITY_BOUNDS
		end if
		break
	end switch
	CallFunction(StageSetup_CheckGoodFuture)
end sub


sub ObjectPlayerInteraction
	if Object.State < BATAPYON2_RESET
		PlayerObjectCollision(C_TOUCH, -14, -12, 14, 12)
		if CheckResult == true
			CallFunction(Player_BadnikBreak)
		end if
	end if
end sub


sub ObjectDraw
	if Object.State < BATAPYON2_RESET
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	// Good
	SpriteFrame(-16, -18, 32, 40, 34, 134)	// #0 - Pata Byon
	SpriteFrame(-16, -18, 32, 56, 67, 167)	// #1 - Pata Byon Jump
	// Bad
	SpriteFrame(-16, -18, 32, 40, 34, 175)	// #2 - Pata Byon
	SpriteFrame(-16, -18, 32, 56, 100, 167)	// #3 - Pata Byon Jump
	
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[BataPyon]
			Object[ArrayPos0].Type = TypeName[BataPyon2]
		end if

		if Object[ArrayPos0].Type == TypeName[BataPyon2]
			Object[ArrayPos0].XOriginPos = Object[ArrayPos0].XPos
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos

			Object[ArrayPos0].XVelocity  = -0x14000
			Object[ArrayPos0].YVelocity  =  0x100000
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


