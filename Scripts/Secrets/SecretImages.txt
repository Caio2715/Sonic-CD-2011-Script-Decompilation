//------------Sonic CD Secret Images Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	: Object.Timer // Also used for fading

// States
#alias 0	: SECRETIMAGES_SETUP
#alias 1	: SECRETIMAGES_FADE_IN
#alias 2	: SECRETIMAGES_CONTROL_DELAY
#alias 3	: SECRETIMAGES_CONTROL_CHECK
#alias 4	: SECRETIMAGES_EXIT


sub ObjectMain
	switch Object.State
	case SECRETIMAGES_SETUP
		if Object.Timer < 8
			Object.Timer++
		else
			Object.Timer = 288
			Object.State++
		end if
		SetScreenFade(0, 0, 0, 255)
		break

	case SECRETIMAGES_FADE_IN
		if Object.Timer > 0
			Object.Timer -= 16
		else
			PlayMusic(Options.Soundtrack)
			Object.State++
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case SECRETIMAGES_CONTROL_DELAY
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case SECRETIMAGES_CONTROL_CHECK
		if KeyPress.ButtonA == true
			Object.State++
		end if

		if KeyPress.ButtonB == true
			Object.State++
		end if

		if KeyPress.ButtonC == true
			Object.State++
		end if

		if KeyPress.Start == true
			Object.State++
		end if

		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult > -1
			Object.State++
		end if
		break

	case SECRETIMAGES_EXIT
		if Object.Timer < 288
			Object.Timer += 8
			Music.Volume -= 5
		else
			StopMusic()
			Options.GameMode = 0
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos = 1	// Menu
			LoadStage()
		end if
		TempValue0 = Object.Timer
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break
	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case 0	// Batman
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(0, Screen.CenterX, Screen.CenterY)
		break

	case 1	// Infinite
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(1, Screen.CenterX, Screen.CenterY)
		break

	case 2	// Anime is not a mistake
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 238, 238, 204, 255)
		DrawSpriteScreenXY(2, Screen.CenterX, Screen.CenterY)
		break

	case 3	// Fastest DJ
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(3, Screen.CenterX, Screen.CenterY)
		break

	case 4	// See You Next Game
		DrawRect(0, 0, Screen.CenterX, Screen.YSize, 0, 34, 221, 255)
		DrawRect(Screen.CenterX, 0, Screen.CenterX, Screen.YSize, 0, 34, 204, 255)
		DrawSpriteScreenXY(4, Screen.CenterX, Screen.CenterY)
		DrawSpriteScreenXY(5, Screen.CenterX, Screen.CenterY)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/SECRETIMAGESs.gif")
	SpriteFrame(-160, -112, 320, 224, 0, 0)		// #0 - Batman Sonic
	SpriteFrame(-160, -112, 320, 224, 0, 225)	// #1 - Fun is Infinite
	SpriteFrame(-160, -112, 320, 224, 0, 450)	// #2 - Anime Eyes Sonic
	SpriteFrame(-160, -112, 320, 224, 0, 675)	// #3 - Fastest DJ MC Sonic
	SpriteFrame(-160, -112, 160, 224, 321, 0)	// #4 - See You Next Game first half
	SpriteFrame(0, -112, 160, 224, 321, 225)	// #5 - See You Next Game second half
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
