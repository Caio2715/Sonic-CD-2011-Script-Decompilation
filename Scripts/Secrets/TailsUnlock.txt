//------------Sonic CD Tails Unlock Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	: Object.Timer // Also used for fading

// States
#alias 0	: TAILSUNLOCK_SETUP
#alias 1	: TAILSUNLOCK_FADE_IN
#alias 2	: TAILSUNLOCK_CONTROL_DELAY
#alias 3	: TAILSUNLOCK_CONTROL_CHECK
#alias 4	: TAILSUNLOCK_EXIT

sub ObjectMain
	switch Object.State
	case TAILSUNLOCK_SETUP
		if Object.Timer < 16
			Object.Timer++
#platform: Mobile
			if Stage.DebugMode == false
				SetAchievement(7, 100)	// All Stages Clear!
			end if
#endplatform
			Object.Timer = 288
			Object.State++
		end if
		SetScreenFade(0, 0, 0, 255)
		break

	case TAILSUNLOCK_FADE_IN
		if Object.Timer > 0
			Object.Timer -= 8
		else
			PlayStageSfx(41, 0)
			Object.State++
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case TAILSUNLOCK_CONTROL_DELAY
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case TAILSUNLOCK_CONTROL_CHECK
		if KeyPress.ButtonA == true
			Object.State++
		end if

		if KeyPress.ButtonB == true
			Object.State++
		end if

		if KeyPress.ButtonC == true
			Object.State++
		end if

		if KeyPress.Start == true
			Object.State++
		end if

		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult > -1
			Object.State++
		end if

		if Object.Timer < 300
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = TAILSUNLOCK_EXIT
		end if
		break

	case TAILSUNLOCK_EXIT
		if Object.Timer < 288
			Object.Timer += 8
		else
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos = 1	// Menu
			LoadStage()
		end if
		TempValue0 = Object.Timer
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break

	end switch
end sub


sub ObjectDraw
	DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
	DrawSpriteScreenXY(0, Screen.CenterX, Screen.CenterY)
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/SecretImages.gif")
	SpriteFrame(-117, -36, 234, 64, 0, 900)		// #0 - See You Next Game
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
