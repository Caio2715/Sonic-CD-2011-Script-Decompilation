//---------------Sonic CD Boundary Adjust Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// States
#alias 0	:	BOUNDARYADJUST_SET_BOUNDS
#alias 1	:	BOUNDARYADJUST_RESTORE_BOUNDS


sub ObjectMain
	if Object.State == BOUNDARYADJUST_SET_BOUNDS
#platform: Use_Origins
		if Object.PropertyValue == 0
			if game.playMode == BOOT_PLAYMODE_BOSSRUSH
				Object.Type = TypeName[Blank Object]
			else
				if Player.XPos > Object.XPos
					// Set Left Bound
					TempValue0  = 0xC30
					TempValue0 -= Screen.CenterX
					Stage.NewXBoundary1 = TempValue0

					// Set Right Bound
					TempValue0 += Screen.XSize
					Stage.NewXBoundary2 = TempValue0

					// Set Top Bound
					TempValue0  = Object.iYPos
					TempValue0 -= Screen.YSize
					Stage.NewYBoundary1 = TempValue0

					// Set Bottom Bound
					Stage.YBoundary2  = Screen.YOffset
					Stage.YBoundary2 += Screen.YSize
					if Stage.YBoundary2 < Object.iYPos
						Stage.YBoundary2 = Object.iYPos
					end if
					Object.State++
				end if
			end if
		else
			if game.playMode != BOOT_PLAYMODE_BOSSRUSH
				Object.Type = TypeName[Blank Object]
			else
				if Player.XPos > Object.XPos
					// Set Left Bound
					TempValue0  = 0xF00
					TempValue0 -= Screen.CenterX
					Stage.NewXBoundary1 = TempValue0

					// Set Right Bound
					TempValue0 += Screen.XSize
					Stage.NewXBoundary2 = TempValue0

					Screen.XOffset = 0xCBC

					// Set Top Bound
					TempValue0  = Object.iYPos
					TempValue0 -= Screen.YSize
					Stage.NewYBoundary1 = TempValue0

					// Set Bottom Bound
					Stage.YBoundary2  = Screen.YOffset
					Stage.YBoundary2 += Screen.YSize
					Stage.YBoundary2  = Object.iYPos
					Object.State++
				end if
			end if
		end if
#endplatform

#platform: Use_Standalone
		if Player.XPos > Object.XPos
			// Set Left Bound
			TempValue0  = 3120
			TempValue0 -= Screen.CenterX
			Stage.NewXBoundary1 = TempValue0
			// Set Right Bound
			TempValue0 += Screen.XSize
			Stage.NewXBoundary2 = TempValue0

			// Set Top Bound
			TempValue0  = Object.iYPos
			TempValue0 -= Screen.YSize			
			Stage.NewYBoundary1 = TempValue0

			// Set Bottom Bound
			Stage.YBoundary2  = Screen.YOffset
			Stage.YBoundary2 += Screen.YSize
			if Stage.YBoundary2 < Object.iYPos
				Stage.YBoundary2 = Object.iYPos
			end if
			Object.State++
		end if
#endplatform
	else
		Stage.YBoundary2 -= 3
		if Stage.YBoundary2 <= Object.iYPos
			Stage.YBoundary2 = Object.iYPos
			Object.Type = TypeName[Blank Object]
		end if
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	if Editor.ShowGizmos == true
		// Draw a little indicator of where the bounds will be moved to
		
		TempValue0  = Object.XPos
		TempValue0 -= 0x180000
		TempValue1  = Object.XPos
		TempValue1 += 0x180000
		DrawLine(TempValue0, Object.XPos, TempValue1, Object.YPos, 255, 255, 0)
	end if
	
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
