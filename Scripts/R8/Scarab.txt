//------------Sonic CD Scarab Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.XOriginPos
#alias Object.Value3	:	Object.YOriginPos
#alias Object.Value4	:	Object.LaunchDelay
#alias Object.Value5	:	Object.LaunchSpeed
#alias Object.Value6	:	Object.OnObject
#alias Object.Value7	:	Object.CarriesMonitor

// States
#alias 0	:	SCARAB_MOVE
#alias 1	:	SCARAB_STOP

// Property Values
#alias 0	:	PROPVAL_FACING_RIGHT
#alias 1	:	PROPVAL_FACING_LEFT
#alias 2	:	PROPVAL_FACING_RIGHT_S
#alias 3	:	PROPVAL_FACING_LEFT_S
#alias 4	:	PROPVAL_FACING_RIGHT_W_OBJ

// Priority
#alias 0	:	PRIORITY_ACTIVE_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

sub ObjectMain
	switch Object.State
	case 0
		Object.Priority = PRIORITY_ACTIVE

		if Object.Timer < 60
			Object.Timer++
			Object.XPos += Object.XVelocity
		else
			Object.Timer = 0
			Object.State = SCARAB_STOP
		end if

		ObjectTileGrip(0, 0, 12, 0)
		if Object.XVelocity > 0
			TempValue0  = Object.XPos
			TempValue0 -= Object.XOriginPos
			if TempValue0 > 0x400000
				FlipSign(Object.XVelocity)
			end if
		else
			TempValue0  = Object.XOriginPos
			TempValue0 -= Object.XPos
			if TempValue0 > 0x400000
				FlipSign(Object.XVelocity)
			end if
		end if

		Object.AnimationTimer += Object.AnimationSpeed
		Object.AnimationTimer %= 40
		break

	case SCARAB_STOP
		if Object.Timer < 30
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = SCARAB_MOVE
		end if
		break
	end switch

	if Object.LaunchDelay > 0
		if Player.State == PlayerObject_Death
			Object.LaunchDelay = 0
		else
			if Object.Direction == FACING_RIGHT
				Player.XPos  = Object.XPos
				Player.XPos -= 0x180000
				Player.YPos  = Object.YPos
				Player.YPos -= 0x30000
			else
				Player.XPos  = Object.XPos
				Player.XPos += 0x180000
				Player.YPos  = Object.YPos
				Player.YPos -= 0x30000
			end if
			Object.LaunchDelay--
			if Object.LaunchDelay == 0
				Player.State = PlayerObject_HandleRolling
				Player.Speed = Object.LaunchSpeed
				Player.XVelocity = Player.Speed
			end if
		end if
	end if

	if Object.CarriesMonitor == true
		if Object[+1].Type == TypeName[Monitor]
			Object[+1].XPos  = Object.XPos
			Object[+1].XPos -= 0x180000

			Object[+1].YPos  = Object.YPos
			Object[+1].YPos -= 0x40000
		else
			Object[+1].Type = TypeName[Blank Object]
			Object.CarriesMonitor = false
		end if
	end if

	CallFunction(StageSetup_CheckGoodFuture)
	if Object.OutOfBounds == true
		TempValue0 = Object.XPos
		TempValue1 = Object.YPos

		Object.XPos = Object.XOriginPos
		Object.YPos = Object.YOriginPos

		if Object.OutOfBounds == true	//...?
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
			Object.State = SCARAB_MOVE
			Object.Timer = 0
			Object.AnimationTimer = 0

			if Object.XVelocity < 0
				FlipSign(Object.XVelocity)
			end if
		else
			Object.XPos = TempValue0
			Object.YPos = TempValue1
		end if
	end if
end sub


sub ObjectPlayerInteraction
	if Object.State < 2	// This is always true
		if Object.LaunchDelay == 0
			if Object.Direction == FACING_RIGHT
				PlayerObjectCollision(C_TOUCH, -32, -12, -12, 12)
				if CheckResult == true
					if Object.OnObject == false
						Object.LaunchDelay = 120

						Player.Animation = ANI_JUMPING
						Player.State = PlayerObject_Blank

						Player.Speed = 0
						Player.XVelocity = 0
						Player.YVelocity = 0

						Player.XPos  = Object.XPos
						Player.XPos -= 0x180000

						Player.YPos  = Object.YPos
						Player.YPos -= 0x30000
						
						Player.Direction = FACING_RIGHT

						if Warp.Destination > 0
							if Warp.Timer > 99
								if Warp.Timer < 220	// They forgot to fix the warp timer here
									Warp.Destination = 0
								end if
							end if
							Warp.Timer = 0
						end if

						Object.OnObject = true
					end if
				else
					Object.OnObject = false
					PlayerObjectCollision(C_TOUCH, -12, -12, 20, 12)
					if CheckResult == true
						CallFunction(PlayerObject_BadnikBreak)
					end if
				end if
			else
				PlayerObjectCollision(C_TOUCH, 12, -12, 32, 12)
				if CheckResult == true
					if Object.OnObject == false
						Object.LaunchDelay = 120

						Player.Animation = ANI_JUMPING
						Player.State = PlayerObject_Blank

						Player.Speed = 0
						Player.XVelocity = 0
						Player.YVelocity = 0

						Player.XPos = Object.XPos
						Player.XPos += 0x180000

						Player.YPos = Object.YPos
						Player.YPos -= 0x30000

						Player.Direction = FACING_LEFT

						if Warp.Destination > 0
							if Warp.Timer > 99
								if Warp.Timer < 220	// They forgot to fix the warp timer here
									Warp.Destination = 0
								end if
							end if
							Warp.Timer = 0
						end if

						Object.OnObject = true
					end if
				else
					Object.OnObject = false
					PlayerObjectCollision(C_TOUCH, -20, -12, 12, 12)
					if CheckResult == true
						CallFunction(PlayerObject_BadnikBreak)
					end if
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	if Object.State < 2	// This is always true
		if Object.DrawOrder == 3
			Object.Frame  = Object.AnimationTimer
			Object.Frame /= 20
			Object.Frame += Object[ArrayPos0].PropertyValue
			DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
			Object.Frame += 2
			SetDrawListEntityRef(Object.EntityNo, 4, Screen[4].DrawListSize)
			Screen[4].DrawListSize++
			Object.DrawOrder = 4
		else
			DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
			Object.DrawOrder = 3
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects.gif")

	SpriteFrame(-24, -12, 24, 24, 107, 206)	// #0 - Scarab Arms frame 0
	SpriteFrame(-32, -12, 32, 24, 132, 206)	// #1 - Scarab Arms frame 1

	SpriteFrame(-32, -12, 56, 24, 165, 206)	// #2 - Scarab frame 0
	SpriteFrame(-32, -12, 56, 24, 165, 231)	// #3 - Scarab frame 1

	SpriteFrame(-24, -12, 24, 24, 107, 206)	// #4 - Scarab Arms frame 0
	SpriteFrame(-32, -12, 32, 16, 67, 240)	// #5 - Scarab Arms frame 1

	SpriteFrame(-32, -12, 56, 24, 165, 206)	// #6 - Scarab frame 0
	SpriteFrame(-40, -12, 64, 24, 100, 231)	// #7 - Scarab frame 1

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Scarab]
			Object[ArrayPos0].XOriginPos = Object[ArrayPos0].XPos
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos

			switch Object[ArrayPos0].PropertyValue
			case PROPVAL_FACING_RIGHT
				Object[ArrayPos0].PropertyValue		= 0	// Set the animation to use un-streched arms
				Object[ArrayPos0].XVelocity			= 0x6000
				Object[ArrayPos0].AnimationSpeed	= 2
				Object[ArrayPos0].Direction			= FACING_RIGHT
				Object[ArrayPos0].LaunchSpeed		= -0xA0000
				break

			case PROPVAL_FACING_LEFT
				Object[ArrayPos0].PropertyValue		= 0	// Set the animation to use un-streched arms
				Object[ArrayPos0].XVelocity			= 0x6000
				Object[ArrayPos0].AnimationSpeed	= 2
				Object[ArrayPos0].Direction			= FACING_LEFT
				Object[ArrayPos0].LaunchSpeed		= 0xA0000
				break

			case PROPVAL_FACING_RIGHT_S
				Object[ArrayPos0].PropertyValue		= 4	// Set the animation to use streched arms
				Object[ArrayPos0].XVelocity			= 0x3000
				Object[ArrayPos0].AnimationSpeed	= 1
				Object[ArrayPos0].Direction			= FACING_RIGHT
				Object[ArrayPos0].LaunchSpeed		= -0x50000
				break

			case PROPVAL_FACING_LEFT_S
				Object[ArrayPos0].PropertyValue		= 4	// Set the animation to use streched arms
				Object[ArrayPos0].XVelocity			= 0x3000
				Object[ArrayPos0].AnimationSpeed	= 1
				Object[ArrayPos0].Direction			= FACING_LEFT
				Object[ArrayPos0].LaunchSpeed		= 0x50000
				break

			case PROPVAL_FACING_RIGHT_W_OBJ
				Object[ArrayPos0].PropertyValue		= 0	// Set the animation to use un-streched arms
				Object[ArrayPos0].XVelocity			= 0x6000
				Object[ArrayPos0].AnimationSpeed	= 2
				Object[ArrayPos0].Direction			= FACING_RIGHT
				Object[ArrayPos0].LaunchSpeed		= -0xA0000
				Object[ArrayPos0].CarriesMonitor	= true
				break
			end switch
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
