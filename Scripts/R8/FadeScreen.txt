//----------------Sonic CD Fade Screen Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.Timer

// States
#alias 0	:	FADESCREEN_SETUP
#alias 1	:	FADESCREEN_AMY
#alias 2	:	FADESCREEN_TAILS
#alias 3	:	FADESCREEN_BLANK
#alias 4	:	FADESCREEN_BOSSRUSH		// Origins Exclusive

// Property Values
#alias 0	:	CUTSCENE_NO_SKIP
#alias 1	:	CUTSCENE_SKIP

// Game modes
#alias 2	:	GAMEMODE_TIMEATTACK

#alias 0	:	OST_JP

// Engine States
#alias 1	:	ENGINE_MAINGAME
#alias 8	:	ENGINE_WAIT


sub ObjectMain
	switch Object.State
	case FADESCREEN_SETUP
		if Object.Timer < 480
			Object.Timer += 4
			Music.Volume--
		else
			Object.Timer = 224
			if Object.PropertyValue == CUTSCENE_NO_SKIP
				Object.State++
			else
				Object.State += 2
			end if
		end if

		if Object.PropertyValue == CUTSCENE_SKIP
			SetScreenFade(0, 0, 0, Object.Timer)
		else
			SetScreenFade(224, 224, 224, Object.Timer)
		end if
		break

	case FADESCREEN_AMY
		if Object.Timer > 0
			Music.Volume--
			Object.Timer -= 16
		else
			StopMusic()

#platform: Use_Origins
			EngineCallback(NOTIFY_BOSS_END)
			TempValue0 = false
			if game.playMode == BOOT_PLAYMODE_BOSSRUSH
				TempValue0 = true
			else
				if game.oneStageFlag != 0
					TempValue0 = true
					EngineCallback(NOTIFY_ACT_FINISH)
					game.callbackResult = -1
					game.callbackParam0 = 1
					game.callbackParam1 = Stage.ListPos
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STAGE_RETRY)
				end if
			end if
#endplatform
#platform: Use_Standalone
			TempValue0 = false
#endplatform
			if TempValue0 != false
				Object.State = FADESCREEN_BOSSRUSH
			else
				if Options.GameMode == GAMEMODE_TIMEATTACK
					TimeAttack.Result = Stage.Seconds
					TimeAttack.Result *= 100
					TempValue0 = Stage.Minutes
					TempValue0 *= 0x1770
					TimeAttack.Result += TempValue0
					TimeAttack.Result += Stage.MilliSeconds
					TempValue0 = Stage.ListPos
					TempValue0  %= 10
					TempValue0 >>= 2
					TempValue1   = Stage.ListPos
					TempValue1  /= 10
					TempValue1  *= 3
					TempValue0  += TempValue1
					TempValue0++
					TempValue1   = Stage.Seconds
					TempValue1  *= 100
					TempValue2   = Stage.Minutes
					TempValue2  *= 0x1770
					TempValue1  += TempValue2
					TempValue1  += Stage.MilliSeconds
					SetLeaderboard(TempValue0, TempValue1)
					Stage.ListPos = 2
				else
					EngineCallback(4)
					if Player.Score > 0
						if Options.GameMode == 1
							if Stage.DebugMode == false
								SetLeaderboard(0, Player.Score)
							end if
						end if
					end if
#platform: Use_Origins
					game.callbackParam0 = StageStatsUsabilityParam1
					game.callbackParam1 = StageStatsUsabilityParam2
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STATS_ENEMY)
					game.callbackParam0 = Object[0].Value0
					EngineCallback(NOTIFY_STATS_RING)
					game.callbackParam0 = StageStatsUsabilityParam5
					EngineCallback(NOTIFY_STATS_PARAM_1)
					game.callbackParam0 = 0
					game.callbackParam1 = StageStatsUsabilityParam4
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STATS_CHARA_ACTION)
					EngineCallback(NOTIFY_ACT_FINISH)
					StageStatsUsabilityParam1 = 0
					StageStatsUsabilityParam2 = 0
					StageStatsUsabilityParam3 = 0
					StageStatsUsabilityParam4 = 0
					StatsUsabilityParam1 = 0
					StatsUsabilityParam2 = 0
					StatsUsabilityParam3 = 0
					StatsUsabilityParam4 = 0

					if SpecialStage.TimeStones > 126	// 0b1111110, each '1' bit represents an obtained time stone
						EngineCallback(NOTIFY_GOOD_ENDING)
					else
						if Good_Future_List > 0x3FFE	// 0b11111111111110, each '1' bit represents a zone with the robot transporters destroyed
							EngineCallback(NOTIFY_GOOD_ENDING)
						end if
					end if
#endplatform

					if SpecialStage.TimeStones > 126	// 0b1111110, each '1' bit represents an obtained time stone
						LoadVideo("Good_Ending")
					else
						if Good_Future_List > 0x3FFE	// 0b11111111111110, each '1' bit represents a zone with the robot transporters destroyed
							LoadVideo("Good_Ending")
						else
							LoadVideo("Bad_Ending")
						end if
					end if

// Because of the way mobile cd worked (using the phone's native video player)
// it needed to reproduce separated videos depending of the soundtrack option
// for the sake of clarity and simplicity, the mobile code got commented out
//					if SpecialStage.TimeStones > 126	// 0b1111110, each '1' bit represents an obtained time stone
//						if Options.Soundtrack == OST_JP
//							LoadVideo("Good_Ending")
//						else
//							LoadVideo("Good_EndingUS")
//						end if
//					else
//						if Good_Future_List > 0x3FFE	// 0b11111111111110, each '1' bit represents a zone with the robot transporters destroyed
//							if Options.Soundtrack == OST_JP
//								LoadVideo("Good_Ending")
//							else
//								LoadVideo("Good_EndingUS")
//							end if
//						else
//							if Options.Soundtrack == OST_JP
//								LoadVideo("Bad_Ending")
//							else
//								LoadVideo("Bad_EndingUS")
//							end if
//						end if
//					end if

					if SaveRAM[36] == false	// Is Tails unlocked?
#platform: Use_Origins
						if game.playMode == BOOT_PLAYMODE_CLASSIC
							Stage.ListPos = 0	// Title Screen
							Engine.State = ENGINE_WAIT
						else
							SaveRAM[36] = true
							WriteSaveRAM()
							Stage.ListPos = 8	// Tails Unlock Screen
						end if
#endplatform

#platform: Use_Standalone
						SaveRAM[36] = true
						WriteSaveRAM()
						Stage.ListPos = 8	// Tails Unlock Screen
#endplatform
					else
						Stage.ListPos = 0	// Title Screen
#platform: Use_Origins
						Engine.State = ENGINE_WAIT
#endplatform
					end if

#platform: Use_Decomp
					// We don't really want to softlock the game don't we?
					if Engine.State == ENGINE_WAIT
						Engine.State = ENGINE_MAINGAME
					end if
#endplatform

#platform: Use_Origins
					game.callbackParam0 = 1
					EngineCallback(NOTIFY_STATS_MOVIE)
#endplatform

#platform: Use_Standalone
					// Originally the game checked if the player was using sonic or tails
					// This got removed on origins, but since it covers this whole thing, this workaround should do
					if Stage.PlayerListPos == 1	// Tails
						Stage.ListPos = 7	// 2011 Remaster Credits
					end if
#endplatform
				end if
				Stage.ActiveList = PRESENTATION_STAGE
				LoadStage()
			end if

		end if
		SetScreenFade(Object.Timer, Object.Timer, Object.Timer, 255)
		break

	case FADESCREEN_TAILS
		if Object.Timer > 0
			Music.Volume--
			Object.Timer -= 16
		else
			StopMusic()
			Stage.ListPos += 2
			LoadStage()
		end if
		break

	case FADESCREEN_BLANK
		break

	case FADESCREEN_BOSSRUSH
		SetScreenFade(255, 255, 255, 255)
		break
	end switch
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
