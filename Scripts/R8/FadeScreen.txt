//----------------Sonic CD Fade Screen Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.Timer

// States
#alias 0	:	FADESCREEN_SETUP
#alias 1	:	FADESCREEN_AMY
#alias 2	:	FADESCREEN_TAILS
#alias 3	:	FADESCREEN_BLANK
#alias 4	:	FADESCREEN_BOSSRUSH		// Origins Exclusive

// Property Values
#alias 0	:	CUTSCENE_NO_SKIP
#alias 1	:	CUTSCENE_SKIP

// Game modes
#alias 2	:	GAMEMODE_TIMEATTACK


sub ObjectMain
	switch Object.State
	case FADESCREEN_SETUP
		if Object.Timer < 480
			Object.Timer += 4
			Music.Volume--
		else
			Object.Timer = 224
			if Object.PropertyValue == CUTSCENE_NO_SKIP
				Object.State++
			else
				Object.State += 2
			end if
		end if

		if Object.PropertyValue == CUTSCENE_SKIP
			SetScreenFade(0, 0, 0, Object.Timer)
		else
			SetScreenFade(224, 224, 224, Object.Timer)
		end if
		break

	case FADESCREEN_AMY
		if Object.Timer > 0
			Music.Volume--
			Object.Timer -= 16
		else
			StopMusic()

#platform: Use_Origins			
			EngineCallback(NOTIFY_BOSS_END)
			TempValue0 = false
			if game.playMode == BOOT_PLAYMODE_BOSSRUSH
				TempValue0 = true
			else
				if game.oneStageFlag != 0
					TempValue0 = true
					EngineCallback(NOTIFY_ACT_FINISH)
					game.callbackResult = -1
					game.callbackParam0 = 1
					game.callbackParam1 = Stage.ListPos
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STAGE_RETRY)
				end if
			end if
#endplatform

			if TempValue0 != false
				Object.State = FADESCREEN_BOSSRUSH
			else
				if Options.GameMode == GAMEMODE_TIMEATTACK
					TimeAttack.Result = Stage.Seconds
					TimeAttack.Result *= 100
					TempValue0 = Stage.Minutes
					TempValue0 *= 0x1770
					TimeAttack.Result += TempValue0
					TimeAttack.Result += Stage.MilliSeconds
					TempValue0 = Stage.ListPos
					TempValue0  %= 10
					TempValue0 >>= 2
					TempValue1   = Stage.ListPos
					TempValue1  /= 10
					TempValue1  *= 3
					TempValue0  += TempValue1
					TempValue0++
					TempValue1   = Stage.Seconds
					TempValue1  *= 100
					TempValue2   = Stage.Minutes
					TempValue2  *= 0x1770
					TempValue1  += TempValue2
					TempValue1  += Stage.MilliSeconds
					SetLeaderboard(TempValue0, TempValue1)
					Stage.ListPos = 2
				else
					EngineCallback(4)
					if Player.Score > 0
						if Options.GameMode == 1
							if Stage.DebugMode == false
								SetLeaderboard(0, Player.Score)
							end if
						end if
					end if
#platform: Use_Origins
					game.callbackParam0 = StageStatsUsabilityParam1
					game.callbackParam1 = StageStatsUsabilityParam2
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STATS_ENEMY)
					game.callbackParam0 = Object[0].Value0
					EngineCallback(NOTIFY_STATS_RING)
					game.callbackParam0 = StageStatsUsabilityParam5
					EngineCallback(NOTIFY_STATS_PARAM_1)
					game.callbackParam0 = 0
					game.callbackParam1 = StageStatsUsabilityParam4
					game.callbackParam2 = 0
					EngineCallback(NOTIFY_STATS_CHARA_ACTION)
					EngineCallback(NOTIFY_ACT_FINISH)
					StageStatsUsabilityParam1 = 0
					StageStatsUsabilityParam2 = 0
					StageStatsUsabilityParam3 = 0
					StageStatsUsabilityParam4 = 0
					StatsUsabilityParam1 = 0
					StatsUsabilityParam2 = 0
					StatsUsabilityParam3 = 0
					StatsUsabilityParam4 = 0

					if SpecialStage.TimeStones > 126
						EngineCallback(NOTIFY_GOOD_ENDING)
					else
						if Good_Future_List > 0x3FFE
							EngineCallback(NOTIFY_GOOD_ENDING)
						end if
					end if
#endplatform
					if SpecialStage.TimeStones > 126
#platform: Standard
						LoadVideo("Good_Ending")
#endplatform
#platform: Mobile
						if Options.Soundtrack == 0
							LoadVideo("Good_Ending")
						else
							LoadVideo("Good_EndingUS")
						end if
#endplatform
					else
						if Good_Future_List > 16382
#platform: Standard
							LoadVideo("Good_Ending")
#endplatform

#platform: Mobile
							if Options.Soundtrack == 0
								LoadVideo("Good_Ending")
							else
								LoadVideo("Good_EndingUS")
							end if
#endplatform
						else
#platform: Standard
							LoadVideo("Bad_Ending")
#endplatform

#platform: Mobile
							if Options.Soundtrack == 0
								LoadVideo("Bad_Ending")
							else
								LoadVideo("Bad_EndingUS")
							end if
#endplatform
						end if
					end if
					
					if SaveRAM[36] == 0
#platform: Use_Origins
						if game.playMode == BOOT_PLAYMODE_CLASSIC
							Stage.ListPos = 0
							Engine.State = 8
						else
							SaveRAM[36] = 1
							WriteSaveRAM()
							Stage.ListPos = 8
						end if
#endplatform

#platform: Use_Standalone
						SaveRAM[36] = 1
						WriteSaveRAM()
						Stage.ListPos = 8
#endplatform
					else
						Stage.ListPos = 0
						Engine.State = 8
					end if
#platform: Use_Origins
					game.callbackParam0 = 1
					EngineCallback(NOTIFY_STATS_MOVIE)
#endplatform

#platform: Use_Standalone
					// Originally the game checked if the player was using sonic or tails
					// This got removed on origins, but since it covers this whole thing, this workaround should do
					if Stage.PlayerListPos == 1
						Stage.ListPos = 7
					end if
#endplatform
				end if
				Stage.ActiveList = PRESENTATION_STAGE
				LoadStage()
			end if

		end if
		SetScreenFade(Object.Timer, Object.Timer, Object.Timer, 255)
		break

	case FADESCREEN_TAILS
		if Object.Timer > 0
			Music.Volume--
			Object.Timer -= 16
		else
			StopMusic()
			Stage.ListPos += 2
			LoadStage()
		end if
		break

	case FADESCREEN_BLANK
		break

	case FADESCREEN_BOSSRUSH
		SetScreenFade(255, 255, 255, 255)
		break
	end switch
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
