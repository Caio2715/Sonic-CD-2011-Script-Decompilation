//----------------Sonic CD HV Platform Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer


sub ObjectMain
	
	switch Object.State
	case 0
	case 8
		if Object.Timer < 30
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break
		
	case 1
	case 2
	case 3
	case 9
	case 10
	case 11
		if Object.Timer < 5
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break
		
	case 4
	case 12
		if Object.Timer < 120
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break
		
	case 5
	case 6
	case 7
	case 13
	case 14
	case 15
		if Object.Timer < 5
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
			Object.State &= 15
		end if
		break
		
	end switch
	
end sub


sub ObjectPlayerInteraction
	
	// Use different hitboxes for the Platform's different sizes
	switch Object.State
	case 0
	case 8
		PlayerObjectCollision(C_BOX, -8, -8, 8, 8)
		break
		
	case 1
	case 7
		PlayerObjectCollision(C_BOX, -24, -8, 24, 8)
		break
		
	case 2
	case 6
		PlayerObjectCollision(C_BOX, -40, -8, 40, 8)
		break
		
	case 3
	case 4
	case 5
		// Tallest version
		PlayerObjectCollision(C_BOX, -56, -8, 56, 8)
		break
		
	case 9
	case 15
		PlayerObjectCollision(C_BOX, -8, -24, 8, 24)
		break
		
	case 10
	case 14
		PlayerObjectCollision(C_BOX, -8, -40, 8, 40)
		break
		
	case 11
	case 12
	case 13
		// Widest version
		PlayerObjectCollision(C_BOX, -8, -56, 8, 56)
		break
		
	end switch
	
end sub


sub ObjectDraw
	
	// Draw the Base Post Frame
	DrawSprite(0)
	
	// And then depending on the Platform's current state, 
	// draw all the extended Platform frames as needed
	switch Object.State
	case 1
	case 7
		DrawSprite(3)
		DrawSprite(4)
		break
		
	case 2
	case 6
		DrawSprite(2)
		DrawSprite(3)
		DrawSprite(4)
		DrawSprite(5)
		break
		
	case 3
	case 4
	case 5
		// Fully extended, vertically
		DrawSprite(1)
		DrawSprite(2)
		DrawSprite(3)
		DrawSprite(4)
		DrawSprite(5)
		DrawSprite(6)
		break
		
	case 9
	case 15
		DrawSprite(9)
		DrawSprite(10)
		break
		
	case 10
	case 14
		DrawSprite(8)
		DrawSprite(9)
		DrawSprite(10)
		DrawSprite(11)
		break
		
	case 11
	case 12
	case 13
		// Fully extended, horizontally
		DrawSprite(7)
		DrawSprite(8)
		DrawSprite(9)
		DrawSprite(10)
		DrawSprite(11)
		DrawSprite(12)
		break
		
	end switch
	
end sub


sub ObjectStartup
	
	LoadSpriteSheet("R8/Objects.gif")
	
	// Base Post Frame
	SpriteFrame(-8, -8, 16, 16, 140, 123)
	
	// All the extension frames
	SpriteFrame(-56, -8, 16, 16, 75, 200)
	SpriteFrame(-40, -8, 16, 16, 75, 200)
	SpriteFrame(-24, -8, 16, 16, 75, 200)
	SpriteFrame(8, -8, 16, 16, 75, 200)
	SpriteFrame(24, -8, 16, 16, 75, 200)
	SpriteFrame(40, -8, 16, 16, 75, 200)
	SpriteFrame(-8, -56, 16, 16, 75, 200)
	SpriteFrame(-8, -40, 16, 16, 75, 200)
	SpriteFrame(-8, -24, 16, 16, 75, 200)
	SpriteFrame(-8, 8, 16, 16, 75, 200)
	SpriteFrame(-8, 24, 16, 16, 75, 200)
	SpriteFrame(-8, 40, 16, 16, 75, 200)
	
	// Find all HV Platform objects in the level
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[HV Platform]
			
			// Start the Platform at fully extended horizontally
			Object[ArrayPos0].State = 12
			
		end if
		
		ArrayPos0++
	loop
	
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = 0
	while TempValue0 < 6
		DrawSprite(TempValue0); TempValue0++
	loop
end sub

sub RSDKLoad
	LoadSpriteSheet("R8/Objects.gif")
	SpriteFrame(-8, -8, 16, 16, 140, 123)
	SpriteFrame(-8, -56, 16, 16, 75, 200)
	SpriteFrame(-8, -40, 16, 16, 75, 200)
	SpriteFrame(-8, -24, 16, 16, 75, 200)
	SpriteFrame(-8, 8, 16, 16, 75, 200)
	SpriteFrame(-8, 24, 16, 16, 75, 200)
	SpriteFrame(-8, 40, 16, 16, 75, 200)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
