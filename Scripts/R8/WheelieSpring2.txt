//-------------Sonic CD Wheelie Spring 2 Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

#alias Player.Value7	:	Player.AnimationCheck

// States
#alias 0	:	WHEELIESPRING2_MOVING_RIGHT
#alias 1	:	WHEELIESPRING2_MOVING_LEFT

// Player Animations
#alias 11	:	ANI_BOUNCING

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	if Object.State == WHEELIESPRING2_MOVING_RIGHT

		if Object.Timer < Object.PropertyValue
			Object.Timer++

			Object.XPos += 0x18000
		else
			Object.State = WHEELIESPRING2_MOVING_LEFT

			Object.Timer = Object.PropertyValue
			FlipSign(Object.Timer)
		end if
	else
		if Object.Timer < Object.PropertyValue
			Object.Timer++

			Object.XPos -= 0x18000
		else
			Object.State = WHEELIESPRING2_MOVING_RIGHT

			Object.Timer = Object.PropertyValue
			FlipSign(Object.Timer)
		end if
	end if
	ObjectTileGrip(CSIDE_FLOOR, 0, 15, 0)
end sub


sub ObjectPlayerInteraction
	if Player.State != PlayerObject_Blank
		if Player.YVelocity >= 0
			PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
			PlayerObjectCollision(C_TOUCH, -15, -18, 15, -12)
			if CheckResult == true

				Object.Frame = 1
		
#platform: Use_Origins
				Player.State = PlayerObject_ResetDropDash
#endplatform

#platform: Use_Standalone
				Player.State = PlayerObject_HandleAir
#endplatform
				Player.Gravity = GRAVITY_AIR
				Player.YVelocity = -0x100000
				Player.AnimationCheck = ANI_WALKING
				if Player.Animation == ANI_RUNNING
					Player.AnimationCheck = ANI_RUNNING
				end if
				if Player.Animation == ANI_PEELOUT
					Player.AnimationCheck = ANI_PEELOUT
				end if
				Player.Animation = ANI_BOUNCING
				Player.Timer = 0
				PlaySfx(11, 0)
					
	#platform: Use_Haptics
				HapticEffect(9, 0, 0, 0)
	#endplatform

			end if
		end if

		if Player.Gravity == GRAVITY_GROUND
			PlayerObjectCollision(C_BOX, -16, -20, 16, 16)
			PlayerObjectCollision(C_TOUCH, -15, -18, 15, -12)
			if CheckResult == true
				Object.Frame = 1
#platform: Use_Origins
				Player.State = PlayerObject_ResetDropDash
#endplatform

#platform: Use_Standalone
				Player.State = PlayerObject_HandleAir
#endplatform
				Player.Gravity = GRAVITY_AIR
				Player.YVelocity = -0x100000
				Player.Animation = ANI_BOUNCING
				Player.Timer = 0
				PlaySfx(11, 0)
					
	#platform: Use_Haptics
				HapticEffect(9, 0, 0, 0)
	#endplatform

			end if
		end if
	end if
end sub


sub ObjectDraw
	if Object.Frame == 0
		DrawSprite(0)
	else
		Object.Frame++

		TempValue0 = Object.Frame
		TempValue0 >>= 1
		switch TempValue0
		case 0
		case 1
			DrawSprite(0)
			break

		case 2
		case 6
			DrawSprite(1)
			break

		case 3
		case 4
		case 5
			DrawSprite(2)
			break

		end switch

		if Object.Frame > 12
			Object.Frame = 0
		end if
	end if

	TempValue0 = Object.Timer
	TempValue0 += Object.PropertyValue
	TempValue0 %= 18
	TempValue0 /= 9
	TempValue0 += 3

	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	SpriteFrame(-16, -16, 32, 16, 84, 1)	// #0 - Red Spring
	SpriteFrame(-16, -8, 32, 8, 84, 1)		// #1 - Red Spring Head
	SpriteFrame(-16, -40, 32, 40, 84, 18)	// #2 - Red Spring Launch
	SpriteFrame(-8, 0, 16, 16, 233, 0)		// #3 - Wheel Frame 0
	SpriteFrame(-8, 0, 16, 16, 233, 17)		// #4 - Wheel Frame 1
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
