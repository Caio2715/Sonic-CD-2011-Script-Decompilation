//------------Sonic CD Tube Switch Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 61: TYPE_TUBESWITCH

#alias Object.Value0 : Object.InTube

// Player Aliases
#alias Player.Value2 : Player.CustomAnimSpeed
#alias Player.Value6 : Player.MinRollSpeed

// Warp Star Aliases
#alias Object.Value0    : WarpStar.Timer
#alias Object.DrawOrder : WarpStar.DrawOrder


// 0 - standard (biased towards right)
// 1 - standard (biased towards left)
// 2 - downards sender
// 3 - downards conveyer tube 
// 4 - upwards conveyer tube
// 5 - XXXXXX
// 7 - vertical tunnel, middle
// 8 - vertical tunnel, endpoints


sub ObjectPlayerInteraction

	PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
	if CheckResult == true
		switch Object.PropertyValue
		case 0
			if Player.Speed >= 0
				if Player.State != PlayerObject_Tunnel
					PlaySfx(6, 0)
				end if
				Player.Direction = FACING_RIGHT
				Player.State = PlayerObject_Tunnel
				Player.Animation = ANI_JUMPING
				Player.MinRollSpeed = 131072
				if Player.Speed < 131072
					Player.Speed = 131072
				end if
			else
				if Player.Speed > -131072
					Player.Speed = -131072
				end if
				Player.State = PlayerObject_HandleRolling
				Player.Animation = ANI_JUMPING
			end if
			break

		case 1
			if Player.Speed <= 0
				if Player.State != PlayerObject_Tunnel
					PlaySfx(6, 0)
				end if
				Player.State = PlayerObject_Tunnel
				Player.Animation = ANI_JUMPING
				Player.MinRollSpeed = 524288
				if Player.Speed > -131072
					Player.Speed = -131072
				end if
			else
				if Player.Speed < 131072
					Player.Speed = 131072
				end if
				Player.State = PlayerObject_HandleRolling
				Player.Animation = ANI_JUMPING
			end if
			break

		case 2
			Player.Gravity = 1
			Player.CollisionMode = 0
			Player.XVelocity = 0
			Player.Speed = 0
			break

		case 3
			if ConveyorBelt_Flag == 0
				if Player.YPos < Object.YPos
					if Player.State != PlayerObject_Blank
						PlaySfx(7, 0)
					end if
					Player.State = PlayerObject_Blank
					if Player.Animation != ANI_JUMPING
						Player.Animation = ANI_JUMPING
					end if
					Player.CustomAnimSpeed = 240
					Player.Speed = 0
					Player.Timer = 0
					Player.XPos = Object.XPos
					Player.XVelocity = 0
					Player.YVelocity = 655360
				end if
			else
				if Player.State == PlayerObject_Blank
					Player.State = PlayerObject_HandleAir
				end if
			end if
			break

		case 4
			if ConveyorBelt_Flag == 3
				if Player.YPos > Object.YPos
#platform: Mobile
					if Warp.Destination > 0
						if Warp.Timer == 0
							Warp.Timer = 1

							ResetObjectEntity(3, TypeName[Warp Star], 0, Player.XPos, Player.YPos)
							WarpStar[3].Timer = 7
							WarpStar[3].DrawOrder = 4
						end if
					end if
#endplatform

					if Player.State != PlayerObject_Blank
						PlaySfx(7, 0)
					end if
					Player.State = PlayerObject_Blank
					if Player.Animation != ANI_JUMPING
						Player.Animation = ANI_JUMPING
					end if
					Player.CustomAnimSpeed = 240
					Player.Speed = 0
					Player.Timer = 0
					Player.XPos = Object.XPos
					Player.XVelocity = 0
					Player.YVelocity = -524288
				end if
			else
				if Player.State == PlayerObject_Blank
					Player.State = PlayerObject_HandleAir
				end if
			end if
			break

		case 5
			if Player.YVelocity < 0
				Player.Gravity = 0
				Player.State = PlayerObject_Tunnel
				Player.Animation = ANI_JUMPING
				Player.CollisionMode = 3
				Player.Angle = 64
				Player.Speed = -655360
				Player.MinRollSpeed = 655360
			end if
			break

		case 6
			if Player.YVelocity > 0
				Player.Gravity = 0
				Player.State = PlayerObject_Tunnel
				Player.Animation = ANI_JUMPING
				Player.CollisionMode = 3
				Player.Angle = 64
				Player.Speed = Player.YVelocity
				Player.MinRollSpeed = 655360
			end if
			break

		case 7
			Player.Gravity = 0
			Player.State = PlayerObject_Tunnel
			Player.Animation = ANI_JUMPING
			if Player.CollisionMode != 3
				Player.CollisionMode = 3
				Player.Angle = 64
			end if

			if ConveyorBelt_Flag == 3
				Player.Speed = -851968
				Player.MinRollSpeed = 851968
			else
				Player.Speed = 655360
				Player.MinRollSpeed = 655360
			end if
			break

		case 8
			if ConveyorBelt_Flag == 3
				Player.Gravity = 0
				Player.State = PlayerObject_Tunnel
				Player.Animation = ANI_JUMPING
				Player.CollisionMode = 3
				Player.Angle = 64
				Player.Speed = -851968
				Player.MinRollSpeed = 851968
				if Object.InTube == false
					PlaySfx(7, 0)
				end if
			end if
			break

		end switch

		Object.InTube = true
	else
		Object.InTube = false
	end if

end sub


// ========================
// Editor Subs
// ========================

// TODO: gonna hold off on doing editor stuff for this one, wanna wait 'til i can actually see everything before i start doing stuff
// basic notes are labelled up above through if someone else wants to do it

sub RSDKDraw
	// Draw a square out of Tube Switch icons

	TempValue0 = Object.XPos
	TempValue0 -= 0x80000
	TempValue1 = Object.YPos
	TempValue1 -= 0x80000
	DrawSpriteXY(0, TempValue0, TempValue1)
	TempValue0 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)
	TempValue1 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)
	TempValue0 -= 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)
	
	if Editor.ShowGizmos == true
		TempValue0 = Object.XPos
		TempValue0 -= 8
		TempValue1 = Object.YPos
		TempValue1 -= 8
		DrawRectOutline(TempValue0, TempValue1, 16, 16, 255, 255, 255, 255)
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67) // "T" (ubeswitch) icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
