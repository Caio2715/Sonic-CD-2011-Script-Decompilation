//------------Sonic CD Title Card Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.BarPos
#alias Object.Value1 : Object.TextPos
#alias Object.Value2 : Object.TextSize
#alias Object.Value3 : Object.Timer
#alias Object.Value4 : Object.FadeR
#alias Object.Value5 : Object.FadeG
#alias Object.Value6 : Object.FadeB
#alias Object.Value7 : Object.CharPos

#alias 0 : TITLECARD_FADEIN
#alias 1 : TITLECARD_SLIDEIN
#alias 2 : TITLECARD_DISPLAY
#alias 3 : TITLECARD_SLIDEOUT

// Player aliases
#alias Object.Value4 : Player.Invincible

#alias 0 : PLAYER_SONIC

// ControlMode Aliases
#alias -1 : CONTROLMODE_NONE
#alias  0 : CONTROLMODE_NORMAL

sub ObjectPlayerInteraction
	Player.Timer = 0
end sub


sub ObjectDraw
	
	switch Object.State
	case TITLECARD_FADEIN
		// Lock Player Control
		Player.ControlMode = CONTROLMODE_NONE
		
		// Just in case, manually clear each and every one of the Player's Control values too
		Player.Up		 = false
		Player.Down		 = false
		Player.Left		 = false
		Player.Right	 = false
		Player.JumpPress = false
		Player.JumpHold	 = false
		
		// If the Player isn't warping, then reset their Speed
		if Warp.XPos < 1
			Player.Speed = 0
		end if
		
		if Object.Timer > 0
			if Object.Timer == 256
				PlayMusic(0)
				if Warp.XPos > 0
					Player.XPos = Warp.XPos
					Player.YPos = Warp.YPos
					Player.XVelocity = Warp.XVelocity
					Player.YVelocity = Warp.YVelocity
					Player.Speed = Warp.XVelocity
					Player.State = Warp.State
					Stage.MilliSeconds = Rec_Milliseconds
					Stage.Seconds = Rec_Seconds
					Stage.Minutes = Rec_Minutes
					Player[0].Invincible = 8000
				end if
				
				Warp.Timer = 0
				Warp.Frame = 0
			end if
			
			SetScreenFade(Object.FadeR, Object.FadeG, Object.FadeB, Object.Timer)
			Object.Timer -= 8
		else
			Fade_Colour = 0
			CheckEqual(Warp.XPos, 0)
			TempValue0 = CheckResult
			CheckLower(LampPost.Check, 32)
			TempValue0 &= CheckResult
			if TempValue0 == true
				Object.State =  TITLECARD_SLIDEIN
				Object.Timer = 0
			else
				Player.ControlMode = CONTROLMODE_NORMAL
				Stage.TimeEnabled = true
				ResetObjectEntity(20, TypeName[Blank Object], 0, 0, 0)
				Warp.XPos = 0
			end if
		end if
		break
		
	case TITLECARD_SLIDEIN
		TempValue0 = 144
		TempValue0 += Object.CharPos
		if Object.TextPos > TempValue0
			Object.TextPos -= 8
		else
			if Object.BarPos == 0
				Object.State = TITLECARD_DISPLAY
			end if
		end if
		
		if Object.BarPos < 0
			Object.BarPos += 8
		end if
		break
		
	case TITLECARD_DISPLAY
		if Object.Timer == 90
			Object.State = TITLECARD_SLIDEOUT
		else
			Object.Timer++
		end if
		break
		
	case TITLECARD_SLIDEOUT
		if Object.TextPos < 408
			Object.TextPos += 16
			Object.BarPos -= 16
		else
			Player.ControlMode = CONTROLMODE_NORMAL
			Stage.TimeEnabled = true
			ResetObjectEntity(20, TypeName[Blank Object], 0, 0, 0)
			Warp.XPos = 0
		end if
		break
		
	end switch
	
	if Object.Type > TypeName[Blank Object]
		TempValue0 = 136
		TempValue0 += Object.CharPos
		DrawSpriteScreenXY(0, TempValue0, Object.BarPos)
		DrawSpriteScreenXY(Stage.ActNo, Object.TextPos, 80)
		DrawSpriteScreenXY(4, Object.TextPos, 80)
		DrawSpriteScreenXY(5, Object.TextPos, 80)
		DrawSpriteScreenXY(6, Object.TextPos, 80)

#platform: Use_Decomp	// Creates a black rectangle that fills the empty space on wider resolutions
		Tempvalue0 = Object.TextPos
		TempValue0 += 220
		DrawRect(TempValue0,163,Screen.XSize,9,0,0,0,255)
#endplatform

		DrawSpriteScreenXY(7, Object.TextPos, 80)
		TempValue0 = 8
		while TempValue0 < Object.TextSize
			DrawSpriteScreenXY(TempValue0, Object.TextPos, 80)
			TempValue0++
		loop
	end if
end sub


sub ObjectStartup
	if Stage.PlayerListPos == 0	// PlayerName[SONIC]
		LoadSpriteSheet("Global/Display.gif")
	else
		LoadSpriteSheet("Global/Display_t.gif")
	end if
	
	if Options.Soundtrack == JP_OST
		switch HUD.TimePeriod
		case PROPVAL_PRESENT
			SetMusicTrack("JP/R7A.ogg", 0, true)
			break
		case PROPVAL_PAST
			SetMusicTrack("R7B.ogg", 0, 730678)
			break
		case PROPVAL_GOOD_FUTURE
			SetMusicTrack("JP/R7C.ogg", 0, 103060)
			break
		case PROPVAL_BAD_FUTURE
			SetMusicTrack("JP/R7D.ogg", 0, true)
			break
		end switch
	else
		switch HUD.TimePeriod
		case PROPVAL_PRESENT
			SetMusicTrack("US/R7A.ogg", 0, 617728)
			break
		case PROPVAL_PAST
			SetMusicTrack("R7B.ogg", 0, 730678)
			break
		case PROPVAL_GOOD_FUTURE
			SetMusicTrack("US/R7C.ogg", 0, 136670)
			break
		case PROPVAL_BAD_FUTURE
			SetMusicTrack("US/R7D.ogg", 0, 92324)
			break
		end switch
	end if
	
	// Set the Flower Object Type No
	Flower_TypeNo = TypeName[Flower]
	
	// Create the Title Card Object
	Object[20].Type = TypeName[TitleCard]
	
	// Different Fade-In times depending on if this is a new level load or if the Player is warping in
	if Warp.XPos > 0
		Object[20].Timer = 256
	else
		Object[20].Timer = 384
	end if
	
	// In the Metal Sonic race Zone, set a Roof to be active
	if Stage.ActNo == 3
		Player.RoofBarrier = true
	end if
	
	// Set the Title Card to always be active
	Object[20].Priority = 1
	
	Object[20].DrawOrder = 6
	
	// Set initial Bar & Text Positions
	Object[20].BarPos = -216
	Object[20].TextPos = 336
	
	// And then since 
	Object[20].CharPos = Screen.CenterX
	Object[20].CharPos -= 160
	
	if Object[20].CharPos > 3
		Object[20].CharPos -= 4
	end if
	Object[20].CharPos &= 248
	Object[20].TextPos += Object[20].CharPos
	Object[20].TextPos += Object[20].CharPos
	Object[20].TextSize = 16
	Object[20].TextSize += 8
	Object[20].FadeR = Fade_Colour
	Object[20].FadeR >>= 16
	Object[20].FadeG = Fade_Colour
	Object[20].FadeG &= 65280
	Object[20].FadeG >>= 8
	Object[20].FadeB = Fade_Colour
	Object[20].FadeB &= 255
	SpriteFrame(0, 0, 32, 200, 223, 1)
	SpriteFrame(96, 96, 48, 48, 41, 1)
	SpriteFrame(96, 96, 48, 48, 90, 1)
	SpriteFrame(96, 96, 48, 48, 139, 1)
	SpriteFrame(64, 97, 48, 16, 41, 67)
	
	// Workaround for steam datafile to show this sprite correctly
#platform: Use_Origins
	SpriteFrame(72, 81, 160, 16, 49, 50)	// #5  - Sonic The Hedgehog CD
#endplatform

#platform: Use_Standalone
	#platform: Mobile
		SpriteFrame(72, 81, 160, 16, 49, 50)	// #5  - Sonic The Hedgehog CD
	#endplatform

	#platform: Standard
		SpriteFrame(64, 81, 160, 16, 41, 50)	// #5  - Sonic The Hedgehog CD
	#endplatform
#endplatform

	SpriteFrame(16, 81, 160, 16, 41, 50)
	SpriteFrame(100, 32, 24, 48, 188, 1)
	SpriteFrame(0, 0, 16, 55, 142, 133)
	SpriteFrame(16, 32, 10, 23, 81, 108)
	SpriteFrame(26, 32, 11, 23, 43, 84)
	SpriteFrame(38, 32, 12, 23, 57, 108)
	SpriteFrame(51, 32, 11, 23, 76, 84)
	SpriteFrame(63, 32, 11, 23, 92, 108)
	SpriteFrame(75, 32, 10, 23, 70, 108)
	SpriteFrame(85, 32, 10, 23, 81, 108)
	SpriteFrame(0, 56, 16, 55, 142, 133)
	SpriteFrame(16, 56, 10, 23, 211, 84)
	SpriteFrame(26, 56, 9, 23, 88, 84)
	SpriteFrame(35, 56, 9, 23, 88, 84)
	SpriteFrame(44, 56, 11, 23, 76, 84)
	SpriteFrame(55, 56, 17, 23, 116, 108)
	SpriteFrame(73, 56, 11, 23, 43, 84)
	SpriteFrame(84, 56, 11, 23, 146, 108)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
