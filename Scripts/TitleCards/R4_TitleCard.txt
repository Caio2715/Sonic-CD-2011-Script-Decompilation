//------------Sonic CD TitleCard Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.BarPos
#alias Object.Value1			:	Object.TextPos
#alias Object.Value2			:	Object.TextSize
#alias Object.Value3			:	Object.FadeA
#alias Object.Value4			:	Object.FadeR
#alias Object.Value5			:	Object.FadeG
#alias Object.Value6			:	Object.FadeB
#alias Object.Value7			:	Object.CharPos

// Player Alias
#alias Object[0].Value4			:	Player.Invincible

// States
#alias 0			:	TITLECARD_FADEIN
#alias 1			:	TITLECARD_SLIDEIN
#alias 2			:	TITLECARD_DISPLAY
#alias 3			:	TITLECARD_SLIDEOUT

// OST
#alias 0			:	JP_OST

// HUD aliases
#alias Object[24].PropertyValue	:	HUD.TimePeriod

// Time Periods
#alias 0			:	PROPVAL_PRESENT
#alias 1			:	PROPVAL_PAST
#alias 2			:	PROPVAL_GOOD_FUTURE
#alias 3			:	PROPVAL_BAD_FUTURE

// Priority
#alias 1			:	PRIORITY_ACTIVE


sub ObjectPlayerInteraction
	Player.Timer = 0
end sub


sub ObjectDraw
	switch Object.State
	case TITLECARD_FADEIN
		Player.ControlMode	= -1
		Player.Up			= false
		Player.Down			= false
		Player.Left			= false
		Player.Right		= false
		Player.JumpPress	= false
		Player.JumpHold		= false
		if Warp.XPos < 1
			Player.Speed = 0
		end if

		if Object.FadeA > 0
			if Object.FadeA == 256
				PlayMusic(0)
				if Warp.XPos > 0
					Player.XPos = Warp.XPos
					Player.YPos = Warp.YPos
					
					Player.XVelocity = Warp.XVelocity
					Player.YVelocity = Warp.YVelocity
					Player.Speed = Warp.XVelocity

					Player.State = Warp.State

					Stage.MilliSeconds = Rec_Milliseconds
					Stage.Seconds = Rec_Seconds
					Stage.Minutes = Rec_Minutes

					Player.Invincible = 8000
				end if
				Warp.Timer = 0
				Warp.Frame = 0
			end if

			SetScreenFade(Object.FadeR, Object.FadeG, Object.FadeB, Object.FadeA)
			Object.FadeA -= 8
		else
			Fade_Colour = 0

			CheckEqual(Warp.XPos, 0)
			TempValue0 = CheckResult
			CheckLower(LampPost.Check, 32)
			TempValue0 &= CheckResult
			if TempValue0 == true
				Object.State = TITLECARD_SLIDEIN
				Object.FadeA = 0
			else
				Player.ControlMode = 0
				Stage.TimeEnabled = true
				ResetObjectEntity(20, TypeName[Blank Object], 0, 0, 0)
				Warp.XPos = 0
			end if
		end if
		break

	case TITLECARD_SLIDEIN
		TempValue0 = 144
		TempValue0 += Object.CharPos
		if Object.TextPos > TempValue0
			Object.TextPos -= 8
		else
			if Object.BarPos == 0
				Object.State = TITLECARD_DISPLAY
			end if
		end if
		if Object.BarPos < 0
			Object.BarPos += 8
		end if
		break

	case TITLECARD_DISPLAY
		if Object.FadeA == 90
			Object.State = TITLECARD_SLIDEOUT
		else
			Object.FadeA++
		end if
		break

	case TITLECARD_SLIDEOUT
		if Object.TextPos < 408
			Object.TextPos += 16
			Object.BarPos -= 16
		else
			Player.ControlMode = 0
			Stage.TimeEnabled = true
			ResetObjectEntity(20, TypeName[Blank Object], 0, 0, 0)
			Warp.XPos = 0
		end if
		break
	end switch

	if Object.Type > TypeName[Blank Object]
		TempValue0 = 136
		TempValue0 += Object.CharPos
		DrawSpriteScreenXY(0, TempValue0, Object.BarPos)
		DrawSpriteScreenXY(Stage.ActNo, Object.TextPos, 80)
		DrawSpriteScreenXY(4, Object.TextPos, 80)
		DrawSpriteScreenXY(5, Object.TextPos, 80)
		DrawSpriteScreenXY(6, Object.TextPos, 80)

#platform: Use_Decomp	// Creates a black rectangle that fills the empty space on wider resolutions
		Tempvalue0 = Object.TextPos
		TempValue0 += 220
		DrawRect(TempValue0,163,Screen.XSize,9,0,0,0,255)
#endplatform

		DrawSpriteScreenXY(7, Object.TextPos, 80)
		TempValue0 = 8
		while TempValue0 < Object.TextSize
			DrawSpriteScreenXY(TempValue0, Object.TextPos, 80)
			TempValue0++
		loop
	end if
end sub


sub ObjectStartup
	if Stage.PlayerListPos == 0	// PlayerName[SONIC]
		LoadSpriteSheet("Global/Display.gif")
	else
		LoadSpriteSheet("Global/Display_t.gif")
	end if
	
	if Options.Soundtrack == JP_OST
		switch HUD.TimePeriod
		case PROPVAL_PRESENT
			SetMusicTrack("JP/R4A.ogg", 0, 1)
			break
		case PROPVAL_PAST
			SetMusicTrack("R4B.ogg", 0, 1)
			break
		case PROPVAL_GOOD_FUTURE
			SetMusicTrack("JP/R4C.ogg", 0, 1)
			break
		case PROPVAL_BAD_FUTURE
			SetMusicTrack("JP/R4D.ogg", 0, 1)
			break
		end switch
	else
		switch HUD.TimePeriod
		case PROPVAL_PRESENT
			SetMusicTrack("US/R4A.ogg", 0, 415148)
			break
		case PROPVAL_PAST
			SetMusicTrack("R4B.ogg", 0, 1)
			break
		case PROPVAL_GOOD_FUTURE
			SetMusicTrack("US/R4C.ogg", 0, 1)
			break
		case PROPVAL_BAD_FUTURE
			SetMusicTrack("US/R4D.ogg", 0, 145616)
			break
		end switch
	end if

	Flower_TypeNo = TypeName[Flower]
	Object[20].Type = TypeName[TitleCard]

	if Warp.XPos > 0
		Object[20].FadeA = 256
	else
		Object[20].FadeA = 384
	end if

	Object[20].Priority = PRIORITY_ACTIVE

	Object[20].DrawOrder = 6

	Object[20].BarPos = -216
	Object[20].TextPos = 336
	Object[20].CharPos = Screen.CenterX
	Object[20].CharPos -= 160
	if Object[20].CharPos > 3
		Object[20].CharPos -= 4
	end if
	Object[20].CharPos &= 248
	Object[20].TextPos += Object[20].CharPos
	Object[20].TextPos += Object[20].CharPos
	Object[20].TextSize = 12
	Object[20].TextSize += 8
	Object[20].FadeR = Fade_Colour
	Object[20].FadeR >>= 16
	Object[20].FadeG = Fade_Colour
	Object[20].FadeG &= 65280
	Object[20].FadeG >>= 8
	Object[20].FadeB = Fade_Colour
	Object[20].FadeB &= 255

	SpriteFrame(0, 0, 32, 200, 223, 1)		// #0  - Bar
	SpriteFrame(96, 96, 48, 48, 41, 1)		// #1  - Zone 1 Circle
	SpriteFrame(96, 96, 48, 48, 90, 1)		// #2  - Zone 2 Circle
	SpriteFrame(96, 96, 48, 48, 139, 1)		// #3  - Zone 3 Circle
	SpriteFrame(64, 97, 48, 16, 41, 67)		// #4  - Zone Text
	
	// Workaround for steam datafile to show this sprite correctly
#platform: Use_Origins
	SpriteFrame(72, 81, 160, 16, 49, 50)	// #5  - Sonic The Hedgehog CD
#endplatform

#platform: Use_Standalone
	#platform: Standard
		SpriteFrame(64, 81, 160, 16, 41, 50)	// #5  - Sonic The Hedgehog CD
	#endplatform

	#platform: Mobile
		SpriteFrame(72, 81, 160, 16, 49, 50)	// #5  - Sonic The Hedgehog CD
	#endplatform
#endplatform

	SpriteFrame(16, 81, 160, 16, 41, 50)	// #6  - Sonic CD Bar Extension
	SpriteFrame(100, 32, 24, 48, 188, 1)	// #7  - Three Lines
	// Round Name
	SpriteFrame(0, 0, 16, 56, 83, 132)		// #8  - T
	SpriteFrame(12, 33, 6, 23, 169, 108)	// #9  - I
	SpriteFrame(19, 33, 11, 23, 76, 84)		// #10 - D
	SpriteFrame(31, 33, 11, 23, 43, 84)		// #11 - A
	SpriteFrame(43, 33, 9, 23, 159, 84)		// #12 - L

	SpriteFrame(0, 56, 16, 56, 83, 132)		// #13 - T
	SpriteFrame(16, 57, 9, 23, 88, 84)		// #14 - E
	SpriteFrame(26, 57, 17, 23, 169, 84)	// #15 - M
	SpriteFrame(44, 57, 10, 23, 211, 84)	// #16 - P
	SpriteFrame(54, 57, 9, 23, 88, 84)		// #17 - E
	SpriteFrame(63, 57, 10, 23, 70, 108)	// #18 - S
	SpriteFrame(73, 57, 10, 23, 81, 108)	// #19 - T
	
	if Stage.ActNo == 1
		Player.RoofBarrier = true
	end if
	
#platform: Use_Origins
	if game.playMode == BOOT_PLAYMODE_MISSION
		TempValue0 = true
	else
		if game.playMode == BOOT_PLAYMODE_BOSSRUSH
			TempValue0 = true
		else
			TempValue0 = false
		end if
	end if
	
	if TempValue0 != false
		PlayMusic(0)
		game.missionCondition = 0
		game.missionEnd = false
		Player.ControlMode = 0
		Stage.TimeEnabled = true
		ResetObjectEntity(20, TypeName[Blank Object], 0, 0, 0)
		Warp.XPos = 0
	end if
#endplatform
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
