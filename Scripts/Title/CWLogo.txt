//------------Sonic CD CWLogo Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//---This Object is normally only used by Mobile Data files---//

// Aliases
#alias Object.Value0	: Object.Timer
#alias Object.Value1	: Object.RectAlpha	//Only used in mobile
#alias Object.Value2 	: Object.BGScale

#alias Object.Rotation	: Object.Rotation
#alias Object.Alpha		: Object.Alpha
#alias Object.Scale		: Object.Scale

//-------States--------//
#alias 0 : CWLOGO_SETUP
#alias 1 : CWLOGO_BGSCALE_INCREASE_1
#alias 2 : CWLOGO_BGSCALE_INCREASE_2
#alias 3 : CWLOGO_LOGO_FADEIN
#alias 4 : CWLOGO_BGSCALE_DECREASE
#alias 5 : CWLOGO_LOGO_FADEOUT

//--Callbacks & Engine States--//
#alias 7 : CALLBACK_EXIT_SELECTED

#alias 6 : ENGINE_EXIT_HIRESMODE

sub ObjectMain
	switch Object.State
	case CWLOGO_SETUP
		Object.Rotation = 0
		Object.BGScale = 0
		Object.State++
		break

	case CWLOGO_BGSCALE_INCREASE_1
		if Object.BGScale < 2048
			if Object.BGScale < 2048
				Object.BGScale += 64
			else
				Object.BGScale += 128
			end if
		else
			Object.RectAlpha = 256
			Object.State++
		end if
		Object.Rotation += 8
		Object.Rotation &= 511
		break

	case CWLOGO_BGSCALE_INCREASE_2
		if Object.BGScale < 4096
			if Object.BGScale < 2048
				Object.BGScale += 64
			else
				Object.BGScale += 128
			end if
			Object.Rotation += 8
			Object.Rotation &= 511
		end if
		if Object.RectAlpha > 0
			Object.RectAlpha -= 8
		else
			Object.State++
		end if
		break

	case CWLOGO_LOGO_FADEIN
		if Object.Timer < 144
			Object.Timer++
		else
			Object.Timer = 0
#platform: Mobile
			Object.Value3 = 0	//Unknown Use
#endplatform
			Object.State++
		end if
		break

	case CWLOGO_BGSCALE_DECREASE
		if Object.BGScale > 0
			if Object.BGScale < 2048
				Object.BGScale -= 64
			else
				Object.BGScale -= 128
			end if
			Object.Rotation -= 8
			if Object.Rotation < 0
				Object.Rotation += 512
			end if
		end if
		if Object.RectAlpha < 256
			Object.RectAlpha += 10
		else
			Object.State++
			Object.RectAlpha = 256
		end if
		break

	case CWLOGO_LOGO_FADEOUT
		if Object.BGScale > 0
			if Object.BGScale < 2048
				Object.BGScale -= 64
			else
				Object.BGScale -= 128
			end if
			Object.Rotation -= 8
			if Object.Rotation < 0
				Object.Rotation += 512
			end if
		else
			if Object.Timer == 16
				ResetObjectEntity(Object.EntityNo, TypeName[Sonic], 0, Object.XPos, Object.YPos)
				Object.InkEffect = 2 //INK_ALPHA
				Object.Alpha = 0
				Object.Timer = 0
#platform: Mobile
				Engine.State = ENGINE_EXIT_HIRESMODE
				Object.RectAlpha = 256
#endplatform
			else
				Object.Timer++
			end if
		end if
		break

	end switch

#platform: Mobile
	if Engine.PlatformID == RETRO_WP7
		if KeyPress.ButtonB == true
			EngineCallback(CALLBACK_EXIT_SELECTED)
		end if
	end if
#endplatform
end sub


sub ObjectDraw
	//8 Point Star Pattern
	DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 96, 255)
	Object.Scale = Object.BGScale
	DrawSpriteFX(1, FX_ROTOZOOM, Object.XPos, Object.YPos)
	Object.Rotation += 64
	DrawSpriteFX(1, FX_ROTOZOOM, Object.XPos, Object.YPos)
	Object.Rotation -= 64
	//Logo Fade In-Out
	switch Object.State
	case CWLOGO_BGSCALE_INCREASE_2
	case CWLOGO_LOGO_FADEIN
	case CWLOGO_BGSCALE_DECREASE
#platform: Standard
		Object.Scale = 160
#endplatform
#platform: Mobile
		Object.Scale = 192
#endplatform
		DrawSpriteFX(0, FX_SCALE, Object.XPos, Object.YPos)
		TempValue0 = Screen.CenterX
		TempValue0 -= 96
		TempValue1 = Screen.CenterY
		TempValue1 -= 48
		DrawRect(TempValue0, TempValue1, 192, 96, 255, 149, 18, Object.RectAlpha)
		break
		
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("Title/CWLogo.gif")
	SpriteFrame(-255, -117, 510, 235, 1, 1)	// #0 - Cristian Whitehead Logo 
	SpriteFrame(-32, -32, 64, 64, 1, 1)		// #1 - Background
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
