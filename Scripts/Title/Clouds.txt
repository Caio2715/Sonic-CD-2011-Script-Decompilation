//------------Sonic CD Clouds Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Speed


sub ObjectDraw

	// The clouds move at a speed of 8px per second
	Object.Speed += 8
	
	// Limit it to 0x07FF
	Object.Speed &= 2047
	
	// The clouds should be scrolling towards the screen, not away!
	TempValue0 = Object.Speed
	FlipSign(TempValue0)
	
	MatrixTranslateXYZ(MAT_WORLD, -512, 720, TempValue0)
	
	Draw3DScene()

end sub


sub ObjectStartup

	// Load the Clouds Sprite Sheet
	LoadSpriteSheet("Title/Clouds.gif")
	
	// No Sprite Frames need to be setup,
	// since the entire sheet will be projected onto the Face
	
	3DScene.NoVertices = 0
	3DScene.NoFaces = 0
	ArrayPos0 = 3DScene.NoVertices
	ArrayPos1 = 3DScene.NoFaces
	
	// Reset vertexes to their starting position
	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixRotateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixRotateXYZ(MAT_VIEW, 8, 0, 0)
	
	ArrayPos0 = 0
	ArrayPos1 = 0
	TempValue2 = 0	// TempValue2 is the starting point of the clouds after loop
	TempValue3 = 0	// TempValue3 is the minimal value for the amount of clouds to generate
	TempValue4 = 0	// TempValue4 is the width of the cloud sprite
	TempValue5 = 0	// TempValue5 is the height of the cloud sprite

	// Make 10 Cloud "Rows"
	while TempValue3 < 10
		TempValue0 = 0
		TempValue1 = -8192
		TempValue4 = 0

		// Long Story Short, this sets the angles for 16 cloud "Columns"
		while TempValue0 < 16
			VertexBuffer[ArrayPos0].x = TempValue1
			VertexBuffer[ArrayPos0].y = 0
			VertexBuffer[ArrayPos0].z = TempValue2
			VertexBuffer[ArrayPos0].u = TempValue4
			VertexBuffer[ArrayPos0].v = TempValue5
			ArrayPos0++
			TempValue2 += 512
			VertexBuffer[ArrayPos0].x = TempValue1
			VertexBuffer[ArrayPos0].y = 0
			VertexBuffer[ArrayPos0].z = TempValue2
			VertexBuffer[ArrayPos0].u = TempValue4
			VertexBuffer[ArrayPos0].v = TempValue5
			VertexBuffer[ArrayPos0].v += 63
			ArrayPos0++
			TempValue2 -= 512
			TempValue1 += 1024
			VertexBuffer[ArrayPos0].x = TempValue1
			VertexBuffer[ArrayPos0].y = 0
			VertexBuffer[ArrayPos0].z = TempValue2
			VertexBuffer[ArrayPos0].u = TempValue4
			VertexBuffer[ArrayPos0].u += 63
			VertexBuffer[ArrayPos0].v = TempValue5
			ArrayPos0++
			TempValue2 += 512
			VertexBuffer[ArrayPos0].x = TempValue1
			VertexBuffer[ArrayPos0].y = 0
			VertexBuffer[ArrayPos0].z = TempValue2
			VertexBuffer[ArrayPos0].u = TempValue4
			VertexBuffer[ArrayPos0].u += 63
			VertexBuffer[ArrayPos0].v = TempValue5
			VertexBuffer[ArrayPos0].v += 63
			TempValue4 += 64
			TempValue4 &= 255
			TempValue2 -= 512
			ArrayPos0 -= 3
			FaceBuffer[ArrayPos1].a = ArrayPos0
			FaceBuffer[ArrayPos1].b = ArrayPos0
			FaceBuffer[ArrayPos1].b += 2
			FaceBuffer[ArrayPos1].c = ArrayPos0
			FaceBuffer[ArrayPos1].c += 1
			FaceBuffer[ArrayPos1].d = ArrayPos0
			FaceBuffer[ArrayPos1].d += 3
			FaceBuffer[ArrayPos1].Flag = 0 // The FACE_TEXTURED_3D flag, since the clouds sprites are gonna be projected onto the face
			ArrayPos0 += 4
			ArrayPos1++
			TempValue0++

			3DScene.NoVertices += 4
			3DScene.NoFaces++
		loop

		TempValue5 += 64
		TempValue5 &= 255
		TempValue2 += 512
		TempValue3++
	loop
	
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Title/Clouds.gif")
	SpriteFrame(-128, -128, 256, 256, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
