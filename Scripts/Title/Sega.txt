//------------Sonic CD Sega Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer

// States
#alias 0 : SEGA_SETUP
#alias 1 : SEGA_FADEIN
#alias 2 : SEGA_JINGLE
#alias 3 : SEGA_FADEOUT
#alias 4 : SEGA_END

// Ink Effect Aliases
#alias 2 : INK_ALPHA

// SFX Aliaes
#alias 0 : SFX_SEGA

// Callbacks & Engine States
#alias 0 : CALLBACK_DISPLAYLOGOS
#alias 7 : CALLBACK_EXIT_SELECTED

#alias 5 : ENGINE_ENTER_HIRESMODE


sub ObjectMain
	
	switch Object.State
	case SEGA_SETUP
		// Setup not only the Object, but some other useful game values as well
		
		ReadSaveRAM()
		
		// First, restore the Soundtrack setting to what it should be
		Options.Soundtrack = SaveRAM[38]
		
		// Some more initialisation code follows, varies between platforms
		
#platform: Use_Originss
		if game.titleMode != 0
			
			// TODO: idk what this var even is lol this is the first script i'm looking at so come back to this later
			
			game.titleMode = 0
			Object.State = SEGA_END
			
		else
			if game.mainMenuMode != 0
				
				// If returning from another Menu (ie Soundtrack) then just skip to the end
				// and don't play the Sega screen again
				// -> This value isn't reset here though, it's reset a few lines down
				
				Object.State = SEGA_END
				
			else
				
				// Nope, nothing special is needed, so let's just show the normal Sega splash!
				
				Object.State = SEGA_FADEIN
				
			end if
		end if
#endplatform
		
#platform: Use_Decomp
		// Origins has all these settings on the wrapper side of things,
		// so this isn't needed there
		if SaveRAM[32] == 1
			Engine.BGMVolume = SaveRAM[33]
			Engine.SFXVolume = SaveRAM[34]
		end if
#endplatform
		
		Object.Timer = 480
		Object.Alpha = 255
		Object.InkEffect = INK_ALPHA
		
		// Start the screen off all black
		SetScreenFade(0, 0, 0, 255)
		
		// And move the Object to the centre of the screen
		Object.iXPos = Screen.CenterX

#platform: Use_Originss
		game.mainMenuMode = 0
#endplatform
		break

	case SEGA_FADEIN
		if Object.Timer > 0
			// Fade in, 8 by 8
			Object.Timer -= 8
		else
			if Engine.OnlineActive < 2
				// Make sure the Engine's ready to go too before moving on just yet!
				Object.State = SEGA_JINGLE
			end if
		end if
		
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case SEGA_JINGLE
		// Hold for a moment, to let the player bask in this wonderful screen
		
		Object.Timer++
		
		if Object.Timer == 160
			// Nope that's over, off we go again
			Object.Timer = 0
			Object.State = SEGA_FADEOUT
		end if
		
		if Object.Timer == 2
			// Play the SFX almost immediatly after entering this state
			PlayStageSfx(SFX_SEGA, false)
		end if
		break

	case SEGA_FADEOUT
		// Do note - this state is kinda messed up, they forget a break at the end so
		// this state doesn't reallly fade out
		
		if Object.Alpha > 7
			Object.Alpha -= 8
		else
			if Object.Timer == 16
				ResetObjectEntity(Object.EntityNo, TypeName[Sonic], 0, Object.XPos, Object.YPos)
				Object.InkEffect = INK_ALPHA
				Object.Alpha = 0
				Object.Timer = 0
			else
				Object.Timer++
			end if
		end if
		
		// No break used here, so it falls through, meaning the Fade Out doesn't really happen...
		
	case SEGA_END
		// The name's a bit odd - this Object is used to skip the sequence,
		//  but it's also how the sequence ends, thanks to the missing break from above
		
		// For simplicity here, I've just chosen to stick with "End"
		
		// But anyways, this state just moves on with the title sequence, nothing too special
		
		ResetObjectEntity(Object.EntityNo, TypeName[Sonic], 0, Object.XPos, Object.YPos)
		Object.InkEffect = INK_ALPHA
		Object.Alpha = 0
		Object.Timer = 0
		break
		
	endswitch
	
	if Engine.PlatformID == RETRO_WP7
		if KeyPress[0].ButtonB == true
			EngineCallback(CALLBACK_EXIT_SELECTED)
		end if
	end if
	
end sub



sub ObjectDraw
	
	DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 96, 255)
	if Object.Alpha > 8
		DrawSpriteFX(0, FX_INK, Object.XPos, Object.YPos)
	end if
	
end sub


sub ObjectStartup
	
	LoadSpriteSheet("Title/Title.gif")
	
	// Sega sprite
	SpriteFrame(-49,-16,98,31,106,1)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-49,-16,98,31,106,1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
