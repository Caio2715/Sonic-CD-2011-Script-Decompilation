//------------Sonic CD Menu Control Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

//-------Aliases-------//
#alias Object.Value0 : Object.Fade
#alias Object.Value1 : Object.ButtonYOffset
#alias Object.Value2 : Object.DragCurrentSelection
#alias Object.Value3 : Object.CurrentSelection
#alias Object.Value4 : Object.ButtonScroll
#alias Object.Value5 : Object.DragEnabled
#alias Object.Value6 : Object.DragYOffset
#alias Object.Value7 : Object.DragSmooth
#alias Object[1].Value0 : Object.DragPos
#alias Object[1].Value1 : Object.RecenterValue
#alias Object[1].Value2 : Object.MaxButtonCount
#alias Object[1].Value3 : Object.DragLimit
#alias Object[1].Value4 : Object.YOffSet1
#alias Object[1].Value5 : Object.YOffSet2
#alias Object[1].Value6 : Object.YOffSet3

//-------States--------//
#alias 0 : MENUCONTROL_SETUP
#alias 1 : MENUCONTROL_BUTTONS_MOVEIN
#alias 2 : MENUCONTROL_CONTROLS
#alias 3 : MENUCONTROL_BLANK
#alias 4 : MENUCONTROL_ENTER_TIMEATTACK
#alias 5 : MENUCONTROL_ENTER_DEMO
#alias 6 : MENUCONTROL_MENUBUTTON_CHECK
#alias 7 : MENUCONTROL_ENTER_TROPHIES
#alias 8 : MENUCONTROL_ENTER_LEADERBOARS


//Object[1] is a blank object only used to share values across the menu objects:

//Object[1].Value0 is used by MenuControl and TrophiesMenu to know from what part of the list you are dragging.
//Object[1].Value1 is used mainly here (MenuControl) for the smooth centering to the screen after exceeding with the drag.
//Object[1].Value2 comes from MenuButton and sets the maximum amount of button to move around.
//Object[1].Value3 comes from MenuButton and defines a value to jump back if exceending with the dragging based on the current button.
//Object[1].Value4 gets values from most of the "Menu" Objects, normally used for checking input.
//*Except in MenuControl and TrophiesMenu, where they are also used as an additional offset for dragging.
//Object[1].Value5 and Object[1].Value6 get their values from TrophiesMenu and MenuControl, both to set dragging offsets.
//Object[1].Value7 is unused.

sub ObjectMain

	switch Object.State
	case MENUCONTROL_SETUP
		Object.State = MENUCONTROL_BUTTONS_MOVEIN
		Object.Fade = 384
		SetScreenFade(0, 0, 0, 255)
		PlayMusic(Options.Soundtrack)
		if Engine.FrameSkipTimer > -1
			Engine.FrameSkipTimer = -1
		end if
		break

	case MENUCONTROL_BUTTONS_MOVEIN
		if Object.Fade > 0
			Object.Fade -= 8
		else
			Object.State = MENUCONTROL_MENUBUTTON_CHECK
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break

	case MENUCONTROL_CONTROLS
	
		#platform: Mobile
			CheckEqual(Options.PhysicalControls,1)
		#endplatform

		#platform: Standard
			CheckResult = 1
		#endplatform

		if CheckResult == 1
			if KeyPress.Up == 1
				Object.CurrentSelection--
				if Object.CurrentSelection < 1
					Object.CurrentSelection = 1
				else
					PlaySfx(23, 0)	//Menu Button SFX
				end if
			end if
			if KeyPress.Down == 1
				Object.CurrentSelection++
				if Object.CurrentSelection > Object.MaxButtonCount
					Object.CurrentSelection = Object.MaxButtonCount
				else
					PlaySfx(23, 0)	//Menu Button SFX
				end if
			end if
			TempValue0 = Object.CurrentSelection
			TempValue0 -= Object.ButtonScroll
			if TempValue0 < 1
				Object.ButtonScroll--
				Object.Direction = 3
			end if
			if TempValue0 > 4
				Object.ButtonScroll++
				Object.Direction = FACING_RIGHT
			end if
			TempValue0 = Object.ButtonYOffset
			TempValue0 -= 32
			TempValue1 = Object.ButtonScroll
			TempValue1 *= -60
			TempValue1 -= TempValue0
			TempValue1 += Object.Direction
			TempValue1 >>= 2
			Object.ButtonYOffset += TempValue1
		end if

#platform: Mobile
		if Options.PhysicalControls == 1
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = 0
				Object.CurrentSelection = 0
				Object.Value2 = 0
			end if
		else
			if Engine.Message == 2
				Object.CurrentSelection = 0
				Object.Value2 = 0
			end if
			if Object.Value2 > 0
				Object.CurrentSelection = Object.Value2
			else
				Object.CurrentSelection = 0
			end if
			if Object.MaxButtonCount > 4
				CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
				if CheckResult > -1
					ArrayPos0 = CheckResult
					if Object.DragEnabled == 0
						Object.DragEnabled = 1
						Object.DragPos = Object.ButtonYOffset
						Object.DragYOffset = TouchScreen[ArrayPos0].YPos
					else
						TempValue0 = TouchScreen[ArrayPos0].YPos
						TempValue0 -= Object.DragYOffset
						TempValue0 += Object.DragPos
						Object.YOffSet3 = Object.YOffSet2
						Object.YOffSet2 = Object.YOffSet1
						Object.YOffSet1 = Object.DragSmooth
						Object.DragSmooth = TempValue0
						Object.DragSmooth -= Object.ButtonYOffset
						Object.ButtonYOffset = TempValue0
					end if
					TempValue0 = Object.DragSmooth
					TempValue0 >>= 2
					if TempValue0 != 0
						Object.CurrentSelection = 0
						Object.Value2 = 0
					end if
				else
					if Object.DragEnabled == 1
						Object.DragEnabled = 0
						Object.DragYOffset = Object.ButtonYOffset
						Object.DragYOffset <<= 8
						Object.DragSmooth += Object.YOffSet1
						Object.DragSmooth += Object.YOffSet2
						Object.DragSmooth += Object.YOffSet3
						Object.DragSmooth >>= 2
						Object.DragSmooth <<= 7
						if Object.DragSmooth == 0
							if Object.CurrentSelection == 0
								Object.DragSmooth = 1
							end if
						end if
					end if
					if Object.DragSmooth != 0
						Object.DragYOffset += Object.DragSmooth
						if Object.DragYOffset < Object.DragLimit
							Object.DragYOffset = Object.DragLimit
							Object.DragSmooth = 0
						end if
						if Object.DragYOffset > 23040
							Object.DragYOffset = 23040
							Object.DragSmooth = 0
						end if
						if Object.DragSmooth > 0
							Object.DragSmooth -= 32
							if Object.DragSmooth < 0
								Object.DragSmooth = 0
							end if
						else
							Object.DragSmooth += 32
							if Object.DragSmooth > 0
								Object.DragSmooth = 0
							end if
						end if
						Object.ButtonYOffset = Object.DragYOffset
						Object.ButtonYOffset >>= 8
						if Object.DragSmooth == 0
							Object.ButtonScroll = Object.ButtonYOffset
							Object.ButtonScroll -= 32
							Object.ButtonScroll /= -60
							if Object.ButtonScroll < 0
								Object.ButtonScroll = 0
							end if
							TempValue0 = Object.MaxButtonCount
							TempValue0 -= 4
							if Object.ButtonScroll > TempValue0
								Object.ButtonScroll = TempValue0
							end if
						end if
					else
						//Drag screen re-centering
						TempValue0 = Object.ButtonYOffset
						TempValue0 -= 32
						Object.RecenterValue = Object.Value4
						Object.RecenterValue *= -60
						Object.RecenterValue -= TempValue0
						Object.RecenterValue >>= 2
						if Object.RecenterValue < -4
							Object.RecenterValue = -4
						end if
						if Object.RecenterValue > 4
							Object.RecenterValue = 4
						end if
						Object.ButtonYOffset += Object.RecenterValue
					end if
				end if
			end if
			if KeyPress.Up == 1
				Object.CurrentSelection = Object.MaxButtonCount
				Options.PhysicalControls = 1
			end if
			if KeyPress.Down == 1
				Object.CurrentSelection = 1
				Options.PhysicalControls = 1
			end if
		end if
#endplatform

		break

	case MENUCONTROL_BLANK //Used to stop the controls temporarily
		break

	case MENUCONTROL_ENTER_TIMEATTACK
		if Object.Fade < 320
			Object.Fade += 8
		else
			Stage.ListPos = 2	//Time Attack
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break

	case MENUCONTROL_ENTER_DEMO	//Only used in Demo versions of the game
		if Object.Fade < 320
			Object.Fade += 8
		else

#platform: Mobile
			if Engine.PlatformID != RETRO_WP7
				if Options.Soundtrack == 0
					LoadVideo("Opening")
				else
					LoadVideo("OpeningUS")
				end if
			end if
#endplatform

			Stage.ActiveList = REGULAR_STAGE
			Stage.ListPos = 0	//Palmtree Panic 1 Present
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break

	case MENUCONTROL_MENUBUTTON_CHECK
		if Object[34].State == 2
			Object.State = MENUCONTROL_CONTROLS
		end if
		break

	case MENUCONTROL_ENTER_TROPHIES		//Mobile Only

		#platform: Mobile
			if Object.Fade < 320
				Object.Fade += 8
			else
				Stage.ListPos = 9	//Trophies (Mobile)
				LoadStage()
			end if
			SetScreenFade(0, 0, 0, Object.Fade)
		#endplatform

		break

	case MENUCONTROL_ENTER_LEADERBOARS	//Mobile Only

		#platform: Mobile
			if Object.Fade < 320
				Object.Fade += 8
			else
				Stage.ListPos = 10	//Leaderboards (Mobile)
				LoadStage()
			end if
			SetScreenFade(0, 0, 0, Object.Fade)
		#endplatform

		break

	end switch
end sub


sub ObjectStartup
	SetMusicTrack("JP/SpecialStage.ogg", 0, 1)
	SetMusicTrack("US/SpecialStage.ogg", 1, 270972)
	Object[0].Type = TypeName[MenuControl]
	Object[0].ButtonYOffset = 32
#platform: Standard
	Object[0].CurrentSelection = 1
#endplatform
#platform: Mobile
	Options.PhysicalControls = 0
#endplatform
	Object[0].DragCurrentSelection = 0
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
