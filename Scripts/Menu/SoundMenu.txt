//------------Sonic CD SoundtrackMenu Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.FadeValue
#alias Object.Value1 : Object.SelectedOption
#alias Object.Value2 : Object.EggmanAnimTimer
#alias Object.Value3 : Object.NoteOscillation
//Object[33] is MenuHeading
#alias Object[33].State				: Object.MHState
#alias Object[33].PropertyValue		: Object.MHPropertyValue
//Object[-1] is MenuWindow
#alias Object[-1].State				: Object.MWState

// Soundtrack Options
#alias -1 : OST_NONE // Touching nothing on mobile
#alias  0 : OST_JP
#alias  1 : OST_US

// States
#alias 0 : STATE_INITIALIZE
#alias 1 : STATE_OST_SELECTION
#alias 2 : STATE_OST_SELECTED
#alias 3 : STATE_EXIT

// Languages
#alias 0 : ENGLISH
#alias 1 : FRENCH
#alias 2 : ITALIAN
#alias 3 : DEUTSCH
#alias 4 : SPANISH
#alias 5 : JAPANESE

sub ObjectMain
	switch Object.State
	case STATE_INITIALIZE
#platform: Standard
		Object.SelectedOption = Options.Soundtrack
#endplatform
#platform: Mobile
		if Options.PhysicalControls == false
			Object.SelectedOption = OST_NONE
		else
			Object.SelectedOption = Options.Soundtrack
		end if
#endplatform

		if Object.FadeValue < 256
			Object.Alpha = Object.FadeValue
			Object.FadeValue += 6
		else
			Object.FadeValue = 0
			Object.Alpha = 255
			Object.State = STATE_OST_SELECTION
		end if
		break
		
	case STATE_OST_SELECTION

#platform: Mobile
		CheckEqual(Options.PhysicalControls,true)
#endplatform

#platform: Standard
		CheckResult = true
#endplatform

		if CheckResult == true
			// Physical Controls
			if KeyPress.Left == true
				Object.SelectedOption--
				if Object.SelectedOption < OST_JP
					Object.SelectedOption = OST_US
				end if
				PlaySfx(23, 0) 	//Menu Button SFX
			end if
			if KeyPress.Right == true
				Object.SelectedOption++
				if Object.SelectedOption > OST_US
					Object.SelectedOption = OST_JP
				end if
				PlaySfx(23, 0) 	//Menu Button SFX
			end if
		end if
#platform: Mobile
		if Options.PhysicalControls == true
			// Enable touch controls if the screen is touched
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.SelectedOption = OST_NONE
			end if
		else
			// Touch Controls
			
			// Check if JP button is touched
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			TempValue3 = CheckResult
			TempValue0 = Screen.CenterX
			TempValue0 -= 90
			TempValue1 = Screen.CenterX
			TempValue1 -= 22
			CheckTouchRect(TempValue0, 112, TempValue1, 144)
			if CheckResult > -1
				Object.SelectedOption = OST_JP
			else
				if TempValue3 < 0
					if Object.SelectedOption == OST_JP
						KeyPress.Start = true
					end if
				else
					if Object.SelectedOption == OST_JP
						Object.SelectedOption = OST_NONE
					end if
				end if
			end if
			
			// Check if US button is touched
			TempValue0 = Screen.CenterX
			TempValue0 += 22
			TempValue1 = Screen.CenterX
			TempValue1 += 90
			CheckTouchRect(TempValue0, 112, TempValue1, 144)
			if CheckResult > -1
				Object.SelectedOption = OST_US
			else
				if TempValue3 < 0
					if Object.SelectedOption == OST_US
						KeyPress.Start = true
					end if
				else
					if Object.SelectedOption == OST_US
						Object.SelectedOption = OST_NONE
					end if
				end if
			end if
			
			CallFunction(MenuHeading_CheckTouchBack)
			
			// Enable physical controls if a button is pressed
			if KeyPress.Left == true
				Object.SelectedOption = OST_US
				Options.PhysicalControls = true
			end if
			if KeyPress.Right == true
				Object.SelectedOption = OST_JP
				Options.PhysicalControls = true
			end if
		end if
#endplatform

		CheckResult = false

#platform: Mobile				
		if Object.SelectedOption > OST_NONE // If a button was touched
			if KeyPress.Start == true
				CheckResult = true
			end if
			if KeyPress.ButtonA == true
				CheckResult = true
			end if
		end if
#endplatform

#platform: Standard
		if KeyPress.Start == true
			CheckResult = true
		end if
		if KeyPress.ButtonA == true
			CheckResult = true
		end if
#endplatform

		if CheckResult == true
			Options.Soundtrack = Object.SelectedOption
			SaveRAM[38] = Options.Soundtrack
			Object.State = STATE_OST_SELECTED
			StopMusic()
			PlaySfx(27, 0)	//Select SFX
			WriteSaveRAM()
		end if
		
		if KeyPress.ButtonB == true
			Object.FadeValue = 248
			Object.State = STATE_EXIT
			PlayStageSfx(1, 0)
		end if
		break
		
	case STATE_OST_SELECTED
		if Object.AnimationTimer < 30
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			Object.State = STATE_EXIT
			PlayMusic(Options.Soundtrack)
		end if
		break
		
	case STATE_EXIT
		if Object.FadeValue > 0
			Object.Alpha = Object.FadeValue
			Object.FadeValue -= 8
		else
			// Tell the other objects the menu is being exited
			Object.Type = TypeName[BlankObject]
			Object.MWState = 3
			//All of this is MenuButton Objects
			Object[34].State = 1
			Object[35].State = 1
			Object[36].State = 1
			Object[37].State = 1
			Object[38].State = 1
			Object[39].State = 1
			Object[40].State = 1
			Object[41].State = 1
			Object[42].State = 1
			//End
			Object.MHState = 1
			Object.MHPropertyValue = 5 // Set the heading to "MAIN MENU"
		end if
		break
	end switch
end sub


sub ObjectDraw
	// Animate and draw Eggman
	TempValue0 = Object.EggmanAnimTimer
	TempValue0 >>= 5
	DrawSpriteScreenFX(TempValue0, FX_INK, Screen.CenterX, Screen.CenterY)
	Object.EggmanAnimTimer++
	Object.EggmanAnimTimer &= 63
	
	// Oscillate and draw the music notes
	Sin(TempValue0, Object.NoteOscillation)
	TempValue0 >>= 6
	TempValue0 += Screen.CenterY
	DrawSpriteScreenFX(6, FX_INK, Screen.CenterX, TempValue0)
	Sin(TempValue0, Object.NoteOscillation)
	TempValue0 >>= 6
	FlipSign(TempValue0)
	TempValue0 += Screen.CenterY
	DrawSpriteScreenFX(7, FX_INK, Screen.CenterX, TempValue0)
	Object.NoteOscillation += 2
	Object.NoteOscillation &= 511
	
	// Set up the positioning for the buttons
	TempValue0 = Screen.CenterX
	TempValue0 -= 56
	TempValue1 = Screen.CenterX
	TempValue1 += 56
	TempValue3 = Screen.CenterY
	TempValue3 += 8
	
	// Draw the JP button
	if Object.SelectedOption == OST_JP
		TempValue2 = Object.AnimationTimer
		TempValue2 &= 3
		TempValue2 >>= 1
		TempValue2 += 2
		DrawSpriteScreenFX(TempValue2, FX_INK, TempValue0, TempValue3)
	else
		DrawSpriteScreenFX(3, FX_INK, TempValue0, TempValue3)
	end if
	
	// Draw the US button
	if Object.SelectedOption == OST_US
		TempValue2 = Object.AnimationTimer
		TempValue2 &= 3
		TempValue2 >>= 1
		TempValue2 += 4
		DrawSpriteScreenFX(TempValue2, FX_INK, TempValue1, TempValue3)
	else
		DrawSpriteScreenFX(5, FX_INK, TempValue1, TempValue3)
	end if
	
	// Draw the border for the soundtrack that's currently playing
	if Options.Soundtrack == OST_JP
		DrawSpriteScreenFX(8, FX_INK, TempValue0, TempValue3)
	else
		DrawSpriteScreenFX(8, FX_INK, TempValue1, TempValue3)
	end if
end sub


sub ObjectStartup
	switch Engine.Language
	case ENGLISH
		LoadSpriteSheet("Menu/MenuGfx2_EN.gif")
		break
	case FRENCH
		LoadSpriteSheet("Menu/MenuGfx2_FR.gif")
		break
	case ITALIAN
		LoadSpriteSheet("Menu/MenuGfx2_IT.gif")
		break
	case DEUTSCH
		LoadSpriteSheet("Menu/MenuGfx2_DE.gif")
		break
	case SPANISH
		LoadSpriteSheet("Menu/MenuGfx2_ES.gif")
		break
	case JAPANESE
		LoadSpriteSheet("Menu/MenuGfx2_JP.gif")
		break
	end switch
	
	SpriteFrame(-20, -56, 40, 40, 259, 513) // Eggman 1
	SpriteFrame(-20, -56, 40, 40, 259, 554) // Eggman 2
	SpriteFrame(-32, -10, 64, 21, 300, 535) // JP Selected
	SpriteFrame(-32, -10, 64, 21, 300, 513) // JP Unselected
	SpriteFrame(-32, -10, 64, 21, 365, 535) // US Selected
	SpriteFrame(-32, -10, 64, 21, 365, 513) // US Unselected
	SpriteFrame(-46, -48, 11, 19, 300, 557) // Music Notes 1
	SpriteFrame(32, -48, 17, 16, 312, 557)  // Music Notes 2
	SpriteFrame(-35, -13, 70, 27, 430, 497) // Current Soundtrack Border
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
