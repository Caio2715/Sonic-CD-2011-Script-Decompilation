//------------Sonic CD Anton Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 36: TYPE_ANTON
#alias Object.Value0		:	Object.Timer
#alias Object.PropertyValue	:	Object.BadnikCondition

// States
#alias 0	:	ANTON_MOVING_LEFT
#alias 1	:	ANTON_MOVING_RIGHT

// CSides
#alias 0	:	CSIDE_FLOOR

// Property Values
#alias 0	:	NEW
#alias 1	:	OLD

sub ObjectMain
	if Object.BadnikCondition == NEW

		Object.Frame += 2
		switch Object.State
		case ANTON_MOVING_LEFT
			Object.XPos -= 65536	// 0x10000

			Object.Timer--
			if Object.Timer == -128
				Object.State = ANTON_MOVING_RIGHT
				Object.Direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_RIGHT
				Object.Timer = -128
				Object.Direction = FACING_LEFT
			end if
			break

		case ANTON_MOVING_RIGHT
			Object.XPos += 65536	// 0x10000

			Object.Timer++
			if Object.Timer == 128
				Object.State = ANTON_MOVING_LEFT
				Object.Direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_LEFT
				Object.Timer = 128
				Object.Direction = FACING_RIGHT
			end if
			break
			
		end switch

	else

		Object.Frame++
		switch Object.State
		case ANTON_MOVING_LEFT
			Object.XPos -= 32768	// 0x8000

			Object.Timer--
			if Object.Timer == -256
				Object.State = ANTON_MOVING_RIGHT
				Object.Direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_RIGHT
				Object.Timer = -256
				Object.Direction = FACING_LEFT
			end if
			break

		case ANTON_MOVING_RIGHT
			Object.XPos += 32768	// 0x8000
			
			Object.Timer++
			if Object.Timer == 256
				Object.State = ANTON_MOVING_LEFT
				Object.Direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_LEFT
				Object.Timer = 256
				Object.Direction = FACING_RIGHT
			end if
			break

		end switch

	end if

	Object.Frame &= 15
	CallFunction(StageSetup_CheckGoodFuture)	// Check if a good future has been made
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -12, -18, 12, 20)
	if CheckResult == true
		CallFunction(PlayerObject_BadnikBreak)
	end if
end sub


sub ObjectDraw
	TempValue0 = Object.Frame
	TempValue0 >>= 3
	if Object.BadnikCondition == NEW
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Wheel
		TempValue0 -= 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Anton
		TempValue0 += 6
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Smoke Puff
	else
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Wheel
		TempValue0 -= 2
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Anton
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Smoke Puff
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-12, -19, 24, 32, 1, 1)		// #0 - Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 1, 1)		// #1 - Anton Frame 1
	SpriteFrame(-12, -19, 24, 32, 26, 1)	// #2 - Old Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 26, 1)	// #3 - Old Anton Frame 1

	SpriteFrame(-8, 4, 16, 16, 1, 34)		// #4 - Anton Wheel Frame 0
	SpriteFrame(-8, 4, 16, 16, 18, 34)		// #5 - Anton Wheel Frame 1

	SpriteFrame(14, -4, 8, 8, 35, 34)		// #6 - Anton Smoke Puff Frame 0
	SpriteFrame(18, -5, 8, 8, 35, 34)		// #7 - Anton Smoke Puff Frame 1
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
