//------------Sonic CD Rise Platform Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Raising
#alias Object.Value1	:	Object.Timer
#alias Object.Value2	:	Object.PlayerFloor.Y
#alias Object.Value3	:	Object.RiseTimer
#alias Object.Value4	:	Object.YVelocity
#alias Object.Value5	:	Object.OriginPos.Y

// States
#alias 0				:	RISEPLATFORM_IDLE
#alias 1				:	RISEPLATFORM_CRUMBLE
#alias 2				:	RISEPLATFORM_RISE
#alias 3				:	RISEPLATFORM_FLOAT

// Priority
#alias 0				:	PRIORITY_ACTIVE_BOUNDS
#alias 1				:	PRIORITY_ACTIVE


sub ObjectMain
	switch Object.State
	case RISEPLATFORM_IDLE
		Object.PlayerFloor.Y = 0
		if Object.Raising == true
			Object.State = RISEPLATFORM_CRUMBLE
			Object.Priority = PRIORITY_ACTIVE
			Object.YVelocity = -262144		// -0x40000
			Object.RiseTimer = 0
		end if
		break

	case RISEPLATFORM_CRUMBLE
		Object.PlayerFloor.Y = Object.YVelocity
		if Object.RiseTimer < 8
			Object.RiseTimer++
		else
			Object.RiseTimer = 0
			Object.State = RISEPLATFORM_RISE
		end if
		if Object.OutOfBounds == true
			Object.YPos = Object.OriginPos.Y
			Object.State = 0
			Object.Timer = 0
		end if
		break

	case RISEPLATFORM_RISE
		if Object.YVelocity < 0
			Object.YVelocity += 8192		// 0x2000
		else
			Object.State = RISEPLATFORM_FLOAT
		end if
		Object.PlayerFloor.Y = Object.YVelocity
		if Object.OutOfBounds == true
			Object.YPos = Object.OriginPos.Y
			Object.State = 0
			Object.Timer = 0
		end if
		break

	case RISEPLATFORM_FLOAT
		if Object.Raising == true

			if Object.Timer < 16
				Object.Timer++
				TempValue0 = Object.Timer
				TempValue0 &= 3
				if TempValue0 == 3
					Object.PlayerFloor.Y = 65536	// 0x10000
				else
					Object.PlayerFloor.Y = 0
				end if

			else
				Object.PlayerFloor.Y = 0
			end if

		else
		
			if Object.Timer > 0
				Object.Timer--
				TempValue0 = Object.Timer
				TempValue0 &= 3
				if TempValue0 == 3
					Object.PlayerFloor.Y = -65536	// -0x10000
				else
					Object.PlayerFloor.Y = 0
				end if

			else
				Object.PlayerFloor.Y = 0
			end if

		end if

		if Object.OutOfBounds == true
			Object.YPos = Object.OriginPos.Y
			Object.State = 0
			Object.Timer = 0
		end if
		break

	end switch
	Object.Raising = false
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_PLATFORM, -32, -9, 32, 12)
	if CheckResult == true
		Object.Raising = true
		Player.YPos += Object.PlayerFloor.Y
	end if
end sub


sub ObjectDraw
	Object.YPos += Object.PlayerFloor.Y
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Rise Platform

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[RisePlatform]
			Object[ArrayPos0].OriginPos.Y = Object[ArrayPos0].YPos
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
