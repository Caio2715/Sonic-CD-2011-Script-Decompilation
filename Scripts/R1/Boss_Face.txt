//------------Sonic CD Boss Face Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Oscillation
#alias Object.Value1		:	Object.BodyEntity
#alias Object.Value2		:	Object.ShoulderEntity
#alias Object.value3		:	Object.Hit
#alias Object.value4		:	Object.BitHolder	// Unused?
#alias Object.value5		:	Object.NewXBoundary1Value
#alias Object.value6		:	Object.NewXBoundary2Value
#alias Object.value7		:	Object.SavedXBoundary2
#alias Object.Rotation		:	Object.Timer
#alias Object.Direction		:	Object.DamageTimer
#alias Object.Scale			:	Object.SavedYBoundary2
#alias Object.InkEffect		:	Object.AnimationTimer

// EGG-HVC-001 Body Aliases
#alias Object[+1].iXPos		:	BossBody.iXPos
#alias Object[+1].iYPos		:	BossBody.iYPos
#alias Object[+1].YPos		:	BossBody.YPos
#alias Object[+1].State		:	BossBody.State
#alias Object.Priority		:	Body.Priority
#alias Object.Value0		:	Body.FaceEntity
#alias Object.Value1		:	Body.LegJointREntity
#alias Object.Value2		:	Body.LegJointLEntity
// Value4 is used to check key events, forward/backward movement and currently active leg
#alias Object.Value4		:	Body.BitHolder
#alias Object.Value5		:	Body.ChargeStart
#alias Object.State			:	Body.State

// EGG-HVC-001 Body Bit 0 Aliases
#alias 0					:	KEY_EVENT_FALSE
#alias 1					:	KEY_EVENT_TRUE

// EGG-HVC-001 Body Bit 1 Aliases
#alias 0					:	MOVE_FORWARD
#alias 1					:	MOVE_BACKWARDS

// EGG-HVC-001 Body Bit 6 Aliases
#alias 0					:	LEFT_LEG_ACTIVE
#alias 1					:	RIGHT_LEG_ACTIVE

// HVC-001 Boss Right Leg Joint Aliases
#alias Object[+2].YPos		:	BossLegJointR.YPos
#alias Object.Priority		:	LegJointR.Priority
#alias Object.Value0		:	LegJointR.BodyEntity
#alias Object.Value1		:	LegJointR.LegREntity

// HVC-001 Boss Right Leg Aliases
#alias Object[+3].YPos		:	BossLegR.YPos
#alias Object[+3].Value7	:	BossLegR.YVelocity
#alias Object.Priority		:	LegR.Priority
#alias Object.Value0		:	LegR.LegJointREntity
#alias Object.Value1		:	LegR.FootREntity

// HVC-001 Boss Right Foot Aliases
#alias Object[+4].Value7	:	BossFootR.YVelocity
#alias Object.Priority		:	FootR.Priority
#alias Object.Value0		:	FootR.LegREntity
#alias Object.Value1		:	FootR.BodyEntity
// HVC-001 Boss Left Leg Joint Aliases
#alias Object[+5].YPos		:	BossLegJointL.YPos
#alias Object.Priority		:	LegJointL.Priority
#alias Object.DrawOrder		:	LegJointL.DrawOrder
#alias Object.Value0		:	LegJointL.BodyEntity
#alias Object.Value1		:	LegJointL.LegLEntity

// HVC-001 Boss Left Leg Aliases
#alias Object[+6].Value7	:	BossLegL.YVelocity
#alias Object.Priority		:	LegL.Priority
#alias Object.DrawOrder		:	LegL.DrawOrder
#alias Object.Value0		:	LegL.LegJointLEntity
#alias Object.Value1		:	LegL.FootLEntity

// HVC-001 Boss Left Foot Aliases
#alias Object[+7].Value7	:	BossFootL.YVelocity
#alias Object.Priority		:	FootL.Priority
#alias Object.DrawOrder		:	FootL.DrawOrder
#alias Object.Value0		:	FootL.LegLEntity
#alias Object.Value1		:	FootL.BodyEntity

// HVC-001 Boss Shoulder Aliases
#alias Object[+8].YPos		:	BossShoulder.YPos
#alias Object.Priority		:	Shoulder.Priority
#alias Object.DrawOrder		:	Shoulder.DrawOrder
#alias Object.Value0		:	Shoulder.FaceEntity
#alias Object.Value1		:	Shoulder.ArmJointLEntity
#alias Object.Value2		:	Shoulder.ArmJointREntity

// HVC-001 Boss Left Arm Joint Aliases
#alias Object[+9].YPos		:	BossArmJointL.YPos
#alias Object.Priority		:	ArmJointL.Priority
#alias Object.Value0		:	ArmJointL.ShoulderEntity
#alias Object.Value1		:	ArmJointL.ArmLEntity

// HVC-001 Boss Left Arm Aliases
#alias Object[+10].YPos		:	BossArmL.YPos
#alias Object.Priority		:	ArmL.Priority
#alias Object.Value0		:	ArmL.ArmJointLEntity
#alias Object.Value1		:	ArmL.BumperLEntity

// HVC-001 Boss Left Bumper Joint Aliases
#alias Object[+11].YPos		:	BossBumperL.YPos
#alias Object.Priority		:	BumperL.Priority
#alias Object.Value0		:	BumperL.ArmLEntity
#alias Object.Value1		:	BumperL.BodyEntity

// HVC-001 Boss Right Arm Joint Aliases
#alias Object[+12].YPos		:	BossArmJointR.YPos
#alias Object.Priority		:	ArmJointR.Priority
#alias Object.DrawOrder		:	ArmJointR.DrawOrder
#alias Object.Value0		:	ArmJointR.ShoulderEntity
#alias Object.Value1		:	ArmJointR.ArmREntity

// HVC-001 Boss Right Arm Aliases
#alias Object[+13].YPos		:	BossArmR.YPos
#alias Object.Priority		:	ArmR.Priority
#alias Object.DrawOrder		:	ArmR.DrawOrder
#alias Object.Value0		:	ArmR.ArmJointREntity
#alias Object.Value1		:	ArmR.BumperREntity

// HVC-001 Boss Right Bumper Joint Aliases
#alias Object[+14].YPos		:	BossBumperR.YPos
#alias Object.Priority		:	BumperR.Priority
#alias Object.DrawOrder		:	BumperR.DrawOrder
#alias Object.Value0		:	BumperR.ArmREntity
#alias Object.Value1		:	BumperR.BodyEntity

#alias Object.State			:	Debris.State
#alias Object.Value2		:	Debris.XOffSet
#alias Object.Value5		:	Debris.YOffSet

// Player Aliases
#alias Player.Value4		:	Player.Invincible

// States
#alias 0					:	R1_EGGMAN_SETUP
#alias 1					:	R1_EGGMAN_WAIT_PLAYER
#alias 2					:	R1_EGGMAN_BODY_CONTROLLER
#alias 3					:	R1_EGGMAN_DEFEATED
#alias 4					:	R1_EGGMAN_BLANK
#alias 5					:	R1_EGGMAN_RESTORE_BOUNDS
#alias 6					:	R1_EGGMAN_SET_BOUNDARIES
#alias 7					:	R1_EGGMAN_SPAWN_HVC

// EGG-HVC-001 Body States
#alias 0					:	BOSSBODY_SETUP
#alias 1					:	BOSSBODY_DESCEND
#alias 2					:	BOSSBODY_LANDING
#alias 3					:	BOSSBODY_FIRST_STEP
#alias 4					:	BOSSBODY_MOVE_FORWARD
#alias 5					:	BOSSBODY_MOVE_BACKWARDS
#alias 6					:	BOSSBODY_PREPARE_CHARGING
#alias 7					:	BOSSBODY_DESTROYED
#alias 8					:	BOSSBODY_STOP_CHARGE

// EGG-HVC-001 Arm Joint States
#alias 5					:	BOSSARMJOINT_DESTROYED
// EGG-HVC-001 Arm States
#alias 2					:	BOSSARM_DESTROYED
// EGG-HVC-001 Bumper States
#alias 2					:	BOSSBUMPER_DESTROYED
// EGG-HVC-001 Leg Joint States
#alias 12					:	BOSSLEGJOINT_DESTROYED
// EGG-HVC-001 Leg States
#alias 7					:	BOSSLEG_DESTROYED
// EGG-HVC-001 Foot States
#alias 6					:	BOSSFOOT_DESTROYED
// EGG-HVC-001 Shoulder States
#alias 2					:	BOSSSHOULDER_DESTROYED

// Priority
#alias 1					:	PRIORITY_ACTIVE

#alias -1					:	ANI_NONE
#alias 0					:	ANI_IDLE
#alias 1					:	ANI_LAUGH
#alias 2					:	ANI_HURT
#alias 3					:	ANI_DESTROYED

// Function declarations
#function R1_EGGMAN_Oscillation

// Fake Table for Oscillation of the boss height
function R1_EGGMAN_Oscillation
	switch Object.Oscillation
	case 0
		TempValue0 = 1
		break
	case 1
		TempValue0 = 0
		break
	case 2
		TempValue0 = 2
		break
	case 3
		TempValue0 = 0
		break
	case 4
		TempValue0 = 3
		break
	case 5
		TempValue0 = 0
		break
	case 6
		TempValue0 = 4
		break
	case 7
		TempValue0 = 5
		break
	case 8
		TempValue0 = 5
		break
	case 9
		TempValue0 = 6
		break
	case 10
		TempValue0 = 6
		break
	case 11
		TempValue0 = 10
		break
	case 12
		TempValue0 = 4
		break
	case 13
		TempValue0 = 10
		break
	case 14
		TempValue0 = 8
		break
	case 15
		TempValue0 = 50
		break
	case 16
		TempValue0 = 5
		break
	case 17
		TempValue0 = 10
		break
	case 18
	case 19
		TempValue0 = -1
		break
	end switch
end function


sub ObjectMain
	switch Object.State
	case R1_EGGMAN_SETUP
		Object.Animation = ANI_NONE
		Object.Priority = PRIORITY_ACTIVE

		Object.BodyEntity = Object.EntityNo
		Object.BodyEntity += 1

		Object.ShoulderEntity = Object.EntityNo
		Object.ShoulderEntity += 8

		Object.NewXBoundary1Value = BossBody.iXPos

		Object.NewXBoundary2Value = Object.NewXBoundary1Value
		Object.NewXBoundary2Value += 320
		TempValue0 = Screen.CenterX
		TempValue0 -= 160
		TempValue0 >>= 1
		Object.NewXBoundary2Value += TempValue0
		Object.SavedXBoundary2 = Stage.XBoundary2
		Object.SavedYBoundary2 = Stage.YBoundary2
		Stage.NewXBoundary2 = Object.NewXBoundary2Value
		Object.State = R1_EGGMAN_WAIT_PLAYER
		break

	case R1_EGGMAN_WAIT_PLAYER
		TempValue0 = Object.NewXBoundary1Value
		if Screen.CenterX > 160
			TempValue0 += 180
		else
			TempValue0 += 160
		end if
		TempValue0 <<= 16

		if Player.XPos > TempValue0
			TempValue0 = Object.YPos
			TempValue0 += 12582912

			if Player.YPos > TempValue0
				Stage.NewXBoundary1 = Object.NewXBoundary1Value
				Stage.YBoundary2 = Player.YPos
				Stage.YBoundary2 >>= 16
				Stage.YBoundary2 += 160

				if Stage.YBoundary2 < 256
					Stage.YBoundary2 = 256
				end if

				Object.State = R1_EGGMAN_SET_BOUNDARIES
			end if

		end if
		break

	case R1_EGGMAN_BODY_CONTROLLER
		if Object.Timer > 0
			Object.Timer--
		end if
		if BossBody.State > 2
			if Object.DamageTimer > 0
				Object.DamageTimer--
			else
				Object.Animation = ANI_IDLE
			end if
		end if

		ArrayPos0 = Object.BodyEntity
		GetBit(TempValue0, Body[ArrayPos0].BitHolder, 0)
		if TempValue0 == KEY_EVENT_TRUE
			if Body[ArrayPos0].State == BOSSBODY_PREPARE_CHARGING
				// Object +15 is a blank object used to hold values
				Object.YPos = Object[+15].Value0
				BossBody.YPos = Object[+15].Value1
				BossLegJointR.YPos = Object[+15].Value2
				BossLegR.YVelocity = Object[+15].Value3
				BossLegR.YPos = Object[+15].YPos
				BossFootR.YVelocity = Object[+15].Value4
				BossLegJointL.YPos = Object[+15].Value5
				BossLegL.YVelocity = Object[+15].Value6
				BossFootL.YVelocity = Object[+15].Value7
				GetBit(TempValue0, Body[ArrayPos0].BitHolder, 6)
				if TempValue0 == LEFT_LEG_ACTIVE
					SetBit(Body[ArrayPos0].BitHolder, 6, RIGHT_LEG_ACTIVE)
				else
					SetBit(Body[ArrayPos0].BitHolder, 6, LEFT_LEG_ACTIVE)
				end if
			end if

			SetBit(Body[ArrayPos0].BitHolder, 0, KEY_EVENT_FALSE)
			if Object.Oscillation == 2
				SetBit(Object.BitHolder, 3, 1)
			end if

			SetBit(Body[ArrayPos0].BitHolder, 1, MOVE_FORWARD)
			Object.Oscillation += 2
			CallFunction(R1_EGGMAN_Oscillation)
			if TempValue0 < 0
				Object.Oscillation = 6
			end if

			CallFunction(R1_EGGMAN_Oscillation)
			if TempValue0 == 6	// Go back
				// Object +15 is a blank object used to hold values
				Object[+15].Value0 = Object.YPos
				Object[+15].Value1 = BossBody.YPos
				Object[+15].Value2 = BossLegJointR.YPos
				Object[+15].Value3 = BossLegR.YVelocity
				Object[+15].YPos = BossLegR.YPos
				Object[+15].Value4 = BossFootR.YVelocity
				Object[+15].Value5 = BossLegJointL.YPos
				Object[+15].Value6 = BossLegL.YVelocity
				Object[+15].Value7 = BossFootL.YVelocity
			end if

			if Body[ArrayPos0].State == BOSSBODY_MOVE_BACKWARDS
				if TempValue0 == 4
					GetBit(TempValue1, Body[ArrayPos0].BitHolder, 6)
					if TempValue1 == LEFT_LEG_ACTIVE
						SetBit(Body[ArrayPos0].BitHolder, 6, RIGHT_LEG_ACTIVE)
					else
						SetBit(Body[ArrayPos0].BitHolder, 6, LEFT_LEG_ACTIVE)
					end if
				end if
			end if
			
			Body[ArrayPos0].State = TempValue0
			Object.Oscillation++
			CallFunction(R1_EGGMAN_Oscillation)
			Object.Oscillation--
			Body[ArrayPos0].ChargeStart = TempValue0
		end if
		break

	case R1_EGGMAN_DEFEATED
		if Object.Timer < 160
			Object.Timer++
			TempValue0 = Object.Timer
			TempValue0 &= 3
			if TempValue0 == 3
				Rand(TempValue0, 128)
				TempValue0 -= 64
				TempValue0 <<= 16
				TempValue0 += Object.XPos
				Rand(TempValue1, 64)
				TempValue1 -= 32
				TempValue1 <<= 16
				TempValue1 += Object.YPos
				CreateTempObject(TypeName[Explosion], 0, TempValue0, TempValue1)
				PlaySfx(22, 0)
			end if

			if Object.Timer == 94
				TempValue0 = Object.EntityNo
				TempValue0++
				Player.Score += 1000
				ResetObjectEntity(TempValue0, TypeName[EggmanJetpack], 0, Object.XPos, Object.YPos)
				Object.Animation = ANI_DESTROYED
#platform: Use_Haptics
				HapticEffect(76, 0, 0, 0)
#endplatform
			end if
		end if
		break

	case R1_EGGMAN_BLANK
		break

	case R1_EGGMAN_RESTORE_BOUNDS
		if Object.Timer < 120
			Music.Volume--
			Stage.YBoundary2 += 4
			Stage.XBoundary2 += 6
			Object.Timer++
		else
			Stage.YBoundary2 = Object.SavedYBoundary2
			Stage.XBoundary2 = Object.SavedXBoundary2
			PlayMusic(0)
			Object.Type = TypeName[Blank Object]
		end if
		break

	case R1_EGGMAN_SET_BOUNDARIES
		if Stage.YBoundary2 > BossBody.iYPos
			Stage.YBoundary2 -= 2
			Stage.YBoundary1 += 2
		else
			Stage.YBoundary2 = BossBody.iYPos
			TempValue0 = BossBody.iYPos
			TempValue0 -= Screen.YSize
			if TempValue0 < 0
				TempValue0 = 0
			end if
			Stage.NewYBoundary1 = TempValue0
			Object.Timer = 0
			Object.State = R1_EGGMAN_SPAWN_HVC
		end if
		break

	case R1_EGGMAN_SPAWN_HVC
		if Object.Timer < 120
			if Object.Timer < 100
				Music.Volume--
			else
				if Object.Timer == 100
					PlayMusic(4)
				end if
			end if
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = R1_EGGMAN_BODY_CONTROLLER
			Object.Animation = ANI_LAUGH
			ArrayPos0 = Object.EntityNo
			ArrayPos0++
			ResetObjectEntity(ArrayPos0, TypeName[BossBody], 0, Object.XPos, Object.YPos)
			Body[ArrayPos0].Priority = PRIORITY_ACTIVE

			Body[ArrayPos0].FaceEntity = Object.EntityNo

			Body[ArrayPos0].LegJointREntity = Object.EntityNo
			Body[ArrayPos0].LegJointREntity += 2

			Body[ArrayPos0].LegJointLEntity = Object.EntityNo
			Body[ArrayPos0].LegJointLEntity += 5
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLegJoint], 0, Object.XPos, Object.YPos)
			LegJointR[ArrayPos0].Priority = PRIORITY_ACTIVE

			LegJointR[ArrayPos0].BodyEntity = Object.EntityNo
			LegJointR[ArrayPos0].BodyEntity += 1

			LegJointR[ArrayPos0].LegREntity = Object.EntityNo
			LegJointR[ArrayPos0].LegREntity += 3
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLeg], 0, Object.XPos, Object.YPos)
			LegR[ArrayPos0].Priority = PRIORITY_ACTIVE

			LegR[ArrayPos0].LegJointREntity = Object.EntityNo
			LegR[ArrayPos0].LegJointREntity += 2

			LegR[ArrayPos0].FootREntity = Object.EntityNo
			LegR[ArrayPos0].FootREntity += 4
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossFoot], 0, Object.XPos, Object.YPos)
			FootR[ArrayPos0].Priority = PRIORITY_ACTIVE

			FootR[ArrayPos0].LegREntity = Object.EntityNo
			FootR[ArrayPos0].LegREntity += 3

			FootR[ArrayPos0].BodyEntity = Object.EntityNo
			FootR[ArrayPos0].BodyEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLegJoint], 0, Object.XPos, Object.YPos)
			LegJointL[ArrayPos0].Priority = PRIORITY_ACTIVE
			LegJointL[ArrayPos0].DrawOrder = 2

			LegJointL[ArrayPos0].BodyEntity = Object.EntityNo
			LegJointL[ArrayPos0].BodyEntity += 1

			LegJointL[ArrayPos0].LegLEntity = Object.EntityNo
			LegJointL[ArrayPos0].LegLEntity += 6
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLeg], 0, Object.XPos, Object.YPos)
			LegL[ArrayPos0].Priority = PRIORITY_ACTIVE
			LegL[ArrayPos0].DrawOrder = 2

			LegL[ArrayPos0].LegJointLEntity = Object.EntityNo
			LegL[ArrayPos0].LegJointLEntity += 5

			LegL[ArrayPos0].FootLEntity = Object.EntityNo
			LegL[ArrayPos0].FootLEntity += 7
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossFoot], 0, Object.XPos, Object.YPos)
			FootL[ArrayPos0].Priority = PRIORITY_ACTIVE
			FootL[ArrayPos0].DrawOrder = 2

			FootL[ArrayPos0].LegLEntity = Object.EntityNo
			FootL[ArrayPos0].LegLEntity += 6

			FootL[ArrayPos0].BodyEntity = Object.EntityNo
			FootL[ArrayPos0].BodyEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossShoulder], 0, Object.XPos, Object.YPos)
			Shoulder[ArrayPos0].Priority = PRIORITY_ACTIVE
			Shoulder[ArrayPos0].DrawOrder = 4

			Shoulder[ArrayPos0].FaceEntity = Object.EntityNo

			Shoulder[ArrayPos0].ArmJointLEntity = Object.EntityNo
			Shoulder[ArrayPos0].ArmJointLEntity += 9

			Shoulder[ArrayPos0].ArmJointREntity = Object.EntityNo
			Shoulder[ArrayPos0].ArmJointREntity += 12
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArmJoint], 0, Object.XPos, Object.YPos)
			ArmJointL[ArrayPos0].Priority = PRIORITY_ACTIVE

			ArmJointL[ArrayPos0].ShoulderEntity = Object.EntityNo
			ArmJointL[ArrayPos0].ShoulderEntity += 8

			ArmJointL[ArrayPos0].ArmLEntity = Object.EntityNo
			ArmJointL[ArrayPos0].ArmLEntity += 10
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArm], 0, Object.XPos, Object.YPos)
			Object[ArrayPos0].Priority = PRIORITY_ACTIVE

			ArmL[ArrayPos0].ArmJointLEntity = Object.EntityNo
			ArmL[ArrayPos0].ArmJointLEntity += 9

			ArmL[ArrayPos0].BumperLEntity = Object.EntityNo
			ArmL[ArrayPos0].BumperLEntity += 11
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossBumper], 0, Object.XPos, Object.YPos)
			BumperL[ArrayPos0].Priority = PRIORITY_ACTIVE

			BumperL[ArrayPos0].ArmLEntity = Object.EntityNo
			BumperL[ArrayPos0].ArmLEntity += 10

			BumperL[ArrayPos0].BodyEntity = Object.EntityNo
			BumperL[ArrayPos0].BodyEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArmJoint], 0, Object.XPos, Object.YPos)
			ArmJointR[ArrayPos0].Priority = PRIORITY_ACTIVE
			ArmJointR[ArrayPos0].DrawOrder = 2

			ArmJointR[ArrayPos0].ShoulderEntity = Object.EntityNo
			ArmJointR[ArrayPos0].ShoulderEntity += 8

			ArmJointR[ArrayPos0].ArmREntity = Object.EntityNo
			ArmJointR[ArrayPos0].ArmREntity += 13
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArm], 0, Object.XPos, Object.YPos)
			ArmR[ArrayPos0].Priority = PRIORITY_ACTIVE
			ArmR[ArrayPos0].DrawOrder = 2

			ArmR[ArrayPos0].ArmJointREntity = Object.EntityNo
			ArmR[ArrayPos0].ArmJointREntity += 12

			ArmR[ArrayPos0].BumperREntity = Object.EntityNo
			ArmR[ArrayPos0].BumperREntity += 14
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossBumper], 0, Object.XPos, Object.YPos)
			BumperR[ArrayPos0].Priority = PRIORITY_ACTIVE
			BumperR[ArrayPos0].DrawOrder = 2

			BumperR[ArrayPos0].ArmREntity = Object.EntityNo
			BumperR[ArrayPos0].ArmREntity += 13

			BumperR[ArrayPos0].BodyEntity = Object.EntityNo
			BumperR[ArrayPos0].BodyEntity += 1
			ArrayPos0++
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	TempValue0 = Player.CollisionRight
	TempValue0 <<= 16
	TempValue0 += Player.XPos
	
	TempValue1 = Stage.XBoundary2
	TempValue1 <<= 16
	if TempValue0 > TempValue1
		Player.XVelocity = 0
		Player.Speed = 0
		Player.XPos = TempValue1

		TempValue0 = Player.CollisionRight
		TempValue0 <<= 16
		Player.XPos -= TempValue0
	end if

	if Object.State > R1_EGGMAN_WAIT_PLAYER
		if Player.Animation != ANI_HURT
			if Object.Hit < 3
				PlayerObjectCollision(C_TOUCH, -28, -28, 28, 28)
				if CheckResult == true
					if Player.Animation == ANI_JUMPING
						Player.XVelocity = Player.XPos
						Player.XVelocity -= Object.XPos
						Player.XVelocity >>= 3
						Player.Speed = Player.XVelocity

						Player.YVelocity = Player.YPos
						Player.YVelocity -= Object.YPos
						Player.YVelocity >>= 3

						if Object.Timer == 0
							if BossBody.State != 1
								PlaySfx(22, 0)
#platform: Use_Haptics
								HapticEffect(75, 0, 0, 0)
#endplatform
								switch Object.Hit
								case 0
									Object.Hit++
									Object.Timer = 30
									Object.Animation = ANI_HURT
									Object.DamageTimer = 120

									ArrayPos0 = Object.ShoulderEntity
									ArrayPos1 = Shoulder[ArrayPos0].ArmJointREntity
									Debris[ArrayPos1].State = BOSSARMJOINT_DESTROYED
									Debris[ArrayPos1].XOffSet = 65536	// 0x10000
									Debris[ArrayPos1].YOffSet = 131072	// 0x20000

									ArrayPos0 = ArmJointR[ArrayPos1].ArmREntity
									Debris[ArrayPos0].State = BOSSARM_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = 65536	// 0x10000

									ArrayPos1 = ArmR[ArrayPos0].BumperREntity
									Debris[ArrayPos1].State = BOSSBUMPER_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -98304	// -0x18000
									break

								case 1
									Object.Hit++
									Object.Timer = 30
									Object.Animation = ANI_HURT
									Object.DamageTimer = 120

									ArrayPos0 = Object.ShoulderEntity
									ArrayPos1 = Shoulder[ArrayPos0].ArmJointLEntity
									Debris[ArrayPos1].State = BOSSARMJOINT_DESTROYED
									Debris[ArrayPos1].XOffSet = 65536	// 0x10000
									Debris[ArrayPos1].YOffSet = 131072	// 0x20000
									
									ArrayPos0 = ArmJointL[ArrayPos1].ArmLEntity
									Debris[ArrayPos0].State = BOSSARM_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = 65536	// 0x10000

									ArrayPos1 = ArmL[ArrayPos0].BumperLEntity
									Debris[ArrayPos1].State = BOSSBUMPER_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -98304	// -0x18000
									break

								case 2
									Object.Hit++
									Object.State = R1_EGGMAN_DEFEATED
									Object.Animation = ANI_HURT
									ArrayPos0 = Object.BodyEntity
									Body[ArrayPos0].State = BOSSBODY_DESTROYED

									ArrayPos0 = Object.ShoulderEntity
									Debris[ArrayPos0].State = BOSSSHOULDER_DESTROYED
									Debris[ArrayPos0].XOffSet = -65536	// -0x10000
									Debris[ArrayPos0].YOffSet = -131072	// -0x20000

									ArrayPos0 = Object.BodyEntity
									ArrayPos1 = Body[ArrayPos0].LegJointREntity
									Debris[ArrayPos1].State = BOSSLEGJOINT_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -163840	// -0x28000

									ArrayPos0 = LegJointR[ArrayPos1].LegREntity
									Debris[ArrayPos0].State = BOSSLEG_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = -131072	// -0x20000

									ArrayPos1 = LegR[ArrayPos0].FootREntity
									Debris[ArrayPos1].State = BOSSFOOT_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -143360	// -0x23000

									ArrayPos0 = Object.BodyEntity
									ArrayPos1 = Body[ArrayPos0].LegJointLEntity
									Debris[ArrayPos1].State = BOSSLEGJOINT_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -163840	// -0x28000

									ArrayPos0 = LegJointL[ArrayPos1].LegLEntity
									Debris[ArrayPos0].State = BOSSLEG_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = -131072	// -0x20000

									ArrayPos1 = LegL[ArrayPos0].FootLEntity
									Debris[ArrayPos1].State = BOSSFOOT_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -143360	// -0x23000
									break
								end switch
							end if
						end if
					else
						if Player.Invincible == 0
							Object.Animation = ANI_LAUGH
							Object.DamageTimer = 180
							Player.State = PlayerObject_Hurt
							if Player.XPos > Object.XPos
								Player.Speed = 131072	// 0x20000
							else
								Player.Speed = -131072	// -0x20000
							end if
						end if
					end if
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	switch Object.Animation
	case ANI_IDLE
		DrawSprite(0)
		break

	case ANI_LAUGH
		if Object.AnimationTimer < 8
			Object.AnimationTimer = 8
		else
			if Object.AnimationTimer > 23
				Object.AnimationTimer = 8
			end if
		end if
		Object.Frame = Object.AnimationTimer
		Object.Frame >>= 3
		Object.AnimationTimer++
		DrawSprite(Object.Frame)
		break

	case ANI_HURT
		if Object.AnimationTimer < 12
			Object.AnimationTimer = 12
		else
			if Object.AnimationTimer > 27
				Object.AnimationTimer = 12
			end if
		end if
		Object.Frame = Object.AnimationTimer
		Object.Frame >>= 2
		Object.AnimationTimer++
		DrawSprite(Object.Frame)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects3.gif")

	SpriteFrame(-28, -24, 40, 16, 206, 1)		// #0 - Eggman Idle

	SpriteFrame(-28, -24, 40, 16, 206, 18)		// #1 - Eggman Laugh Frame 0
	SpriteFrame(-28, -24, 40, 16, 206, 35)		// #2 - Eggman Laugh Frame 1

	SpriteFrame(-28, -40, 40, 32, 124, 1)		// #3 - Eggman Hurt Frame 0
	SpriteFrame(-28, -24, 40, 16, 206, 52)		// #4 - Eggman Hurt Frame 1
	SpriteFrame(-28, -40, 40, 32, 165, 1)		// #5 - Eggman Hurt Frame 2
	SpriteFrame(-28, -24, 40, 16, 206, 69)		// #6 - Eggman Hurt Frame 3
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
