//-------------Sonic CD Diagonal Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.OriginPos.X
#alias Object.Value2	:	Object.PlayerFloor.X
#alias Object.Value3	:	Object.OriginPos.Y
#alias Object.Value4	:	Object.PlayerFloor.Y
#alias Object.Value5	:	Object.Oscillation
#alias Object.Value6	:	Object.Amplitude

// Property Values
#alias 0				:	DIAGONAL_UR_DL
#alias 1				:	DIAGONAL_UL_DR	// in a map editor you will find this as property value 3


sub ObjectMain
	Object.Oscillation = Oscillation
	Object.Oscillation <<= 1
	if Object.PropertyValue == DIAGONAL_UL_DR
		Object.Oscillation += 256
	end if

	Cos(Object.PlayerFloor.X, Object.Oscillation)
	Object.PlayerFloor.X *= 6144	// 0x1800
	Object.PlayerFloor.X += Object.XPos
	Object.PlayerFloor.X &= 4294901760
	Object.PlayerFloor.X -= Object.OriginPos.X

	Cos(Object.PlayerFloor.Y, Object.Oscillation)
	FlipSign(Object.PlayerFloor.Y)
	Object.PlayerFloor.Y *= Object.Amplitude
	Object.PlayerFloor.Y += Object.YPos
	Object.PlayerFloor.Y &= 4294901760
	Object.PlayerFloor.Y -= Object.OriginPos.Y
end sub


sub ObjectPlayerInteraction
	TempValue0 = Object.XPos
	Object.XPos = Object.OriginPos.X

	TempValue1 = Object.YPos
	Object.YPos = Object.OriginPos.Y

	PlayerObjectCollision(C_PLATFORM, -32, -9, 32, 12)
	if CheckResult == true
		Player.XPos += Object.PlayerFloor.X
		Player.YPos += Object.PlayerFloor.Y
	end if

	Object.XPos = TempValue0
	Object.YPos = TempValue1
end sub


sub ObjectDraw
	Cos(Object.OriginPos.X, Object.Oscillation)
	Object.OriginPos.X *= 6144		// 0x1800
	Object.OriginPos.X += Object.XPos
	Object.OriginPos.X &= 4294901760

	Cos(Object.OriginPos.Y, Object.Oscillation)
	FlipSign(Object.OriginPos.Y)
	Object.OriginPos.Y *= Object.Amplitude
	Object.OriginPos.Y += Object.YPos
	Object.OriginPos.Y &= 4294901760
	
	DrawSpriteXY(0, Object.OriginPos.X, Object.OriginPos.Y)
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Platform

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[DiagPlatform]
			if Object[ArrayPos0].PropertyValue > 1
				Object[ArrayPos0].Amplitude = -6144		// -0x1800
				Object[ArrayPos0].PropertyValue -= 2
			else
				Object[ArrayPos0].Amplitude = 6144		// 0x1800
			end if
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Platform

	AddEditorVariable("startDir")
	SetActiveVariable("startDir")
	AddEnumVariable("Left, Down", 0)
	AddEnumVariable("Right, Down", 1)
	AddEnumVariable("Left, Up", 2)
	AddEnumVariable("Right, Up", 3)
end sub
