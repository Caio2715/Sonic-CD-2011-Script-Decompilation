//------------Sonic CD Kamemusi Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.FireTimer

// Bullet Aliases
#alias OBject.Value1	:	Bullet.XVelocity
#alias OBject.Value2	:	Bullet.YVelocity

// States
#alias 0				:	KAMEMUSI_MOVE_LEFT
#alias 1				:	KAMEMUSI_MOVE_RIGHT
#alias 2				:	KAMEMUSI_SHOOT

// CSides
#alias 0	:	CSIDE_FLOOR

// Property Values
#alias 0				:	NEW
#alias 1				:	OLD


sub ObjectMain
	if Object.PropertyValue == NEW
		switch Object.State
		case KAMEMUSI_MOVE_LEFT
			Object.Frame++
			Object.Frame %= 60

			Object.XPos -= 43690	// 0xAAAA

			Object.Timer--
			if Object.Timer == -192
				Object.State = KAMEMUSI_MOVE_RIGHT
				Object.Direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)	// Check if there's no more floor
			if CheckResult == false
				Object.State = KAMEMUSI_MOVE_RIGHT
				Object.Timer = -192
				Object.Direction = FACING_LEFT
			end if

			if Object.FireTimer > -1
				PlayerObjectCollision(C_TOUCH, -64, -64, 64, 64)
				if CheckResult == true
					Object.State = KAMEMUSI_SHOOT
					Object.DrawOrder = 4
				end if
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_MOVE_RIGHT
			Object.Frame++
			Object.Frame %= 60

			Object.XPos += 43690	// 0xAAAA

			Object.Timer++
			if Object.Timer == 192
				Object.State = KAMEMUSI_MOVE_LEFT
				Object.Direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)	// Check if there's no more floor
			if CheckResult == 0
				Object.State = KAMEMUSI_MOVE_LEFT
				Object.Timer = 192
				Object.Direction = FACING_RIGHT
			end if

			if Object.FireTimer > -1
				PlayerObjectCollision(C_TOUCH, -64, -64, 64, 64)
				if CheckResult == true
					Object.State = KAMEMUSI_SHOOT
					Object.DrawOrder = 4
				end if
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_SHOOT
			if Object.FireTimer == 60
				CreateTempObject(TypeName[Bullet], 0, Object.XPos, Object.YPos)
				Bullet[TempObjectPos].XVelocity = -131072	// -0x20000
				Bullet[TempObjectPos].YVelocity = -327680	// -0x50000
			end if

			if Object.FireTimer == 62
				CreateTempObject(TypeName[Bullet], 0, Object.XPos, Object.YPos)
				Bullet[TempObjectPos].XVelocity = 131072	// 0x20000
				Bullet[TempObjectPos].YVelocity = -327680	// -0x50000
				PlayStageSfx(1, 0)
			end if

			if Object.FireTimer == 64
				Object.State = Object.Direction
				Object.FireTimer = -120
				Object.DrawOrder = 3
			else
				Object.FireTimer++
			end if

			break
		end switch
	else
		switch Object.State
		case KAMEMUSI_MOVE_LEFT
			Object.Frame++
			Object.Frame %= 12

			Object.XPos -= 14336	// 0x3800

			Object.Timer--
			if Object.Timer == -384
				Object.State = KAMEMUSI_MOVE_RIGHT
				Object.Direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)
			if CheckResult == false
				Object.State = KAMEMUSI_MOVE_RIGHT
				Object.Timer = -384
				Object.Direction = FACING_LEFT
			end if

			if Object.FireTimer == 82
				Object.State = KAMEMUSI_SHOOT
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if

			break

		case KAMEMUSI_MOVE_RIGHT
			Object.Frame++
			Object.Frame %= 12

			Object.XPos += 14336	// 0x3800

			Object.Timer++
			if Object.Timer == 384
				Object.State = KAMEMUSI_MOVE_LEFT
				Object.Direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)
			if CheckResult == false
				Object.State = KAMEMUSI_MOVE_LEFT
				Object.Timer = 384
				Object.Direction = FACING_RIGHT
			end if

			if Object.FireTimer == 82
				Object.State = KAMEMUSI_SHOOT
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_SHOOT	// in his broken state, the shoot will fail
			if Object.FireTimer == 14
				Object.State = Object.Direction
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if
			break

		end switch
	end if
	CallFunction(StageSetup_CheckGoodFuture)	// Check if it should be a flower
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -16, -14, 16, 14)
	if CheckResult == true
		CallFunction(PlayerObject_BadnikBreak)
	end if
end sub


sub ObjectDraw
	if Object.PropertyValue == NEW
		if Object.State < KAMEMUSI_SHOOT
			TempValue0 = Object.Frame
			TempValue0 /= 30
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		else
			DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
		end if
	else
		if Object.State < KAMEMUSI_SHOOT
			TempValue0 = Object.Frame
			TempValue0 /= 6
			TempValue0 += 2
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		else
			DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")
	SpriteFrame(-16, -15, 32, 29, 223, 34)	// #0 - Kamemusi Movement Frame 0
	SpriteFrame(-16, -14, 32, 28, 223, 93)	// #1 - Kamemusi Movement Frame 1
	SpriteFrame(-16, -15, 32, 29, 223, 34)	// #2 - Kamemusi Movement Frame 2
	SpriteFrame(-16, -14, 32, 28, 223, 64)	// #3 - Kamemusi Movement Frame 3
	SpriteFrame(-16, -17, 32, 31, 223, 122)	// #4 - Kamemusi Shoot Position
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
