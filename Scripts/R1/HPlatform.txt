//------------Sonic CD Horz Platform Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.OnObject
#alias Object.Value1	:	Object.Timer
#alias Object.Value2	:	Object.XVelocity
#alias Object.Value3	:	Object.OriginPos.X
#alias Object.Value4	:	Object.YVelocity
#alias Object.Value5	:	Object.Oscillation

// Property Values
#alias 0				:	HPLATFORM_START_LEFT
#alias 1				:	HPLATFORM_START_RIGHT


sub ObjectMain
	Object.Oscillation = Oscillation
	Object.Oscillation <<= 1

	if Object.PropertyValue == HPLATFORM_START_RIGHT
		Object.Oscillation += 256
	end if
	
	Cos(Object.XVelocity, Object.Oscillation)
	Object.XVelocity *= 6144	// 0x1800
	Object.XVelocity += Object.XPos
	Object.XVelocity &= -65536	// -0x10000
	Object.XVelocity -= Object.OriginPos.X

	if Object.OnObject == true
		// Loss a bit of height when stepped
		if Object.Timer < 16
			Object.Timer++
			TempValue0 = Object.Timer
			TempValue0 &= 3
			if TempValue0 == 3
				Object.YVelocity = 65536	// 0x10000
			else
				Object.YVelocity = 0
			end if
		else
			Object.YVelocity = 0
		end if
	else
		// Restore original YPos
		if Object.Timer > 0
			Object.Timer--
			TempValue0 = Object.Timer
			TempValue0 &= 3
			if TempValue0 == 3
				Object.YVelocity = -65536	// -0x10000
			else
				Object.YVelocity = 0
			end if
		else
			Object.YVelocity = 0
		end if
	end if
	Object.OnObject = false
end sub


sub ObjectPlayerInteraction
	TempValue0 = Object.XPos
	Object.XPos = Object.OriginPos.X

	PlayerObjectCollision(C_PLATFORM, -32, -9, 32, 12)
	if CheckResult == true
		Object.OnObject = true
		Player.XPos += Object.XVelocity
		Player.YPos += Object.YVelocity
	end if

	Object.XPos = TempValue0
end sub


sub ObjectDraw
	Cos(Object.OriginPos.X, Object.Oscillation)
	Object.OriginPos.X *= 6144		// 0x1800
	Object.OriginPos.X += Object.XPos
	Object.OriginPos.X &= -65536	// -0x10000

	Object.YPos += Object.YVelocity
	DrawSpriteXY(0, Object.OriginPos.X, Object.YPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Horizontal Platform
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
