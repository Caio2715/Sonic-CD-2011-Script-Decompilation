//---------------Sonic CD Vert Platform Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.CollisionOffset.Y
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.Oscillation

// Property Values
#alias 0				:	VPLATFORM_START_TOP
#alias 1				:	VPLATFORM_START_BOTTOM

// Collision
#alias 1				:	FLOOR


sub ObjectMain
	Object.Oscillation = Oscillation
	Object.Oscillation <<= 1

	if Object.PropertyValue == VPLATFORM_START_BOTTOM
		Object.Oscillation += 256
	end if

	Sin(Object.YVelocity, Object.Oscillation)
	Object.YVelocity *= 6144	// 0x1800
	Object.YVelocity += Object.YPos
	Object.YVelocity &= 4294901760	// -0x10000
	Object.YVelocity -= Object.CollisionOffset.Y
end sub


sub ObjectPlayerInteraction
	TempValue0 = Object.YPos
	Object.YPos = Object.CollisionOffset.Y

	PlayerObjectCollision(C_PLATFORM, -32, -9, 32, 12)
	if CheckResult == FLOOR
		Player.YPos += Object.YVelocity
	end if

	Object.YPos = TempValue0
end sub


sub ObjectDraw
	Sin(Object.CollisionOffset.Y, Object.Oscillation)
	Object.CollisionOffset.Y *= 6144	// 0x1800
	Object.CollisionOffset.Y += Object.YPos
	Object.CollisionOffset.Y &= 4294901760	// -0x10000

	DrawSpriteXY(0, Object.XPos, Object.CollisionOffset.Y)
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Vertical Platform
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // startDir
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // startDir
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 101, 109)	// #0 - Vertical Platform

	AddEditorVariable("startDir")
	SetActiveVariable("startDir")
	AddEnumVariable("Up", 0)
	AddEnumVariable("Down", 1)
end sub
