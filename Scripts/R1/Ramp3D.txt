//------------------Sonic CD 3D Ramp Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1		:	Player.OriginPos.X
#alias Object[+3].Value0	:	StarBush1.OriginPos.X
#alias Object[+3].XPos		:	StarBush1.XPos
#alias Object[+4].Value0	:	StarBush2.OriginPos.X
#alias Object[+4].XPos		:	StarBush2.XPos
#alias Object[+5].Value0	:	BoostSpinner.OriginPos.X
#alias Object[+5].XPos		:	BoostSpinner.XPos

// States
#alias 0					:	RAMP3D_RUN
#alias 1					:	RAMP3D_EXIT_UNUSED	// a copy of Exit Boost
#alias 2					:	RAMP3D_EXIT
#alias 3					:	RAMP3D_SET_YBOUND	// this doesn't really have anything to do with this object
#alias 4					:	RAMP3D_ENTER
#alias 5					:	RAMP3D_DROP
#alias 6					:	RAMP3D_SET_YBOUND2	// this doesn't really have anything to do with this object
#alias 7					:	RAMP3D_RUN_FROM_TOP
#alias 8					:	RAMP3D_EXIT_BOOST

// Gravity
#alias 0					:	GRAVITY_GROUND
#alias 1					:	GRAVITY_AIR

// Priority
#alias 0					:	PRIORITY_ACTIVE_BOUNDS
#alias 1					:	PRIORITY_ACTIVE


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case RAMP3D_RUN
		if Player.Gravity == GRAVITY_GROUND

			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true

				if Player.State != PlayerObject_Blank
					Player.State = PlayerObject_Blank

					Player.TrackScroll = true
					Player.TileCollisions = false

					Player.Speed = 0
					Player.XVelocity = 0
					Player.YVelocity = -786432	// -0xC0000

					Player.AnimationSpeed = 0
				end if
			end if
		end if
		break

	case RAMP3D_EXIT_UNUSED
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			Player.State = PlayerObject_HandleAir
			
			Player.TileCollisions = true

			Player.Speed = 524288	// 0x80000
			Player.XVelocity = 524288	// 0x80000
			Player.YVelocity = -917504	// -0xE0000

			Player.Gravity = GRAVITY_AIR
			
			Player.Rotation = 384

			Stage.NewYBoundary2 = 1280
			Stage.NewXBoundary1 = 2080

			if Player.Animation == ANI_ROLL3D
				Player.Animation = ANI_JUMPING
			else
				if Player.Animation != ANI_JUMPING
					Player.Animation = ANI_WALKING
				end if
			end if

			Object[15].Type = TypeName[BackgroundFX]
			Object[15].Priority = PRIORITY_ACTIVE
			Object[15].DrawOrder = 0
			ArrayPos0 = 0
			while ArrayPos0 < 2
				HParallax[ArrayPos0].ScrollPos = 0
				ArrayPos0++
			loop
		end if
		break

	case RAMP3D_EXIT
		if Player.State == PlayerObject_Blank
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				Player.State = PlayerObject_HandleAir

				Player.TileCollisions = true

				Player.Speed = 262144	// 0x40000
				Player.XVelocity = 262144	// 0x40000
				Player.YVelocity = -786432	// 0xC0000

				Player.Gravity = GRAVITY_AIR

				Player.Rotation = 384

				Stage.NewYBoundary2 = 1280

				if Player.Animation == ANI_ROLL3D
					Player.Animation = ANI_JUMPING
				else
					if Player.Animation != ANI_JUMPING
						Player.Animation = ANI_WALKING
					end if
				end if
			end if
		end if
		break

	case RAMP3D_SET_YBOUND
		if Player.XPos > Object.XPos
			Stage.YBoundary2 = 1024
		else
			Stage.YBoundary2 = 1280
		end if
		break

	case RAMP3D_ENTER
		if Player.XPos > Object.XPos

			if Player.Gravity == GRAVITY_GROUND

				if Player.State != PlayerObject_Blank

					if Player.State == PlayerObject_HandleRolling

						if Player.Angle > 216
							Player.Animation = ANI_JUMPING
						else
							if Player.Angle == 0
								Player.Animation = ANI_JUMPING
							else
								Player.Animation = ANI_ROLL3D
							end if
						end if
					else

						if Player.Speed != 0
							Player.State = PlayerObject_Handle3DRampAnims
						end if
					end if
				end if
			end if

			StarBush1.OriginPos.X  = Player.XPos
			StarBush1.OriginPos.X -= Object.XPos

			BoostSpinner.OriginPos.X  = StarBush1.OriginPos.X
			BoostSpinner.OriginPos.X /= 3

			StarBush1.OriginPos.X >>= 1

			StarBush2.OriginPos.X  = StarBush1.OriginPos.X

			StarBush1.OriginPos.X += StarBush1.XPos

			StarBush2.OriginPos.X += StarBush2.XPos

			BoostSpinner.OriginPos.X += BoostSpinner.XPos

			TempValue0  = Object.YPos
			TempValue0 -= Player.YPos
			if TempValue0 < 14680064	// 0xE00000
			
				TempValue0 = Player.XPos
				TempValue0 -= Player.OriginPos.X
				HParallax[0].ScrollPos += TempValue0

				ArrayPos0 = 13
				TempValue2 = 128
				while ArrayPos0 < 141
					TempValue1 = TempValue0
					TempValue1 *= TempValue2
					TempValue1 >>= 7
					HParallax[ArrayPos0].ScrollPos += TempValue1

					ArrayPos0++

					TempValue2--
				loop

				TempValue0 <<= 1
				HParallax[1].ScrollPos += TempValue0
			end if
		else
			StarBush1.OriginPos.X = StarBush1.XPos

			StarBush2.OriginPos.X = StarBush2.XPos
			
			BoostSpinner.OriginPos.X = BoostSpinner.XPos

			if Player.Gravity == GRAVITY_GROUND

				if Player.State == PlayerObject_Handle3DRampAnims
					Player.State = PlayerObject_HandleGround
				end if

			end if

		end if
		Player.OriginPos.X = Player.XPos
		break
		
	case RAMP3D_DROP
		if Player.State == PlayerObject_HandleAir

			PlayerObjectCollision(C_TOUCH, -16, -128, 16, 16)
			if CheckResult == true
				Player.Animation = ANI_DROPPING
				Player.OriginPos.X = Player.XPos
			end if

			if Player.Animation == ANI_DROPPING
				Player.Speed = 0
				Player.XVelocity = 0
				Player.XPos = Player.OriginPos.X
			end if

		end if
		break

	case RAMP3D_SET_YBOUND2
		if Player.YPos < Object.YPos

			if Player.XPos > Object.XPos
				Stage.YBoundary2 = 792
			else
				Stage.YBoundary2 = 1024
			end if

		end if
		break

	case RAMP3D_RUN_FROM_TOP
		if Player.XPos > Object.XPos

			Stage.YBoundary2 = 1024
			if Player.Gravity == GRAVITY_GROUND

				if Player.State == PlayerObject_Handle3DRampAnims
					Player.State = PlayerObject_HandleGround
				end if

			end if

		else
			Stage.YBoundary2 = 792
		end if
		break

	case RAMP3D_EXIT_BOOST
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			Player.Timer = 0

			Player.State = PlayerObject_HandleAir

			Player.TileCollisions = true

			Player.Speed = 524288	// 0x80000
			Player.XVelocity = 524288	// 0x80000
			Player.YVelocity = -917504	// -0xE0000

			Player.Gravity = GRAVITY_AIR

			Player.Rotation = 384

			Stage.NewYBoundary2 = 1280
			Stage.NewXBoundary1 = 2080

			if Player.Animation == ANI_ROLL3D
				Player.Animation = ANI_JUMPING
			else
				if Player.Animation != ANI_JUMPING
					Player.Animation = ANI_WALKING
				end if
			end if

			Object[15].Type = TypeName[BackgroundFX]
			Object[15].Priority = PRIORITY_ACTIVE
			Object[15].DrawOrder = 0
			ArrayPos0 = 0
			while ArrayPos0 < 2
				HParallax[ArrayPos0].ScrollPos = 0
				ArrayPos0++
			loop
		end if

		if Player.YVelocity < 0
			Player.ControlMode = -1
			Player.Left = false
			Player.Right = false
			Object.Priority = PRIORITY_ACTIVE
		else
			Player.ControlMode = 0
			Object.Priority = PRIORITY_ACTIVE_BOUNDS
		end if
		
		break
	end switch
end sub


sub ObjectStartup
	if Player.XPos > 131072000	// 0x7D00000
		Stage.YBoundary2 = 1280
		Object[15].Type = TypeName[BackgroundFX]
		Object[15].Priority = PRIORITY_ACTIVE
		Object[15].DrawOrder = 0
	end if

	if Player.XPos > 262144000	// 0xFA00000
		Stage.YBoundary2 = 1024
	end if
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon


	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("3D Ramp Run", 0)
	AddEnumVariable("3D Ramp Start", 4)
end sub
